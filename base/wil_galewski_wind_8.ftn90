!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer, 
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms 
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer 
!version 3 or (at your option) any later version that should be found at: 
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software; 
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), 
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
!**s/r wil_galewski_wind_8 
!
#include "model_macros_f.h"
!
      function wil_galewski_wind_8 (evaluated_at_lat_8,kind)
!
      implicit none
!
      integer kind
      real*8 wil_galewski_wind_8,evaluated_at_lat_8
!
!author M.Tanguay 
!
!revision
! v4_04 - Tanguay M.       - initial version
! v4_04 - Tanguay M.       - Galewski's case
!
!object
!     Initialize wind in balanced state of Galewski's case (Tellus,2004)  
!
!     kind = 1 = u wind
!     kind = 2 = vorticity
!
!implicits
#include "wil_dcst.cdk"
!
!     ---------------------------------------------------------
      real*8 umax_8,lat0_8,lat1_8,emag_8,ratio_8,wind_wk_8,vort_wk_8, &
             part1_8,part2_8,part3_8,lat_8
!
      integer n_max 
      parameter( n_max = 100 )
!     ---------------------------------------------------------
!
      umax_8  = 80.d0
      lat0_8  = Wil_Dcst_pi_8/7.
      lat1_8  = Wil_Dcst_pi_8/2. - lat0_8
      emag_8  = exp(-4./(lat1_8-lat0_8)**2)
       lat_8  = evaluated_at_lat_8
!
      wind_wk_8 = 0.
      vort_wk_8 = 0.
!
      if (lat_8.gt.lat0_8.and.lat_8.lt.lat1_8) then
!
!         Uini is a barotropically unstable state (with jet at 45N)
!         ---------------------------------------------------------
          ratio_8   = (lat_8-lat0_8)*(lat_8-lat1_8)
          ratio_8   = 1./ratio_8
          wind_wk_8 = (umax_8/emag_8)*exp(ratio_8)
!
!         Vorini is the corresponding vorticity = -1/a cos(theta)*d(cos(theta)*uini)/d(theta)
!         -----------------------------------------------------------------------------------
          part1_8   = tan(lat_8)
          part3_8   = (lat_8-lat0_8)*(lat_8-lat1_8)
          part3_8   = part3_8**2
          part2_8   = (lat_8-lat0_8)+(lat_8-lat1_8)
          vort_wk_8 = (wind_wk_8/Wil_Dcst_rayt_8) * ( part1_8 + part2_8/part3_8 )
!
      endif
!
      if (kind.eq.1) wil_galewski_wind_8 = wind_wk_8 
      if (kind.eq.2) wil_galewski_wind_8 = vort_wk_8 
!
!     ---------------------------------------------------------
!
      return
      end
