!---------------------------------- LICENCE BEGIN -------------------------------
! GEM - Library of kernel routines for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This library is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------

!**function samelevel - 
!
#include "model_macros_f.h"
!
      logical function samelevel ( zam_8,zbm_8,lvm, zat_8,zbt_8,lvt, &
                                   analtopo,ni,nj, F_topo,Mminx,Mmaxx,Mminy,Mmaxy)
      implicit none
!
      integer ni,nj,lvm,lvt,Mminx,Mmaxx,Mminy,Mmaxy
      real analtopo(ni,nj),F_topo (Mminx:Mmaxx,Mminy:Mmaxy)
      real*8 zam_8(lvm),zbm_8(lvm),zat_8(lvt),zbt_8(lvt)
!
!author
!
!revision
! v3_30 - Desgagne          - Initial version
!
!object
!     Compare analysis levels to model levels
!
!arguments
!
!implicites
#include "glb_ld.cdk"
#include "lun.cdk"
#include "vtopo.cdk"
#include "type.cdk"
#include "ver.cdk"
!
!modules
!*
      integer i,j,err,errprdf,prdfsum
      real difsig,prdfsgz,prdf
      parameter (difsig = 1.e-5)
!
! ---------------------------------------------------------------------
!
      samelevel = .true.
!
      if (G_nk.ne.lvm-1) then
          if (Lun_debug_L) &
              write(Lun_out,*)  &
              'Number of analysis levels(incl diag)=',lvm
          if (Lun_out.gt.0) then
             write(lun_out,*) ' ******* WARNING ********'
             write(Lun_out,*) 'SAME GRID BUT NOT SAME NUMBER OF LEVELS'
          endif
          samelevel = .false.
          return
      endif
      do i=1,lvm-1
         if (abs(zam_8(i)-Ver_a_8%m(i)).gt. difsig .or. &
             abs(zbm_8(i)-Ver_b_8%m(i)).gt. difsig) then
                if (Lun_debug_L) then 
                 write(Lun_out,*) &
                 'level=',i,'zam_8,Ver_a_8%m=',zam_8(i),Ver_a_8%m(i)
                 write(Lun_out,*) &
                 'level=',i,'zbm_8,Ver_b_8%m=',zbm_8(i),Ver_b_8%m(i)
                endif
                if (Lun_out.gt.0) then
                    write(lun_out,*) ' ******* WARNING ********'
                    write(Lun_out,*) 'SAME GRID BUT NOT SAME MOMENTUM LEVELS'
                endif
                samelevel = .false.
                return
         endif
      enddo
      do i=1,lvt-1
         if (abs(zat_8(i)-Ver_a_8%t(i)).gt. difsig .or. &
             abs(zbt_8(i)-Ver_b_8%t(i)).gt. difsig) then
             if (Lun_debug_L) then 
                 write(Lun_out,*) &
                 'level=',i,'zat_8,Ver_a_8%t=',zat_8(i),Ver_a_8%t(i)
                 write(Lun_out,*) &
                 'level=',i,'zbt_8,Ver_b_8%t=',zbt_8(i),Ver_b_8%t(i)
             endif
             if (Lun_out.gt.0) then
                write(lun_out,*) ' ******* WARNING ********'
                write(Lun_out,*) 'SAME GRID BUT NOT SAME THERMO LEVELS'
             endif
             samelevel = .false.
             return
         endif
      enddo

!     Check if analysis and model have the same topography
      prdfsgz = 25.
      errprdf = 0
      do j= 1+pil_s, l_nj-pil_n
      do i= 1+pil_w, l_ni-pil_e
         prdf= abs( analtopo(i,j) - F_topo(i,j))
         if ( prdf .gt. prdfsgz ) errprdf = 1
      enddo
      enddo
      call rpn_comm_allreduce (errprdf,prdfsum,1,"MPI_INTEGER", &
                                            "MPI_SUM","grid",err)

      if ( prdfsum.ne.0 .and. .not. Vtopo_L) then
           if (Lun_out.gt.0) then
               write(lun_out,*) ' ******* WARNING ********'
               write(lun_out,*) &
                  'ANALYSIS AND MODEL HAVE THE SAME GRID & LEVELS'
               write(lun_out,*) &
              ' ...BUT THE TOPOGRAPHY IS NOT EQUIVALENT...'
           endif
           samelevel = .false.
      endif

      return
      end

