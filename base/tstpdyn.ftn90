!---------------------------------- LICENCE BEGIN -------------------------------
! GEM - Library of kernel routines for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This library is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------

!**s/r tstpdyn -  Performs a dynamical timestep of the model
!
#include "model_macros_f.h"
!
      subroutine tstpdyn ( F_fnitraj )
!
      implicit none
#include <arch_specific.hf>
!
      integer F_fnitraj
!
!author
!     Alain Patoine ( after version v1_93 of tstpdyn2 )
!
!revision
! v2_00 - Desgagne M.       - initial MPI version
! v2_10 - Tanguay M.        - store TRAJ for 4D-Var 
! v2_30 - Edouard S.        - introduce Ncn 
! v3_00 - Desgagne & Lee    - Lam configuration
! v3_03 - Tanguay M.        - Adjoint NoHyd configuration
! v3_10 - Corbeil & Desgagne & Lee - AIXport+Opti+OpenMP
! v3_20 - Tanguay M.        - Option of storing instead of redoing TRAJ
! v3_21 - Desgagne M.       - introduce new timing routines
! v4_04 - Tanguay M.        - Staggered version TL/AD
! v4_05 - Girard C.         - Added boundary condition for top piloting
! v4_40 - Tanguay M.        - Revision TL/AD
!
!object
!    
!arguments
!  Name        I/O                 Description
!----------------------------------------------------------------
! F_fnitraj     I         number of iterations to compute upstream 
!                         positions  
!----------------------------------------------------------------
!
!implicits
#include "gmm.hf"
#include "glb_ld.cdk"
#include "lun.cdk"
#include "cstv.cdk"
#include "schm.cdk"
#include "orh.cdk"
#include "sol.cdk"
#include "nl.cdk"
#include "lctl.cdk"
#include "step.cdk"
#include "p_geof.cdk" 
#include "vtopo.cdk"
       include "v4dg.inc"
!
!      common/xiln/iln
!
      type(gmm_metadata) :: meta2d
      integer iln, dim, dimt, dimb, err, gmmstat
      real dth
!
!*
!     ---------------------------------------------------------------
!
!C    rhs:	Compute rhs of the governing equations
!C    int:	Perform Semi-Lagrangian advection
!C    pre:	Compute non-linear Helmholtz problem (invariant part)
!C    nli:	Compute non-linear components of the r.h.s. of the 
!C              Helmholtz problem and linear and nonlinear components 
!C              of the r.h.s. of the Helmholtz equation
!C    solver:	Linear Helmholtz solver
!C    bac:	Backsubtitution
!     ---------------------------------------------------------------
!
      call timing_start ( 4, 'TSTPDYN  ')
      dth =  Cstv_dt_8/2.
!
      if (Lun_debug_L) write(Lun_out,1000)
!
!     ---------------------------------------------------------------
!
!     Store TRAJ predictive variables
!     -------------------------------
      call v4d_rwtraj (2) 
!
      if ( Orh_icn .eq. 1 ) then
!     
!     Compute RHS
!     
         call tmg_start0 ( 3, 'RHS      ' )
         call rhs ()
         call tmg_stop0 (3)

      endif
!
!     Store TRAJ Positions TH
!     -----------------------
      call v4d_rwtraj (3) 
!
!     Compute the average of the t1 and t0 winds (uth,vth,zdth)
!     
      call tmg_start0(4, 'ADW      ')
      call wndth ()
!
!     Store TRAJ Winds TH
!     -------------------
      call v4d_rwtraj (4) 
!     
!     SL Advection
!     
      call itf_adx_main (F_fnitraj)
      call tmg_stop0(4)
!
      call tmg_start0(5, 'PRE      ')
!
      if ( Orh_crank_L .and. Orh_icn.eq.1 ) then
         if (Vtopo_L .and. (Lctl_step .ge. Vtopo_start)) then
            gmmstat = gmm_get(gmmk_fis0_s,fis0,meta2d)
            call var_topo2 (fis0, real(Lctl_step), LDIST_DIM)
            call rpn_comm_xch_halo (fis0,LDIST_DIM,l_ni,l_nj,1,&
                    G_halox,G_haloy,G_periodx,G_periody,l_ni,0)
         endif
      endif
!
      call pre ()
      call tmg_stop0 (5)
!
      if ( Lun_debug_L ) write (Lun_out,1005) Schm_itnlh
!
      dim = (l_maxx-l_minx+1)*(l_maxy-l_miny+1)*l_nk
      dimb= (l_maxx-l_minx+1)*(l_maxy-l_miny+1)
      dimt= (l_maxx-l_minx+1)*(l_maxy-l_miny+1)*(l_nk+1)
      call hpalloc (nl_u_, dim, err,1)
      call hpalloc (nl_v_, dim, err,1)
      call hpalloc (nl_t_, dimt,err,1) 
      call hpalloc (nl_c_, dim, err,1)
      call hpalloc (nl_f_, dimt,err,1)
      call hpalloc (nl_b_, dimb,err,1)
      nl_w_ = 0
      if (.not.Schm_hydro_L) call hpalloc (nl_w_, dimt, err,1)
!
      do 100 iln=1,Schm_itnlh
!
!        Store TRAJ modified by BAC at previous Orh_icn
!        ----------------------------------------------
         if ( Orh_icn.ne.1.and.iln.eq.1 ) call v4d_rwtraj (7) 
!
         call tmg_start0 ( 6, 'NLI      ' )
         call nli ()
         call tmg_stop0 (6)
!         
         call tmg_start0 ( 7, 'SOL      ' )
         call sol_main (iln)
         call tmg_stop0 (7)
!
!        Store TRAJ requested to build BAC_TR 
!        ------------------------------------
         if ( V4dg_conf.ne.0 .and. V4dg_oktr_L ) V4dg_iln = iln
         call v4d_rwtraj (8) 
!
         call tmg_start0 ( 8, 'BAC      ' )
         call bac ( iln, Schm_itnlh )
         call tmg_stop0 (8)
!
!        Store TRAJ requested during BAC_AD 
!        ----------------------------------
         call v4d_rwtraj (9) 
!
 100  continue
!
      call hpdeallc(nl_u_, err,1)
      call hpdeallc(nl_v_, err,1)
      call hpdeallc(nl_t_, err,1)
      call hpdeallc(nl_c_, err,1)
      call hpdeallc(nl_f_, err,1)
      call hpdeallc(nl_b_, err,1)
      if (.not.Schm_hydro_L) call hpdeallc(nl_w_, err,1)
!
      call timing_stop ( 4 )
!
!     ---------------------------------------------------------------
!
 1000 format( &
       3X,'PERFORM A DYNAMICAL STEP: (S/R TSTPDYN)', &
      /3X,'========================================',/)
 1005 format( &
       3X,'ITERATING SCHM_ITNLH=',I3,' TIMES TO SOLVE NON-LINEAR ', &
          'HELMHOLTZ PROBLEM')
      return
      end
