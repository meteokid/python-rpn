!---------------------------------- LICENCE BEGIN -------------------------------
! GEM - Library of kernel routines for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This library is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------

!**s/r init_component
!

      subroutine init_component (F_component_S)
      implicit none
!
      character*(*) F_component_S
#include <arch_specific.hf>
!
!author   Michel Desgagne -- Spring 2013 --
!
!revision
! v4_50 - M. Desgagne       - Initial version
!
#include "component.cdk"
#include "grd.cdk"
#include "dcst.cdk"
#include "cst_lis.cdk"
#include "lun.cdk"
#include "path.cdk"
#include "ptopo.cdk"
#include "version.cdk"
#include <clib_interface.cdk>
!
      logical, external :: set_dcst_8
      integer, external :: rpn_comm_init_multi_level, RPN_COMM_comm, pe_zero_topo, model_timeout_alarm
      external init_ndoms
!
      character(len=50 ) :: DSTP,dummy,name_S,arch_S,compil_S
      character(len=256) :: my_dir
      logical :: is_official_L
      integer :: ierr,mydomain,ngrids
!
!--------------------------------------------------------------------
!
      ierr= model_timeout_alarm(600)
      call rpn_comm_mydomain (init_ndoms, mydomain)

      write(my_dir,'(a,i4.4)') 'cfg_',mydomain

      ierr = clib_getenv ('TASK_WORK'  ,Path_work_S  )
      ierr = clib_getenv ('TASK_INPUT' ,Path_input_S )
      ierr = clib_getenv ('TASK_OUTPUT',Path_output_S)

      Path_input_S  = trim(Path_input_S ) // '/' // trim(my_dir)
      Path_work_S   = trim(Path_work_S  ) // '/' // trim(my_dir)
      Path_output_S = trim(Path_output_S) // '/' // trim(my_dir)

      ierr = clib_chdir (trim(Path_work_S))

      COMPONENT= F_component_S

      ngrids=1
      if (Grd_yinyang_L) ngrids=2

      Ptopo_couleur= RPN_COMM_init_multi_level (pe_zero_topo, Ptopo_myproc,Ptopo_numproc, &
                                                Ptopo_npex,Ptopo_npey,Grd_ndomains,ngrids )

      if (Grd_yinyang_L) then
         Ptopo_intracomm = RPN_COMM_comm ('GRID')
         Ptopo_intercomm = RPN_COMM_comm ('GRIDPEERS')
         call RPN_COMM_size ('MULTIGRID',Ptopo_world_numproc,ierr)
         call RPN_COMM_rank ('MULTIGRID',Ptopo_world_myproc ,ierr)

         if (Ptopo_couleur.eq.0) Grd_yinyang_S = 'YIN'
         if (Ptopo_couleur.eq.1) Grd_yinyang_S = 'YAN'
         ierr= clib_chdir(trim(Grd_yinyang_S))
	 Ptopo_ncolors = 2
      else
         Ptopo_couleur = 0
         Ptopo_ncolors = 1
      endif

      call msg_set_can_write (Ptopo_myproc == 0)

      call pe_all_topo

      ierr = 0
      if ( .not. set_dcst_8 ( Dcst_cpd_8,liste_S,cnbre, &
                              Lun_out,Ptopo_numproc ) ) ierr=-1
      call handle_error (ierr,'e_gemnml','set_dcst_8')

      call gemtim4 ( Lun_out, 'END OF e_initntr', .false. )
!
!--------------------------------------------------------------------
!
      return
      end

