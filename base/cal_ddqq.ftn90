!---------------------------------- LICENCE BEGIN -------------------------------
! GEM - Library of kernel routines for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This library is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------

!** s/r cal_ddqq - Computes horizontal divergence of wind-like fields 
#include "model_macros_f.h"

      subroutine cal_ddqq ( F_div,F_vor,F_qr, F_uu,F_vv         , &
                            F_filtdd,F_coefdd, F_filtqq,F_coefqq, &
                            Minx,Maxx,Miny,Maxy,Nk )
      implicit none
#include <arch_specific.hf>

      integer  F_filtdd,F_filtqq, Minx,Maxx,Miny,Maxy,Nk
      real     F_div(Minx:Maxx,Miny:Maxy,Nk), &
               F_vor(Minx:Maxx,Miny:Maxy,Nk), &
               F_qr (Minx:Maxx,Miny:Maxy,Nk), &
               F_uu (Minx:Maxx,Miny:Maxy,Nk), &
               F_vv (Minx:Maxx,Miny:Maxy,Nk), F_coefdd,F_coefqq
!author
!    Michel Desgagne   - spring 2014
!
!revision
! v4_70 - Desgagne M.       - initial version
!
!arguments
!  Name        I/O                 Description
!----------------------------------------------------------------
! F_div         O        Resulted divergence on G-grid
! F_uu          I        wind-like field on U-grid
! F_vv          I        wind-like field on V-grid
!----------------------------------------------------------------

#include "glb_ld.cdk"
#include "geomn.cdk"
#include "geomg.cdk"
#include "dcst.cdk"

      integer i, j, k, i0, in, j0, jn
      real deg2rad
!     __________________________________________________________________
!
      F_div(1,:,:) = 0. ; F_div(l_ni,:,:)= 0.
      F_div(:,1,:) = 0. ; F_div(:,l_nj,:)= 0.
      F_vor(1,:,:) = 0. ; F_vor(l_ni,:,:)= 0.
      F_vor(:,1,:) = 0. ; F_vor(:,l_nj,:)= 0.
      F_qr (1,:,:) = 0. ; F_qr (l_ni,:,:)= 0.
      F_qr (:,1,:) = 0. ; F_qr (:,l_nj,:)= 0.

      i0 = 1
      in = l_niu
      j0 = 1
      jn = l_njv
      if ((G_lam).and.(l_west)) i0 = 2
      if (l_south) j0 = 2

      do k = 1 , Nk
      do j = j0, jn
      do i = i0, in
         F_div(i,j,k) = geomg_invcy2_8(j) * ( &
         (F_uu(i,j,k)-F_uu(i-1,j,k))*geomg_invDX_8(i,j) * geomg_cy_8(j) &
        +(F_vv(i,j,k)-F_vv(i,j-1,k))*Geomg_invDY_8(  j) * geomg_cy_8(j) )
      end do
      end do
      end do

      do k =  1, Nk
      do j = j0, jn
      do i = i0, in
         F_vor(i,j,k) = geomg_invcyv2_8(j) * (  &
         (F_vv(i+1,j,k)-F_vv(i,j,k))*geomg_invDXu_8(i,j) * geomg_cy_8 (j)&
        -(F_uu(i,j+1,k)-F_uu(i,j,k))*geomg_invDYv_8(  j) * geomg_cyv_8(j))
      end do
      end do
      end do

      if (F_filtdd.gt.0) &
           call filter ( F_div, F_filtdd,F_coefdd,'G', .false.,&
                               l_minx,l_maxx,l_miny,l_maxy, Nk )
      if (F_filtqq.gt.0) &
           call filter ( F_vor, F_filtqq,F_coefqq,'G', .false.,&
                               l_minx,l_maxx,l_miny,l_maxy, Nk )

      deg2rad= acos( -1.0)/180.
      do k =  1, Nk
      do j = j0, jn
      do i = i0, in
         F_qr(i,j,k)= F_vor(i,j,k) + 2.0*Dcst_omega_8&
                      * sin(Geomn_latrx(i,j)*deg2rad)
      end do
      end do
      end do

!     __________________________________________________________________
!
      return
      end
