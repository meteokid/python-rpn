!---------------------------------- LICENCE BEGIN -------------------------------
! GEM - Library of kernel routines for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This library is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------

!  s/r set_zeta    - Generates A and B of the hybrid coordinate
!                    Also sets Z and other related vertical parameters.
!
#include "model_macros_f.h"
!
      subroutine set_zeta2( F_hybuser, Nk )
      use vGrid_Descriptors, only: vgrid_descriptor,vgd_new,vgd_get,vgd_levels, &
           VGD_OK,VGD_ERROR
      use vgrid_wb, only: vgrid_wb_put
   !     
      implicit none
#include <arch_specific.hf>
!
      integer Nk
      real, dimension(Nk) :: F_hybuser        !user-specified hybrid coordinate values
!
! authors
!      A. Plante & C. Girard - CMC - janvier 2008
!
! revision
!
! v4_00 - Plante & Girard   - Log-hydro-pressure coord on Charney-Phillips grid
! v4_4  - Plante - add standard pressure profils for physics.
!
! object
!    To return A, B parameters for momentum and thermodynamic levels
!    These levels are used for DYNAMICAL CALCULATIONS in the models !    Some are VIRTUAL levels
!
!    Also to return other parameters related to the vertical discretization
!
!         Z, dZ, 1/dZ, dBdZ, etc
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! n staggered MOMENTUM & THERMO LEVELS !!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!               ttttttttttttttttt Cstv_ztop_8
!
!               - - - - - - - - - Ver_z_8%m(1)
!
!               ================= Ver_z_8%t(1) = ( Ver_z_8%m(2) + Ver_z_8%m(1) ) / 2
!
!               - - - - - - - - - Ver_z_8%m(2)
!
!                      ...
!
!               - - - - - - - - - Ver_z_8%m(k)
!
!               ================= Ver_z_8%t(k) = ( Ver_z_8%m(k+1) + Ver_z_8%m(k) ) / 2
!
!               - - - - - - - - - Ver_z_8%m(k+1)
!
!                      ...
!
!               - - - - - - - - - Ver_z_8%m(n)
!
!               ================= Ver_a_8%t(n+1)
!
!               sssssssssssssssss Cstv_zsrf_8,Ver_z_8%t(n)
!
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! n staggered MOMENTUM & THERMO LAYERS !!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!
!               Cstv_ztop_8 ttttttttttttttttttttttttttttttttttttttttttt Cstv_ztop_8
!                                                     \
!               Ver_z_8%m(1)- - - - - - - - - - - - - - Ver_dz_8%m(1)=Ver_z_8%t(1)-Cstv_ztop_8
!                                          /          /
!   Ver_z_8%m(2)-Ver_z_8%m(1)=Ver_dz_8%t(1)    ======================== Ver_z_8%t(1)
!                                          \          \
!               Ver_z_8%m(2)- - - - - - - - - - - - - - Ver_dz_8%m(2)=Ver_z_8%t(2)-Ver_z_8%t(1)
!                                                     /
!                           =========================================== Ver_z_8%t(2)
!
!                                                     ...
!
!                           =========================================== Ver_z_8%t(k-1)
!                                                     \
!               Ver_z_8%m(k)- - - - - - - - - - - - - - Ver_dz_8%m(k)=Ver_z_8%t(k)-Ver_z_8%t(k-1)
!                                          /          /
! Ver_z_8%m(k+1)-Ver_z_8%m(k)=Ver_dz_8%t(k)    ======================== Ver_z_8%t(k)
!                                          \
!             Ver_z_8%m(k+1)- - - - - - - - - - - - - - - - - - - - - -
!
!                                                     ...
!
!                           =========================================== Ver_z_8%t(n-1)
!                                                   \  
!                                                    \ 
!                                                     \
!                           - - - - - - - - - - - - - - Ver_dz_8%m(n)=Ver_z_8%t(n)-Ver_z_8%t(n-1)
!                                          /          /
!    Cstv_zsrf_8-Ver_z_8%m(n)=Ver_dz_8%t(n)          /  
!                                          \        / 
!               Cstv_zsrf_8 sssssssssssssssssssssssssssssssssssssssssss Ver_z_8%t(n)
!
! arguments
! none
!
#include <WhiteBoard.hf>
#include "glb_ld.cdk"
#include "lun.cdk"
#include "dcst.cdk"
#include "cstv.cdk"
#include "grd.cdk"
#include "type.cdk"
#include "ver.cdk"
#include "schm.cdk"
#include "dimout.cdk"
#include "level.cdk"
!
      type(vgrid_descriptor) :: vcoord
      integer k,istat,options
      integer, dimension(:), pointer :: ip1m=>null(),ip1t=>null()
      real, dimension(:), pointer :: hybm_free=>null(),hybt_free=>null(),std_p_prof=>null()
      real :: hybm(0:G_nk+1),hybt(0:G_nk+2)
      real*8 zero, half, one, two
      parameter(zero=0.d0,half=0.5d0,one=1.d0,two=2.d0)
      character*8 dumc
!     __________________________________________________________________
!
      allocate(   Ver_hyb%m(G_nk+1),       Ver_hyb%t(G_nk+2), &
           Ver_std_p_prof%m(G_nk+1),Ver_std_p_prof%t(G_nk+2), &
                  Ver_ip1%m(G_nk+1),       Ver_ip1%t(G_nk+2), &
                  Ver_a_8%m(G_nk+1),       Ver_a_8%t(G_nk+2), &
                  Ver_b_8%m(G_nk+1),       Ver_b_8%t(G_nk+2), &
                  Ver_z_8%m(G_nk+1),       Ver_z_8%t(G_nk  ), &
                 Ver_dz_8%m(G_nk  ),      Ver_dz_8%t(G_nk  ), &
                Ver_idz_8%m(G_nk  ),     Ver_idz_8%t(G_nk  ), &
               Ver_dbdz_8%m(G_nk  ),    Ver_dbdz_8%t(G_nk  ), &
              Ver_fistr_8%m(G_nk  ),   Ver_fistr_8%t(G_nk  ), &
                 Ver_wp_8%m(G_nk  ),      Ver_wp_8%t(G_nk  ), &
                 Ver_wm_8%m(G_nk  ),      Ver_wm_8%t(G_nk  ), &
                                                  stat=istat)
      call check_alloc(istat,'set_zeta',1)
!
      Cstv_RTstr_8= Dcst_rgasd_8*Cstv_Tstr_8
      Cstv_pref_8 = 100000.d0
!
      Ver_code = 6
!
      call handle_error_l(Cstv_pref_8.eq.100000.d0, 'set_zeta','Cstv_pref_8 must equal 100000.d0')
!
      Cstv_Zsrf_8 = log(Cstv_pref_8)
      Cstv_Ztop_8 = log(Cstv_ptop_8)
!
      ! Construct vertical coordinate
      istat = vgd_new (vcoord, kind=5, version=Level_version, hyb=F_hybuser, &
                     rcoef1=Grd_rcoef(1),rcoef2=Grd_rcoef(2) , &
                     ptop_8=Cstv_ptop_8, pref_8=Cstv_pref_8,stdout_unit=Lun_out)
      call handle_error_l(istat==VGD_OK,'set_zeta','coordinate construction failed')

      ! Retrieve information required to fill model arrays
      Ver_a_8%m=>null();Ver_b_8%m=>null();Ver_a_8%t=>null();Ver_b_8%t=>null()
      if (vgd_get(vcoord,'CA_M - vertical A coefficient (m)',Ver_a_8%m  )/= VGD_OK) istat = VGD_ERROR
      if (vgd_get(vcoord,'CB_M - vertical B coefficient (m)',Ver_b_8%m  )/= VGD_OK) istat = VGD_ERROR
      if (vgd_get(vcoord,'CA_T - vertical A coefficient (t)',Ver_a_8%t  )/= VGD_OK) istat = VGD_ERROR
      if (vgd_get(vcoord,'CB_T - vertical B coefficient (t)',Ver_b_8%t  )/= VGD_OK) istat = VGD_ERROR
      if (vgd_get(vcoord,'VCDM - vertical coordinate (m)'   ,hybm_free  )/= VGD_OK) istat = VGD_ERROR
      if (vgd_get(vcoord,'VCDT - vertical coordinate (t)'   ,hybt_free  )/= VGD_OK) istat = VGD_ERROR
      if (vgd_get(vcoord,'VIPM - level ip1 list (m)'        ,ip1m       )/= VGD_OK) istat = VGD_ERROR
      if (vgd_get(vcoord,'VIPT - level ip1 list (t)'        ,ip1t       )/= VGD_OK) istat = VGD_ERROR
      call handle_error_l(istat==VGD_OK,'set_zeta','retrieving coordinate info')

      istat = vgd_levels(vcoord,ip1m,std_p_prof,100000.,in_log=.false.) 
      call handle_error_l(istat==VGD_OK,'set_zeta','problem getting standard pressure profile for m levels')
      Ver_std_p_prof%m=std_p_prof

      deallocate(std_p_prof)
      istat = vgd_levels(vcoord,ip1t,std_p_prof,100000.,in_log=.false.)
      call handle_error_l(istat==VGD_OK,'set_zeta','problem getting standard pressure profile for t levels')
      Ver_std_p_prof%t=std_p_prof

      ! Add outside level at top boundary and fill hybrid coordinate values
      hybm(1:size(hybm_free)) = hybm_free
      hybt(1:size(hybt_free)) = hybt_free
      hybm(0) = real(Cstv_ptop_8/Cstv_pref_8)
      hybt(0) = hybm(0)
      Ver_hyb%m(1:G_nk+1) = hybm(1:G_nk+1)
      Ver_hyb%t(1:G_nk+2) = hybt(1:G_nk+2)
      Ver_hyb_top = hybm(0)
      if (Schm_autobar_L) Ver_hyb%t(1) = hybt(0)
!
!     -------------
!     Now define Z:
!     -------------
!
      do k = 1, G_nk+1
         Ver_z_8%m(k) = Ver_a_8%m(k)
      enddo
!
      do k = 1, G_nk
         Ver_z_8%t(k) = Ver_a_8%t(k+1)
      enddo
!
!     --------------
!     Compute fistar
!     --------------
!     
      do k = 1, G_nk
         Ver_fistr_8%m(k)=-Cstv_RTstr_8*(Ver_z_8%m(k)-Cstv_Zsrf_8)
         Ver_fistr_8%t(k)=-Cstv_RTstr_8*(Ver_z_8%t(k)-Cstv_Zsrf_8)
      enddo
!
      
      if(Schm_adw_extrap_L)then
         ! Once the Ver_a_8%t are shifted, replace the above Ver_z_8%t(k) with Ver_a_8%t(k)
         ! and remove the following line
         Ver_z_8%t(G_nk)=Cstv_Zsrf_8
      endif

!     ----------------------
!     Compute dZ, 1/dZ, dBdZ
!     ----------------------
!
      Ver_dz_8%m(1)      = Ver_z_8%t(1)   - Cstv_Ztop_8
      if(Schm_adw_extrap_L)then
         do k=2,G_nk
            Ver_dz_8%m(k)   = Ver_z_8%t(k) - Ver_z_8%t(k-1)
         enddo
      else
         do k=2,G_nk-1
            Ver_dz_8%m(k)   = Ver_z_8%t(k) - Ver_z_8%t(k-1)
         enddo
         Ver_dz_8%m(G_nk)   = Cstv_Zsrf_8    - Ver_z_8%t(G_nk-1)         
      endif
!
      do k=1,G_nk
          Ver_idz_8%m(k) = one/Ver_dz_8%m(k)
           Ver_dz_8%t(k) = Ver_z_8%m(k+1) - Ver_z_8%m(k)
          Ver_idz_8%t(k) = one/Ver_dz_8%t(k)
         Ver_dbdz_8%t(k) = (Ver_b_8%m(k+1)-Ver_b_8%m(k))*Ver_idz_8%t(k)
      enddo
!
!     -------------------------------------------------------
!     Compute AVERGING WEIGHTS FROM THERMO TO MOMENTUM LEVELS 
!     -------------------------------------------------------
!
      do k=1,G_nk
         if(k.eq.G_nk) then
            Ver_wp_8%m(k) = Ver_dz_8%t(k)*Ver_idz_8%m(k)
         else
            Ver_wp_8%m(k) = Ver_dz_8%t(k)*half*Ver_idz_8%m(k)
         endif
            Ver_wm_8%m(k) = one-Ver_wp_8%m(k)
      enddo
!
          Ver_dbdz_8%m(1) = Ver_wp_8%m(1) * Ver_dbdz_8%t(1)  &
                          + Ver_wm_8%m(1) * (Ver_b_8%m(1)-0.d0)/(Ver_z_8%m(1)-Cstv_Ztop_8)
      do k=2,G_nk
          Ver_dbdz_8%m(k) = Ver_wp_8%m(k) * Ver_dbdz_8%t(k)  &
                          + Ver_wm_8%m(k) * Ver_dbdz_8%t(k-1)
      enddo
!
!     -------------------------------------------------------
!     Compute AVERGING WEIGHTS FROM MOMENTUM TO THERMO LEVELS 
!     -------------------------------------------------------
!
      do k=1,G_nk-1
         Ver_wp_8%t(k) = half
         Ver_wm_8%t(k) = half
      enddo
      Ver_wp_8%t(G_nk) = 1.d0
      Ver_wm_8%t(G_nk) = 0.d0
!
!     Encode Ver_ip1 for OUTPUT
!
      Ver_ip1%m(1:G_nk+1) = ip1m(1:G_nk+1)
      Ver_ip1%t(1:G_nk+1) = ip1t(1:G_nk+1)
      Ver_ip1%t(G_nk+2)   = Ver_ip1%m(G_nk+1)
!
!     ----------------------------------------------------------
!     Save vcoord and ip1m/t for output
!     ----------------------------------------------------------
      istat = vgrid_wb_put('ref-m',vcoord,ip1m)
      istat = vgrid_wb_put('ref-t',vcoord,ip1t)

      options = WB_REWRITE_NONE+WB_IS_LOCAL

      istat= wb_put('model/Vgrid/size-hybm',size(Ver_hyb%m),options)
      istat= wb_put('model/Vgrid/size-hybt',size(Ver_hyb%t),options)
      istat= wb_put('model/Vgrid/hybm'     ,Ver_hyb%m      ,options)
      istat= wb_put('model/Vgrid/hybt'     ,Ver_hyb%t      ,options)
      istat= wb_put('model/Vgrid/am'       ,Ver_a_8%m      ,options)
      istat= wb_put('model/Vgrid/bm'       ,Ver_b_8%m      ,options)
      istat= wb_put('model/Vgrid/at'       ,Ver_a_8%t      ,options)
      istat= wb_put('model/Vgrid/bt'       ,Ver_b_8%t      ,options)
      istat= wb_put('model/Vgrid/rcoef'    ,Grd_rcoef      ,options)
      istat= wb_put('model/Vgrid/ptop'     ,Cstv_ptop_8    ,options)
      istat= wb_put('model/Vgrid/pref'     ,Cstv_pref_8    ,options)
      istat= wb_put('model/Vgrid/vcode'    ,Ver_code       ,options)
!     __________________________________________________________________
!
      return
      end
