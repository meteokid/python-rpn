!---------------------------------- LICENCE BEGIN -------------------------------
! GEM - Library of kernel routines for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This library is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------

!  s/r set_zeta    - Generates A and B of the hybrid coordinate
!                    Also sets Z and other related vertical parameters.
!
      subroutine set_zeta2( F_hybuser, Nk )
      use vGrid_Descriptors, only: vgrid_descriptor,vgd_new,vgd_get,vgd_levels, &
           VGD_OK,VGD_ERROR,vgd_print
      use vgrid_wb, only: vgrid_wb_put
      implicit none
#include <arch_specific.hf>

      integer Nk
      real, dimension(Nk) :: F_hybuser        !user-specified hybrid coordinate values
!
! authors
!      A. Plante & C. Girard - CMC - janvier 2008
!
! revision
!
! v4_00 - Plante & Girard   - Log-hydro-pressure coord on Charney-Phillips grid
! v4_4  - Plante - add standard pressure profils for physics.
!
! object
!    To return A, B parameters for momentum and thermodynamic levels
!    These levels are used for DYNAMICAL CALCULATIONS in the models !    Some are VIRTUAL levels
!
!    Also to return other parameters related to the vertical discretization
!
!         Z, dZ, 1/dZ, dBdZ, etc
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! n staggered MOMENTUM & THERMO LEVELS !!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!               ttttttttttttttttt Cstv_ztop_8
!
!               - - - - - - - - - Ver_z_8%m(1)
!
!               ================= Ver_z_8%t(1) = ( Ver_z_8%m(2) + Ver_z_8%m(1) ) / 2
!
!               - - - - - - - - - Ver_z_8%m(2)
!
!                      ...
!
!               - - - - - - - - - Ver_z_8%m(k)
!
!               ================= Ver_z_8%t(k) = ( Ver_z_8%m(k+1) + Ver_z_8%m(k) ) / 2
!
!               - - - - - - - - - Ver_z_8%m(k+1)
!
!                      ...
!
!               - - - - - - - - - Ver_z_8%m(n)
!
!               ================= Ver_a_8%t(n) This level goes to the surface if no phy
!               
!               sssssssssssssssss Cstv_zsrf_8,Ver_z_8%t(n),Ver_a_8%t(n+1)
!
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! n staggered MOMENTUM & THERMO LAYERS !!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!
!               Cstv_ztop_8 ttttttttttttttttttttttttttttttttttttttttttt Cstv_ztop_8
!                                                     \
!               Ver_z_8%m(1)- - - - - - - - - - - - - - Ver_dz_8%m(1)=Ver_z_8%t(1)-Cstv_ztop_8
!                                          /          /
!   Ver_z_8%m(2)-Ver_z_8%m(1)=Ver_dz_8%t(1)    ======================== Ver_z_8%t(1)
!                                          \          \
!               Ver_z_8%m(2)- - - - - - - - - - - - - - Ver_dz_8%m(2)=Ver_z_8%t(2)-Ver_z_8%t(1)
!                                                     /
!                           =========================================== Ver_z_8%t(2)
!
!                                                     ...
!
!                           =========================================== Ver_z_8%t(k-1)
!                                                     \
!               Ver_z_8%m(k)- - - - - - - - - - - - - - Ver_dz_8%m(k)=Ver_z_8%t(k)-Ver_z_8%t(k-1)
!                                          /          /
! Ver_z_8%m(k+1)-Ver_z_8%m(k)=Ver_dz_8%t(k)    ======================== Ver_z_8%t(k)
!                                          \
!             Ver_z_8%m(k+1)- - - - - - - - - - - - - - - - - - - - - -
!
!                                                     ...
!
!                           =========================================== Ver_z_8%t(n-1)
!                                                   \  
!                                                    \ 
!                                                     \
!                           - - - - - - - - - - - - - - Ver_dz_8%m(n)=Ver_z_8%t(n)-Ver_z_8%t(n-1)
!                                          /          /
!    Cstv_zsrf_8-Ver_z_8%m(n)=Ver_dz_8%t(n)          /  
!                                          \        / 
!               Cstv_zsrf_8 sssssssssssssssssssssssssssssssssssssssssss Ver_z_8%t(n)
!
! arguments
! none
!
#include <WhiteBoard.hf>
#include "glb_ld.cdk"
#include "lun.cdk"
#include "dcst.cdk"
#include "cstv.cdk"
#include "grd.cdk"
#include "type.cdk"
#include "ver.cdk"
#include "schm.cdk"
#include "dimout.cdk"
#include "level.cdk"

      type(vgrid_descriptor) :: vcoord
      integer k,istat,options_readwrite,options_readonly
      integer, dimension(:), pointer :: wkpti
      real, dimension(:), pointer :: std_p_prof=>null(),wkpt
      real*8  wk_8
      real*8, parameter :: zero=0.d0, one=1.d0, half=0.5d0
      real*8, dimension(:), pointer :: wkpt8
!     __________________________________________________________________
!
      allocate(   Ver_hyb%m(G_nk+1),       Ver_hyb%t(G_nk+1), &                 
           Ver_std_p_prof%m(G_nk+1),Ver_std_p_prof%t(G_nk+1), &
                  Ver_ip1%m(G_nk+1),       Ver_ip1%t(G_nk+1), &
                  Ver_a_8%m(G_nk+1),       Ver_a_8%t(G_nk+1), &
                  Ver_b_8%m(G_nk+1),       Ver_b_8%t(G_nk+1), &
                  Ver_z_8%m(G_nk+1),       Ver_z_8%t(G_nk  ), &
                 Ver_dz_8%m(G_nk  ),      Ver_dz_8%t(G_nk  ), &
                Ver_idz_8%m(G_nk  ),     Ver_idz_8%t(G_nk  ), &
               Ver_dbdz_8%m(G_nk  ),    Ver_dbdz_8%t(G_nk  ), &
                 Ver_wp_8%m(G_nk  ),      Ver_wp_8%t(G_nk  ), &
                 Ver_wm_8%m(G_nk  ),      Ver_wm_8%t(G_nk  ), &
               Ver_Tstr_8%m(G_nk+1),    Ver_Tstr_8%t(G_nk  ), &
                 Ver_gama_8(G_nk  ),      Ver_epsi_8(G_nk  ), &
                Ver_FIstr_8(G_nk+1),       Ver_bzz_8(G_nk  ), &
                Ver_onezero(G_nk+1),   stat=istat)

      call check_alloc(istat,'set_zeta',1)

      Cstv_pref_8 = 100000.d0

      Ver_code = 6

      call handle_error_l(Cstv_pref_8.eq.100000.d0, 'set_zeta','Cstv_pref_8 must equal 100000.d0')

      ! Construct vertical coordinate
      istat = vgd_new ( vcoord, kind=5, version=Level_version, hyb=F_hybuser    , &
                        rcoef1=Grd_rcoef(1), rcoef2=Grd_rcoef(2)                , &
                        ptop_out_8=wk_8, pref_8=Cstv_pref_8, stdout_unit=Lun_out, &
                        dhm=0., dht=0. )
      Cstv_ptop_8=wk_8

      if (Lun_debug_L) istat = vgd_print(vcoord)
 
      Cstv_Zsrf_8 = log(Cstv_pref_8)
      Cstv_Ztop_8 = log(Cstv_ptop_8)

      call handle_error_l(istat==VGD_OK,'set_zeta','coordinate construction failed')

      ! Retrieve information required to fill model arrays
      nullify(wkpt,wkpti,wkpt8)
      if (vgd_get(vcoord,'CA_M - vertical A coefficient (m)',wkpt8) /= VGD_OK) istat = VGD_ERROR
      Ver_a_8%m = wkpt8(1:size(Ver_a_8%m)); deallocate(wkpt8); nullify(wkpt8)
      if (vgd_get(vcoord,'CB_M - vertical B coefficient (m)',wkpt8) /= VGD_OK) istat = VGD_ERROR
      Ver_b_8%m = wkpt8(1:size(Ver_b_8%m)); deallocate(wkpt8); nullify(wkpt8)
      if (vgd_get(vcoord,'CA_T - vertical A coefficient (t)',wkpt8) /= VGD_OK) istat = VGD_ERROR
      Ver_a_8%t = wkpt8(1:size(Ver_a_8%t)); deallocate(wkpt8); nullify(wkpt8)
      if (vgd_get(vcoord,'CB_T - vertical B coefficient (t)',wkpt8) /= VGD_OK) istat = VGD_ERROR
      Ver_b_8%t = wkpt8(1:size(Ver_b_8%t)); deallocate(wkpt8); nullify(wkpt8)
      if (vgd_get(vcoord,'VCDM - vertical coordinate (m)'   ,wkpt) /= VGD_OK) istat = VGD_ERROR
      Ver_hyb%m = wkpt(1:size(Ver_hyb%m)); deallocate(wkpt); nullify(wkpt)
      if (vgd_get(vcoord,'VCDT - vertical coordinate (t)'   ,wkpt) /= VGD_OK) istat = VGD_ERROR
      Ver_hyb%t = wkpt(1:size(Ver_hyb%t)); deallocate(wkpt); nullify(wkpt)
      if (vgd_get(vcoord,'VIPM - level ip1 list (m)'        ,wkpti) /= VGD_OK) istat = VGD_ERROR
      Ver_ip1%m = wkpti(1:size(Ver_ip1%m)); deallocate(wkpti); nullify(wkpti)
      if (vgd_get(vcoord,'VIPT - level ip1 list (t)'        ,wkpti) /= VGD_OK) istat = VGD_ERROR
      Ver_ip1%t = wkpti(1:size(Ver_ip1%t)); deallocate(wkpti); nullify(wkpti)

      call handle_error_l(istat==VGD_OK,'set_zeta','retrieving coordinate info')      
      istat = vgd_levels(vcoord,Ver_ip1%m,std_p_prof,100000.,in_log=.false.) 
      call handle_error_l(istat==VGD_OK,'set_zeta','problem getting standard pressure profile for m levels')
      Ver_std_p_prof%m=std_p_prof

      deallocate(std_p_prof)
      istat = vgd_levels(vcoord,Ver_ip1%t,std_p_prof,100000.,in_log=.false.)
      
      call handle_error_l(istat==VGD_OK,'set_zeta','problem getting standard pressure profile for t levels')
      Ver_std_p_prof%t=std_p_prof

!     -------------
!     Now define Z:
!     -------------

      do k = 1, G_nk+1
         Ver_z_8%m(k) = Ver_a_8%m(k)
      enddo

      do k = 1, G_nk
         Ver_z_8%t(k) = Ver_a_8%t(k)
      enddo      

      Ver_z_8%t(G_nk)=Cstv_Zsrf_8

!     ----------------------
!     Compute dZ, 1/dZ, dBdZ
!     ----------------------

      Ver_dz_8%m(1)      = Ver_z_8%t(1)   - Cstv_Ztop_8
      do k=2,G_nk
         Ver_dz_8%m(k)   = Ver_z_8%t(k) - Ver_z_8%t(k-1)
      enddo

      do k=1,G_nk
          Ver_idz_8%m(k) = one/Ver_dz_8%m(k)
           Ver_dz_8%t(k) = Ver_z_8%m(k+1) - Ver_z_8%m(k)
          Ver_idz_8%t(k) = one/Ver_dz_8%t(k)
         Ver_dbdz_8%t(k) = (Ver_b_8%m(k+1)-Ver_b_8%m(k))*Ver_idz_8%t(k)
      enddo

!     -------------------------------------------------------
!     Compute AVERGING WEIGHTS FROM THERMO TO MOMENTUM LEVELS 
!     -------------------------------------------------------

      do k=1,G_nk
         if(k.eq.G_nk) then
            Ver_wp_8%m(k) = Ver_dz_8%t(k)*Ver_idz_8%m(k)
         else
            Ver_wp_8%m(k) = Ver_dz_8%t(k)*half*Ver_idz_8%m(k)
         endif
            Ver_wm_8%m(k) = one-Ver_wp_8%m(k)
      enddo

          Ver_dbdz_8%m(1) = Ver_wp_8%m(1) * Ver_dbdz_8%t(1)  &
                          + Ver_wm_8%m(1) * (Ver_b_8%m(1)-zero)/(Ver_z_8%m(1)-Cstv_Ztop_8)
      do k=2,G_nk
          Ver_dbdz_8%m(k) = Ver_wp_8%m(k) * Ver_dbdz_8%t(k)  &
                          + Ver_wm_8%m(k) * Ver_dbdz_8%t(k-1)
      enddo

      if(Schm_autobar_L) then
         do k=1,G_nk
            Ver_dbdz_8%m(k) = one/(Cstv_Zsrf_8-Ver_z_8%m(1))
         enddo
      endif

!     -------------------------------------------------------
!     Compute AVERGING WEIGHTS FROM MOMENTUM TO THERMO LEVELS 
!     -------------------------------------------------------

      do k=1,G_nk-1
         Ver_wp_8%t(k) = half
         Ver_wm_8%t(k) = half
      enddo
      Ver_wp_8%t(G_nk) = one
      Ver_wm_8%t(G_nk) = zero

!     -------------------------------------------------------
!     Compute DOUBLE VERTICAL AVERGING OF B (B bar zz) 
!     -------------------------------------------------------

      Ver_bzz_8(1) = Ver_wp_8%m(1)*(Ver_wp_8%t(1)*Ver_b_8%m(2)  &
                    +Ver_wm_8%t(1)*Ver_b_8%m(1))
      do k=2,G_nk
         Ver_bzz_8(k) = Ver_wp_8%m(k)*(Ver_wp_8%t(k  )*Ver_b_8%m(k+1)  &
              +Ver_wm_8%t(k  )*Ver_b_8%m(k  )) &
              +Ver_wm_8%m(k)*(Ver_wp_8%t(k-1)*Ver_b_8%m(k  )  &
              +Ver_wm_8%t(k-1)*Ver_b_8%m(k-1))
      enddo

!     -------------------------------------------------------
!     Initialize Ver_onezero
!     -------------------------------------------------------

      Ver_onezero=1.
      Ver_onezero(1)=0.

!     ----------------------------------------------------------
!     Put last thermo a and b at surface if there is non physics
!     ----------------------------------------------------------

! Doing this will produce an output file that the
! model cannot read back
      if (.not.Schm_phyms_L) then
         Ver_a_8%t(G_nk)= Cstv_Zsrf_8
         Ver_b_8%t(G_nk)= one
         Ver_hyb%t(G_nk)= one
      endif

!     ----------------------------------------------------------
!     Define Tstar when not variable
!     ----------------------------------------------------------
      do k = 1, G_nk+1
         Ver_Tstr_8%m(k)=Cstv_Tstr_8
      enddo

!     ----------------------------------------------------------
!     Use Tstar as given in tmean (80 values)
!     ----------------------------------------------------------
 !*   do k = 1, G_nk+1
 !*      Ver_Tstr_8%m(k)=tmean(k)
 !*   enddo
!
!     ----------------------------------------------------------
!     Compute TSTAR variable for Schaer case
!     ----------------------------------------------------------
 !!   nstar=0.01
 !!   tsurf=288.
 !!   a00 = nstar**2/Dcst_grav_8
 !!   a01 = (Dcst_cpd_8*tsurf*a00)/Dcst_grav_8
 !!   capc1 = Dcst_grav_8**2/(nstar**2*Dcst_cpd_8*tsurf)
 !!   do k=1,G_nk+1
 !!      a02 = (exp(Ver_a_8%m(k))/Cstv_pref_8)**Dcst_cappa_8
 !!      hh=-log((capc1-1.+a02)/capc1)/a00
 !!      Ver_Tstr_8%m(k)=tsurf*((1.-capc1)*exp(a00*hh)+capc1)
 !!   end do

      do k = 1, G_nk
         Ver_Tstr_8%t(k)=Ver_wp_8%t(k)*Ver_Tstr_8%m(k+1)+Ver_wm_8%t(k)*Ver_Tstr_8%m(k)
      enddo

!     --------------
!     Compute fistar
!     --------------
      Ver_fistr_8(G_nk+1)=zero
      do k = G_nk, 1, -1
         Ver_fistr_8(k)=Ver_fistr_8(k+1)-Dcst_Rgasd_8*Ver_Tstr_8%t(k)*(Ver_z_8%m(k)-Ver_z_8%m(k+1))
      enddo

!     ----------------------------------------------------------
!     Save vcoord and ip1m/t for output
!     ----------------------------------------------------------
      istat = vgrid_wb_put('ref-m',vcoord,Ver_ip1%m,'PW_P0:P')
      istat = vgrid_wb_put('ref-t',vcoord,Ver_ip1%t,'PW_P0:P')

      options_readwrite = WB_IS_LOCAL
      options_readonly = options_readwrite + WB_REWRITE_NONE

      istat= wb_put('model/Vgrid/size-hybm',size(Ver_hyb%m),options_readonly)
      istat= wb_put('model/Vgrid/size-hybt',size(Ver_hyb%t),options_readonly)
      istat= wb_put('model/Vgrid/hybm'     ,Ver_hyb%m      ,options_readwrite)
      istat= wb_put('model/Vgrid/hybt'     ,Ver_hyb%t      ,options_readwrite)
      istat= wb_put('model/Vgrid/am'       ,Ver_a_8%m      ,options_readonly)
      istat= wb_put('model/Vgrid/bm'       ,Ver_b_8%m      ,options_readonly)
      istat= wb_put('model/Vgrid/at'       ,Ver_a_8%t      ,options_readonly)
      istat= wb_put('model/Vgrid/bt'       ,Ver_b_8%t      ,options_readonly)
      istat= wb_put('model/Vgrid/rcoef'    ,Grd_rcoef      ,options_readonly)
      istat= wb_put('model/Vgrid/ptop'     ,Cstv_ptop_8    ,options_readonly)
      istat= wb_put('model/Vgrid/pref'     ,Cstv_pref_8    ,options_readonly)
      istat= wb_put('model/Vgrid/vcode'    ,Ver_code       ,options_readonly)

      if (Lun_debug_L) call prgenab()
!     __________________________________________________________________
!
      return
      end
