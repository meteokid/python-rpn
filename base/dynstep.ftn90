!---------------------------------- LICENCE BEGIN -------------------------------
! GEM - Library of kernel routines for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This library is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------

!**s/r dynstep -  Control of the dynamical timestep of the model

      subroutine dynstep
      implicit none
#include <arch_specific.hf>

!author 
!     Michel Roch - rpn - nov 1993
!
!revision
! v2_00 - Desgagne M.       - initial MPI version
! v2_10 - Tanguay M.        - introduce diffusion at T1 when 4D-Var 
! v2_11 - Desgagne M.       - vertical sponge layer
! v2_21 - Tanguay M.        - ADJ of vertical sponge layer
! v2_31 - Tanguay M.        - adapt ADJ to diffusion in gem_run 
! v3_00 - Desgagne & Lee    - Lam configuration
! v3_03 - Tanguay M.        - Adjoint Lam configuration
! v3_11 - Gravel S.         - provide for variable topography
! v3_20 - Pellerin P.       - schm_offline_l: allow to run 
! v3_30 - Lee V.            - new Lam I/O with acid test
!                             without the dynamical kernel
! v4_00 - Plante & Girard   - Log-hydro-pressure coord on Charney-Phillips grid
! v4_60 - Lee V.            - call yyg_nest_tracers before t02t1

#include "gmm.hf"
#include "glb_ld.cdk"
#include "lun.cdk"
#include "orh.cdk"
#include "init.cdk"
#include "lctl.cdk"
#include "step.cdk"
#include "rstr.cdk"
#include "schm.cdk"
#include "vtopo.cdk"
#include "grd.cdk"
#include "tr3d.cdk"
#include "vt1.cdk"

      character(len=GMM_MAXNAMELENGTH) :: tr_name
      logical first_L, secon_L
      integer itraj,n,istat
      real, pointer, dimension(:,:,:) :: tr1
!
!     ---------------------------------------------------------------
!
      if (Lun_debug_L) write(Lun_out,1000)
      call timing_start ( 10, 'DYNSTEP' )

!     first_L is TRUE  for the first timestep
!           or the first timestep after digital filter initialisation

      first_L = (Step_kount.eq.1).or.(Rstri_idon_L .and.  &
                 Step_kount.eq.(Init_dfnp+1)/2)

!     secon_L is TRUE  for the second timestep
!            or the second timestep after digital filter initialisation

      secon_L = (Step_kount.eq.2).or.(Rstri_idon_L .and.  &
                 Step_kount.eq.(Init_dfnp+1)/2 + 1)

!     Orh_crank_L is TRUE for a time step using C-N procedure

      Orh_crank_L= first_L .or. ( secon_L .and. ( Schm_xwvt3 .gt. 0 ))  &
                           .or. ( mod(Lctl_step-1,Schm_modcn) .eq. 0 )

      itraj = Schm_itraj

      if (Schm_bitpattern_L) then
         call pospers ()
      else
         if ( first_L) then
            call pospers ()
            itraj = max( 5, Schm_itraj )
         endif
      endif

! Perform time interpolation of Lateral BCs for LAM configurations

      if ( Orh_crank_L ) then
  
!        CN iterations
    
         if (Lun_debug_L) write(Lun_out,1005) Schm_itcn-1

         do Orh_icn = 1,Schm_itcn-1
    
            call tstpdyn (itraj)
            itraj = Schm_itraj

            call hzd_momentum

         end do
         if (Lun_debug_L) write(Lun_out,1006)
  
         Orh_icn=Schm_itcn
 
      else

         Orh_icn = 1
  
      endif
 
!     ------------------------------------------------------------
!     Perform the timestep
!     ------------------------------------------------------------
 
      call tstpdyn ( Schm_itraj )
!     
!     ------------------------------------------------------------
!     C	  When the timestep is completed, rename all the
!     C        variables at time level t1 -> t0 and rename all the
!     C        variables at time level t0 -> t1 for the next timestep
!     ------------------------------------------------------------

      call t02t1 ()

      if (Grd_yinyang_L) then
         do n= 1, Tr3d_ntr
            tr_name = 'TR/'//trim(Tr3d_name_S(n))//':P'
            istat = gmm_get(tr_name, tr1)
            call yyg_xchng (tr1 , l_minx,l_maxx,l_miny,l_maxy, G_nk,&
                            .true., 'CUBIC')
         end do
         istat = gmm_get(gmmk_wt1_s , tr1)
         call yyg_xchng (tr1 , l_minx,l_maxx,l_miny,l_maxy, G_nk,&
                         .true., 'CUBIC')
      else
         if (G_lam) call nest_gwa ()
      endif

      call pw_update_GPW()
      call pw_update_UV('P')
      call pw_update_T ('P')

!     ===================
!     END OF ONE TIMESTEP
!     ===================

      if ( Lctl_step-Vtopo_start .eq. Vtopo_ndt) Vtopo_L = .false.

      call timing_stop ( 10 )

 1000 format( &
      /,'CONTROL OF DYNAMICAL STEP: (S/R DYNSTEP)', &
      /,'========================================'/)
 1005 format( &
      /3X,'##### Crank-Nicholson iterations: ===> PERFORMING',I3, &
          ' timestep(s) #####'/)
 1006 format( &
      /3X,'##### Crank-Nicholson iterations: ===> DONE... #####'/)
!
!     ---------------------------------------------------------------
!
      return
      end
