!---------------------------------- LICENCE BEGIN -------------------------------
! GEM - Library of kernel routines for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This library is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------

!**s/r hatoprg - downward vertical integration 
!
#include "model_macros_f.h"
!
      subroutine hatoprg ( F_gg, F_ff, F_con, F_hz_8, ng, Nk )
!
      implicit none
!
      integer ng, Nk
      real F_gg(ng,Nk),F_ff(ng,Nk),F_con
      real*8  F_hz_8(Nk)
!
!author
!     J. Cote - rpn - nov 93 
!
!revision
! v2_00 - Desgagne M.       - initial MPI version (from hatoprg v1_03)
! v3_00 - Desgagne & Lee    - Lam configuration
! v3_21 - Desgagne M.       - Revision OpenMP
! v3_30 - Tanguay M.        - Revision Openmp LAM 
! v4_40 - Snelgrove, Ken    - bug found by MAC Linux
!
!object  DOWNWARD vertical integration 
!
!                 / z(sfc) 
!        G   =  C |        F dz 
!         k       / z(k)
!
!arguments
!  Name        I/O                 Description
!----------------------------------------------------------------
!  F_gg        O          resulting vertical integration
!  F_ff        I          input field to be integrated  
!  F_con       I          mutiplication coefficient 
!  F_hz_8      I          intervals in z-direction
!----------------------------------------------------------------
!
!implicits 
#include "glb_ld.cdk"
!
      integer i, k
      real*8 wk(G_nk-1),r
!
!*
!     __________________________________________________________________
!
!     size of F_hz_8 could be longer than wk
      wk(:  ) = .5*F_con*F_hz_8(1:(G_nk-1))
!
!$omp parallel private(r,k)
!$omp do
      do i= 1, ng
!
         r=0.
         do k= G_nk-1, 1, -1
!
           F_gg(i,k+1) = r
           r = r + wk(k)*( F_ff(i,k+1) + F_ff(i,k) )
!
         end do
         F_gg(i,1) = r
!
      end do
!$omp enddo
!$omp end parallel
!
!     __________________________________________________________________
!
      return
      end
