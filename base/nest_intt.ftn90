!---------------------------------- LICENCE BEGIN -------------------------------
! GEM - Library of kernel routines for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This library is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------

!**s/r nest_intt -- Linear interpolation in time of nesting data
!
#include "model_macros_f.h"
!
      subroutine nest_intt
      implicit none
!
!author   M. Desgagne - April 2002
!
!revision
! v3_01 - Desgagne M.               - initial version (after MC2 v_4.9.3)
! v3_03 - Tanguay M.                - Adjoint Lam configuration
! v3_20 - Pellerin P. and Y. Delage - Special interpolations for MEC 
! v4    - Girard-Plante-Lee         - Staggered version
! v4_04 - Plante A.                 - Remove offline
! v4_05 - Lepine M.                 - VMM replacement with GMM
! v4_10 - Tanguay M.                - Adjust digital filter when LAM
!
#include "gmm.hf"
#include "glb_ld.cdk"
#include "lam.cdk"
#include "nest.cdk"
#include "tr3d.cdk"
#include "schm.cdk"
#include "lctl.cdk"
#include "cstv.cdk"
#include "lun.cdk"
#include "step.cdk"
!
      integer,external ::  newdate
!
      type(gmm_metadata) :: mymeta
      character*16 datev
      character(len=GMM_MAXNAMELENGTH) :: tr_name
      integer yy,mo,dd,hh,mm,ss,dum,i,j,k,dat,np,ip,id,err,n,istat
      real, pointer, dimension(:,:,:) :: tr_deb,tr_fin,tr
      real*8  one,sid,rsid,dayfrac,tx,dtf,a,b
      parameter(one=1.0d0, sid=86400.0d0, rsid=one/sid )
!
!     ---------------------------------------------------------------
!
      dayfrac = dble (Lctl_step)*Cstv_dt_8*rsid
      call incdatsd  (datev, Step_runstrt_S, dayfrac)
      call prsdate   (yy,mo,dd,hh,mm,ss,dum,datev)
      call pdfjdate2 (tx, yy,mo,dd,hh,mm,ss)
!
      istat = gmm_get(gmmk_nest_u_deb_s ,nest_u_deb ,mymeta)
      istat = gmm_get(gmmk_nest_v_deb_s ,nest_v_deb ,mymeta)
      istat = gmm_get(gmmk_nest_w_deb_s ,nest_w_deb ,mymeta)
      istat = gmm_get(gmmk_nest_t_deb_s ,nest_t_deb ,mymeta)
      istat = gmm_get(gmmk_nest_zd_deb_s,nest_zd_deb,mymeta)
      istat = gmm_get(gmmk_nest_s_deb_s ,nest_s_deb ,mymeta)

      istat = gmm_get(gmmk_nest_u_s ,nest_u ,mymeta)
      istat = gmm_get(gmmk_nest_v_s ,nest_v ,mymeta)
      istat = gmm_get(gmmk_nest_w_s ,nest_w ,mymeta)
      istat = gmm_get(gmmk_nest_t_s ,nest_t ,mymeta)
      istat = gmm_get(gmmk_nest_zd_s,nest_zd,mymeta)
      istat = gmm_get(gmmk_nest_s_s ,nest_s ,mymeta)

      istat = gmm_get(gmmk_nest_u_fin_s ,nest_u_fin ,mymeta)
      istat = gmm_get(gmmk_nest_v_fin_s ,nest_v_fin ,mymeta)
      istat = gmm_get(gmmk_nest_w_fin_s ,nest_w_fin ,mymeta)
      istat = gmm_get(gmmk_nest_t_fin_s ,nest_t_fin ,mymeta)
      istat = gmm_get(gmmk_nest_zd_fin_s,nest_zd_fin,mymeta)
      istat = gmm_get(gmmk_nest_s_fin_s ,nest_s_fin ,mymeta)

      if (.not. Schm_hydro_L) then
         istat = gmm_get(gmmk_nest_q_deb_s,nest_q_deb,mymeta)
         istat = gmm_get(gmmk_nest_q_s    ,nest_q    ,mymeta)
         istat = gmm_get(gmmk_nest_q_fin_s,nest_q_fin,mymeta)
      endif

      dayfrac = dble(Step_nesdt)*rsid

      if (tx .lt. Lam_tdeb) then

         Lam_current_S  = Step_runstrt_S
         Lam_previous_S = Lam_current_S
         call prsdate   (yy,mo,dd,hh,mm,ss,dum,Step_runstrt_S)
         call pdfjdate2 (Lam_tdeb,yy,mo,dd,hh,mm,ss)
         do
            call incdatsd (datev, Lam_current_S,dayfrac)
            Lam_current_S = datev
            call prsdate   (yy,mo,dd,hh,mm,ss,dum,Lam_current_S)
            call pdfjdate2 (Lam_tfin, yy,mo,dd,hh,mm,ss)
            if (Lam_tfin.ge.tx) exit
            Lam_previous_S = Lam_current_S
            Lam_tdeb       = Lam_tfin
         end do
         Lam_current_S = Lam_previous_S
         Lam_tfin      = Lam_tdeb

         call nest_indata ( Lam_previous_S )

      endif

      dtf = 1.0d0
      if (tx.gt.Lam_tfin) then

         dtf = (tx-Lam_tfin) * sid / Cstv_dt_8
         Lam_previous_S = Lam_current_S
         Lam_tdeb       = Lam_tfin

         call incdatsd (datev, Lam_current_S, dayfrac)
         Lam_current_S = datev
         call prsdate   (yy,mo,dd,hh,mm,ss,dum,Lam_current_S)
         call pdfjdate2 (Lam_tfin, yy,mo,dd,hh,mm,ss)

         nest_u_deb  = nest_u_fin
         nest_v_deb  = nest_v_fin
         nest_w_deb  = nest_w_fin
         nest_t_deb  = nest_t_fin
         nest_zd_deb = nest_zd_fin
         nest_s_deb  = nest_s_fin
         if (.not. Schm_hydro_L) nest_q_deb = nest_q_fin
         do n=1,Tr3d_ntr
            tr_name = 'NEST/'//trim(Tr3d_name_S(n))//':F'
      	    istat = gmm_get(tr_name,tr_fin,mymeta)
            tr_name = 'NEST/'//trim(Tr3d_name_S(n))//':A'
      	    istat = gmm_get(tr_name,tr_deb,mymeta)
            tr_deb (1:l_ni,1:l_nj,1:G_nk+2) = tr_fin (1:l_ni,1:l_nj,1:G_nk+2)
         end do

         call nest_indata ( Lam_current_S )

      endif
!
!     Temporal linear interpolation
!      
      b = (tx - Lam_tdeb) / (Lam_tfin - Lam_tdeb)
!     b = nint( b * 10000000000.0_8 ) / 10000000000.0_8 ! Keep first 10 decimals
      a = one - b

      if (Lun_debug_L) write(Lun_out,'(/"In nest_intt, temporal interpolation parametres A,B =",1p2e24.16/)') a,b

      nest_u (1:l_ni,1:l_nj,1:G_nk+1) = a*nest_u_deb (1:l_ni,1:l_nj,1:G_nk+1) + b*nest_u_fin (1:l_ni,1:l_nj,1:G_nk+1)
      nest_v (1:l_ni,1:l_nj,1:G_nk+1) = a*nest_v_deb (1:l_ni,1:l_nj,1:G_nk+1) + b*nest_v_fin (1:l_ni,1:l_nj,1:G_nk+1)
      nest_w (1:l_ni,1:l_nj,1:G_nk+1) = a*nest_w_deb (1:l_ni,1:l_nj,1:G_nk+1) + b*nest_w_fin (1:l_ni,1:l_nj,1:G_nk+1)
      nest_t (1:l_ni,1:l_nj,1:G_nk+2) = a*nest_t_deb (1:l_ni,1:l_nj,1:G_nk+2) + b*nest_t_fin (1:l_ni,1:l_nj,1:G_nk+2)
      nest_zd(1:l_ni,1:l_nj,1:G_nk+1) = a*nest_zd_deb(1:l_ni,1:l_nj,1:G_nk+1) + b*nest_zd_fin(1:l_ni,1:l_nj,1:G_nk+1)
      nest_s (1:l_ni,1:l_nj         ) = a*nest_s_deb (1:l_ni,1:l_nj         ) + b*nest_s_fin (1:l_ni,1:l_nj         )
      if (.not. Schm_hydro_L) &
      nest_q (1:l_ni,1:l_nj,1:G_nk+1) = a*nest_q_deb (1:l_ni,1:l_nj,1:G_nk+1) + b*nest_q_fin (1:l_ni,1:l_nj,1:G_nk+1)

      do n=1,Tr3d_ntr
         tr_name = 'NEST/'//trim(Tr3d_name_S(n))//':F'
         istat = gmm_get(tr_name,tr_fin,mymeta)
         tr_name = 'NEST/'//trim(Tr3d_name_S(n))//':C'
         istat = gmm_get(tr_name,tr,mymeta)
         tr_name = 'NEST/'//trim(Tr3d_name_S(n))//':A'
         istat = gmm_get(tr_name,tr_deb,mymeta)
         tr (1:l_ni,1:l_nj,1:G_nk+2) = a*tr_deb(1:l_ni,1:l_nj,1:G_nk+2) + b*tr_fin(1:l_ni,1:l_nj,1:G_nk+2)
      end do
!
!     ---------------------------------------------------------------
!
      return
      end

