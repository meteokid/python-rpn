!---------------------------------- LICENCE BEGIN -------------------------------
! GEM - Library of kernel routines for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This library is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------

!**s/r pw_update - Update physical quantities WZ, GZ, PM and PT
!
      subroutine pw_update_GPW
      implicit none
#include <arch_specific.hf>

!author
!     Michel Desgagne - May 2010
!
!revision
! v4_14 - Desgagne, M.     - Initial revision

#include "gmm.hf"
#include "glb_ld.cdk"
#include "vt1.cdk"
#include "p_geof.cdk"
#include "ver.cdk"
#include "pw.cdk"

      integer i, j, k, istat
      real wk1(l_ni,l_nj,2),wk2(l_ni,l_nj,2)
      real fip(l_minx:l_maxx,l_miny:l_maxy,G_nk+1)
!     ________________________________________________________________
!
      istat = gmm_get(gmmk_pw_wz_plus_s , pw_wz_plus )
      istat = gmm_get(gmmk_pw_gz_plus_s , pw_gz_plus )
      istat = gmm_get(gmmk_pw_pm_plus_s , pw_pm_plus )
      istat = gmm_get(gmmk_pw_pt_plus_s , pw_pt_plus )

      istat = gmm_get(gmmk_tt1_s  ,   tt1)
      istat = gmm_get(gmmk_wt1_s  ,   wt1)
      istat = gmm_get(gmmk_st1_s  ,   st1)
      istat = gmm_get(gmmk_fis0_s ,  fis0)
      istat = gmm_get(gmmk_qt1_s  ,   qt1)

      call diag_fi (fip, st1, tt1, qt1, fis0, &
                    l_minx,l_maxx,l_miny,l_maxy,G_nk, 1, l_ni, 1, l_nj)

!$omp parallel private(wk1,wk2,i,j)

!$omp do
      do k=1,l_nk+1
         do j=1,l_nj
         do i=1,l_ni
            wk1(i,j,1) = Ver_a_8%m(k) + Ver_b_8%m(k) * st1(i,j)
            wk1(i,j,2) = Ver_a_8%t(k) + Ver_b_8%t(k) * st1(i,j)
         enddo
         enddo
         call vsexp(wk2(1,1,1), wk1(1,1,1), 2*l_ni*l_nj)
         pw_pm_plus(1:l_ni,1:l_nj,k) = wk2(1:l_ni,1:l_nj,1)
         pw_pt_plus(1:l_ni,1:l_nj,k) = wk2(1:l_ni,1:l_nj,2)
      end do
!$omp enddo

!$omp do
      do k=1,l_nk
         pw_wz_plus(:,:,k) = wt1(:,:,k)
         pw_gz_plus(1:l_ni,1:l_nj,k)= fip(1:l_ni,1:l_nj,k) + &
                                      Ver_fistr_8%m(k)
      end do
!$omp enddo
      pw_gz_plus(1:l_ni,1:l_nj,l_nk+1)= fis0(1:l_ni,1:l_nj)

!$omp do
      do j= 1, l_nj
      do i= 1, l_ni
         pw_pt_plus(i,j,l_nk+1) = pw_pm_plus(i,j,l_nk+1)
      end do
      end do
!$omp enddo

!$omp end parallel

!     ________________________________________________________________
!
      return
      end
