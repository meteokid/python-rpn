!---------------------------------- LICENCE BEGIN -------------------------------
! GEM - Library of kernel routines for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This library is free software; you can redistribute it and/or modify it 
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A 
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------
#include "msg.h"
#include "stop_mpi.h"

!/**
subroutine adx_cliptraj3(F_x,  F_y, i0, in, j0, jn, F_ni,F_nj,k0,F_nk, mesg)
   implicit none
#include "adx_dims.cdk"
   !@objective Clip SL hor. trajectories to either fit inside the 
   !                    physical domain of the processor or to the
   !                    actual maximum allowed COURRANT number (LAM)
   !@arguments
   character(len=*) :: mesg
   integer :: F_ni,F_nj,F_nk  !I, dims of F_x, F_y
   integer :: i0,in,j0,jn,k0  !I, scope of the operator
   real, dimension(F_ni,F_nj,F_nk) :: &
        F_x, F_y              !I/O, upstream pos
   !@author Michel Desgagne, Spring 2008
   !@revisions
   ! v3_31 - Desgagne M.  - Initial version
   ! v4_40 - Qaddouri/Lee - Yin-Yang trajectory clipping
!**/
#include "adx_grid.cdk"
   real*8,  parameter :: EPS_8 = 1.D-5
!  integer, parameter :: BCS_BASE = 4
   integer :: BCS_BASE ! BCS points for Yin-Yang, normal LAM

   character(len=MSG_MAXLEN) :: msg_S
   integer :: n, i,j,k, cnt, sum_cnt, err, totaln
   real :: minposx,maxposx,minposy,maxposy, posxmin,posxmax,posymin,posymax
   !---------------------------------------------------------------------

   BCS_BASE= 4
   if (Adx_yinyang_L) BCS_BASE = 3
   minposx = adx_xx_8(adx_lminx+1) + EPS_8
   if (adx_is_west)  minposx = adx_xx_8(1+BCS_BASE) + EPS_8
   maxposx = adx_xx_8(adx_lmaxx-1) - EPS_8
   if (adx_is_east)  maxposx = adx_xx_8(adx_lni-BCS_BASE) - EPS_8
   minposy = adx_yy_8(adx_lminy+1) + EPS_8
   if (adx_is_south) minposy = adx_yy_8(1+BCS_BASE) + EPS_8
   maxposy = adx_yy_8(adx_lmaxy-1) - EPS_8
   if (adx_is_north) maxposy = adx_yy_8(adx_lnj-BCS_BASE) - EPS_8

   cnt=0

   !- Clipping to processor boundary
   do k=k0,F_nk
      do j=j0,jn
         do i=i0,in
            if ( (F_x(i,j,k)<minposx).or.(F_x(i,j,k)>maxposx).or. &
                 (F_y(i,j,k)<minposy).or.(F_y(i,j,k)>maxposy) ) then
               cnt=cnt+1
            endif
            F_x(i,j,k) = min(max(F_x(i,j,k),minposx),maxposx)
            F_y(i,j,k) = min(max(F_y(i,j,k),minposy),maxposy)
         enddo
      enddo
   enddo

   n = max(1,adx_maxcfl)
   totaln = (F_ni*n*2 + (F_nj-2*n)*n*2) * (F_nk-k0+1)

   call rpn_comm_Allreduce(cnt,sum_cnt,2,"MPI_INTEGER", "MPI_SUM","grid",err)

   if (trim(mesg).ne."" .and. sum_cnt>0) then
      write(msg_S,'(a,i5,a,f6.2,2x,a)')  &
           ' ADW trajtrunc: npts=',sum_cnt, &
           ', %=',real(sum_cnt)/real(totaln)*100., &
           mesg
      call msg(MSG_INFO,msg_S)
   endif
   !---------------------------------------------------------------------
   return
end subroutine adx_cliptraj3

!========================================================================
!== stubs ===============================================================
!========================================================================

subroutine adx_cliptraj()
   call stop_mpi(STOP_ERROR,'adx_cliptraj','called a stub')
   return
end subroutine
subroutine adx_cliptraj2()
   call stop_mpi(STOP_ERROR,'adx_cliptraj2','called a stub')
   return
end subroutine

