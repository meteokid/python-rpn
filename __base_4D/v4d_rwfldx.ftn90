!---------------------------------- LICENCE BEGIN -------------------------------
! GEM - Library of kernel routines for the GEM numerical atmospheric model
! Copyright (C) 1990-2010 - Division de Recherche en Prevision Numerique
!                       Environnement Canada
! This library is free software; you can redistribute it and/or modify it
! under the terms of the GNU Lesser General Public License as published by
! the Free Software Foundation, version 2.1 of the License. This library is
! distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
! without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
! PARTICULAR PURPOSE. See the GNU Lesser General Public License for more details.
! You should have received a copy of the GNU Lesser General Public License
! along with this library; if not, write to the Free Software Foundation, Inc.,
! 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
!---------------------------------- LICENCE END ---------------------------------

!**s/r v4d_rwfldx - Read (Write) a specific field from (on) WA file 
!                   when the field has no HALO
!
#include "model_macros_f.h"
!
      subroutine v4d_rwfldx (F_field,Ni,Nj,Nk, &
                             F_addr,F_diag_L,F_name_S,F_adjoint_L,F_diff,F_icode)
!
      implicit none
#include <arch_specific.hf>
!
      integer NI,Nj,Nk,F_icode
      real F_field(*)
!
      integer F_iun,F_addr,F_diff
      logical F_diag_L,F_adjoint_L
      character*8 F_name_S
!
!author M.Tanguay
!
!revision
! v2_31 - Tanguay M.        - initial MPI version
! v3_21 - Tanguay M.        - Revision Openmp
! v4_04 - Tanguay M.        - Staggered version TL/AD 
! v4_40 - Tanguay M.        - Revision TL/AD
!
!object
!     see id section
!
!arguments
!  Name        I/O                 Description
!----------------------------------------------------------------
! F_field      I/O                 Field without HALO
! Ni Nj Nk     I                   Dimensions of F_field 
! F_addr       I/O                 Position in WA file
! F_diag_L     I                   Give diagnostics if TRUE 
! F_name_S     I                   Name of the field
! F_adjoint_L  I                   TRUE if adjoint run
! F_diff       I                   To be subtracted from position if adjoint run 
! F_icode      I                   If F_icode.GT.0 -Write F_field on WA file 
!                                  If F_icode.LE.0 -Read  F_field from WA file
!----------------------------------------------------------------
!
!implicits
#include "lun.cdk"
      include "v4dg.inc"
!
      integer ind
!
      if (F_icode.gt.0) then
!
!         Write F_field INCORE 
!         --------------------
          if(V4dg_trsize.lt.F_addr + Ni*Nj*Nk -1) call handle_error(-1,'v4d_rwfldx','V4dg_trsize.lt.F_addr + Ni*Nj*Nk -1') 
!
!$omp parallel do
          do ind = 1,Ni*Nj*Nk
             V4dg_trincore(F_addr + ind - 1) = F_field(ind)
          enddo
!$omp end parallel do
!
!         Diagnostics
!         -----------
          if(F_diag_L) write(Lun_out,*) & 
                       'WA FILE: wawrit ','Waf_adr = ',F_addr,'for ',F_name_S
!
!         Update address
!         --------------
          F_addr = F_addr + Ni*Nj*Nk 
!
      else
!
!         Read F_field INCORE 
!         -------------------
!$omp parallel do
          do ind = 1,Ni*Nj*Nk
             F_field(ind) = V4dg_trincore(F_addr + ind - 1)
          enddo
!$omp end parallel do
!
!         Diagnostics
!         -----------
          if(F_diag_L) write(Lun_out,*) &
                       'WA FILE: waread ','Waf_adr = ',F_addr,'for ',F_name_S
!
!         Update address
!         --------------
          if(     F_adjoint_L) F_addr = F_addr - F_diff 
          if(.not.F_adjoint_L) F_addr = F_addr + Ni*Nj*Nk 
!
      endif
!
!     ---------------------------------------------------------------
!
      return
      end
