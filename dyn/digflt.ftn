***s/r digflt -  Compute digitally filtered fields
*
#include "model_macros_f.h"
*
      subroutine digflt
*
#include "impnone.cdk"
*
*author
*     Alain Patoine - march 94
*
*revision
* v2_00 - Desgagne M.       - initial MPI version
* v2_30 - Edouard  S.       - remove pi' at the top
* v2_31 - Desgagne M.       - re-introduce tracers
* v3_00 - Desgagne & Lee    - Lam configuration
* v3_21 - Lee V.            - Remove Tr2d
*
*object
*	
*arguments
*	none
*
*implicits
#include "glb_ld.cdk"
#include "mem.cdk"
#include "init.cdk"
#include "lctl.cdk"
#include "lun.cdk"
#include "rstr.cdk"
#include "schm.cdk"
#include "vta.cdk"
#include "vt1.cdk"
#include "vtx.cdk"
#include "tr3d.cdk"
#include "p_bus.cdk"
*
*modules
      integer  vmmlod,vmmget,vmmuld
      external vmmlod,vmmget,vmmuld
      integer  open_db_file,close_db_file,rewind_db_file,
     $         read_db_file,write_db_file
      external open_db_file,close_db_file,rewind_db_file,
     $         read_db_file,write_db_file
*
      integer err, key(10), i, j, k, n, key1_, keya_, key1(Tr3d_ntr), 
     $        keya(Tr3d_ntr)
      real tr1,tra
      pointer (patr1, tr1(LDIST_SHAPE,*)),(patra, tra(LDIST_SHAPE,*))
      real    busper(max(1,p_bper_siz)), buf(l_ni,G_nk+1), dfcoef 
      real, allocatable, dimension(:,:,:,:) :: tr2,tr3
*
*     __________________________________________________________________
*
      dfcoef = Init_dfco( abs( (Init_dfnp-1)/2 - Lctl_step ) )
*
      key(1)  = VMM_KEY(uta  )
      key(2)  = VMM_KEY(ut1  )
      key(3)  = VMM_KEY(vta  )
      key(4)  = VMM_KEY(vt1  )
      key(5)  = VMM_KEY(tta  )
      key(6)  = VMM_KEY(tt1  )
      key(7)  = VMM_KEY(psdta)
      key(8)  = VMM_KEY(psdt1)
      key(9)  = VMM_KEY(sta  )
      key(10) = VMM_KEY(st1  )
      err = vmmlod(key,10)
      err = VMM_GET_VAR(uta  )
      err = VMM_GET_VAR(ut1  )
      err = VMM_GET_VAR(vta  )
      err = VMM_GET_VAR(vt1  )
      err = VMM_GET_VAR(tta  )
      err = VMM_GET_VAR(tt1  )
      err = VMM_GET_VAR(psdta)
      err = VMM_GET_VAR(psdt1)
      err = VMM_GET_VAR(sta  )
      err = VMM_GET_VAR(st1  )
*
      do k=1,l_nk+1
      do j= 1, l_nj 
      do i= 1, l_ni 
           tta(i,j,k) =   tta(i,j,k) + dfcoef *   tt1(i,j,k)
         psdta(i,j,k) = psdta(i,j,k) + dfcoef * psdt1(i,j,k)
      end do
      end do
      end do
      do k=1,l_nk
      do j= 1, l_nj 
      do i= 1, l_ni 
           uta(i,j,k) =   uta(i,j,k) + dfcoef *   ut1(i,j,k)
           vta(i,j,k) =   vta(i,j,k) + dfcoef *   vt1(i,j,k)
      end do
      end do
      end do
*
      do j= 1, l_nj
      do i= 1, l_ni
         sta(i,j)   = sta(i,j)   + dfcoef * st1(i,j)
      end do
      end do
*
      err = vmmuld(-1,0)
*
***************************************************************
*     Non-hydrostatic model fields                            
***************************************************************
*
      if ( .not. Schm_hydro_L )then
*
         key(1) = VMM_KEY(wta  )
         key(2) = VMM_KEY(wt1  )
         key(3) = VMM_KEY(qsecta )
         key(4) = VMM_KEY(qsect1 )
         err = vmmlod(key,4)
         err = VMM_GET_VAR(wta  )
         err = VMM_GET_VAR(wt1  )
         err = VMM_GET_VAR(qsecta )
         err = VMM_GET_VAR(qsect1 )
*
         do k=1,l_nk+1
         do j= 1, l_nj
         do i= 1, l_ni
              wta(i,j,k) =   wta(i,j,k) + dfcoef *   wt1(i,j,k)
           qsecta(i,j,k) =qsecta(i,j,k) + dfcoef *qsect1(i,j,k)
         end do
         end do
         end do
         do j= 1, l_nj
         do i= 1, l_ni
           qsecta(i,j,0) =qsecta(i,j,0) + dfcoef *qsect1(i,j,0)
         end do
         end do
*
         err = vmmuld(-1,0)
*
      endif

****************************************************************
*     Passive tracers (no passive tracers in linear model)
****************************************************************

      if ( Init_dftr_L ) then
*
         key1_ = VMM_KEY (trt1)
         keya_ = VMM_KEY (trta)
         do n=1,Tr3d_ntr
            key1(n) = key1_ + n
            keya(n) = keya_ + n
         end do
         if (Tr3d_ntr.gt.0) then
            err = vmmlod(key1,Tr3d_ntr)  
            err = vmmlod(keya,Tr3d_ntr)  
            do n=1,Tr3d_ntr
               err = vmmget(key1(n),patr1,tr1)
               err = vmmget(keya(n),patra,tra)
               do k=1,G_nk+1
               do j=1,l_nj
               do i=1,l_ni
                  tra(i,j,k) = tra(i,j,k) + dfcoef * tr1(i,j,k)
               end do
               end do
               end do
            end do
            err = vmmuld(key1,Tr3d_ntr)
            err = vmmuld(keya,Tr3d_ntr)
         endif
*
      elseif ( Lctl_step .eq. (Init_dfnp-1)/2 ) then
*
         key1_ = VMM_KEY (trt1)
         keya_ = VMM_KEY (trta)
         do n=1,Tr3d_ntr
            key1(n) = key1_ + n
            keya(n) = keya_ + n
         end do
         if (Tr3d_ntr.gt.0) then
            err = vmmlod(key1,Tr3d_ntr)  
            err = vmmlod(keya,Tr3d_ntr)  
            do n=1,Tr3d_ntr
               err = vmmget(key1(n),patr1,tr1)
               err = vmmget(keya(n),patra,tra)
               do k=1,G_nk+1
               do j=1,l_nj
               do i=1,l_ni
                  tra(i,j,k) = tr1(i,j,k)
               end do
               end do
               end do
            end do
            err = vmmuld(key1,Tr3d_ntr)
            err = vmmuld(keya,Tr3d_ntr)
         endif
*
      endif

***********************************************************************
* Physics fields at half span are saved to be used after initialisation
***********************************************************************

      if ( Lctl_step .eq. (Init_dfnp-1)/2 .and. Schm_phyms_L ) then
         if (Mem_phyncore_L) then
            call hpalloc(Phy_busper3D_digf_,p_bper_siz*p_nj,err,1)
            do i=1,p_bper_siz*p_nj
               Phy_busper3D_digf(i) = Phy_busper3D(i)
            end do
         else
            err = open_db_file   (Lun_waphy)
            err = rewind_db_file (Lun_waphy)
            err = open_db_file   (Lun_wapta)
            err = rewind_db_file (Lun_wapta)
            do k = 1,p_nj
               err = read_db_file  (Lun_waphy,j,1)
               err = read_db_file  (Lun_waphy,busper,p_bper_siz)
               err = write_db_file (Lun_wapta,j,1)
               err = write_db_file (Lun_wapta,busper,p_bper_siz)
            end do
            err = close_db_file (Lun_waphy)
            err = close_db_file (Lun_wapta)
         endif
      endif
*
      Rstri_half_L = .false.
      if (Lctl_step.ge.(Init_dfnp-1)/2) Rstri_half_L = .true.
*
      return
      end

