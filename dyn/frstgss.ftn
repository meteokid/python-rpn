***s/r frstgss - Copy data from time level t1 that will be used as a
*                first guess at time level t0
*
#include "model_macros_f.h"
*
      subroutine frstgss ()
*
      implicit none
*
*author 
*     Michel Roch - rpn - nov 1993
*
*revision
* v2_00 - Desgagne M.       - initial MPI version
* v2_30 - Edouard  S.       - replace Schm_elast_L by Schm_cptop_L
*                             end remove pi' at the top 
* v3_21 - Tanguay M.        - Revision Openmp
*
*object
*    See above id
*	
*arguments
*	none
*
*implicits
#include "glb_ld.cdk"
#include "lun.cdk"
#include "schm.cdk"
#include "cori.cdk"
#include "vt0.cdk"
#include "vt1.cdk"
*
*modules
      integer  vmmlod,vmmget,vmmuld
      external vmmlod,vmmget,vmmuld
*
      integer pnerr, pnlkey1(12),pnlod
      integer i, j, k
**
*     ---------------------------------------------------------------
*
      if (Lun_debug_L) write (Lun_out,1000)
*
      pnlkey1(1) = VMM_KEY(tt0)
      pnlkey1(2) = VMM_KEY(st0)
      pnlkey1(3) = VMM_KEY(psdt0)
      pnlkey1(4) = VMM_KEY(tt1)
      pnlkey1(5) = VMM_KEY(st1)
      pnlkey1(6) = VMM_KEY(psdt1)
*
      pnlod=6
      pnerr = vmmlod(pnlkey1,pnlod)

      pnerr = VMM_GET_VAR(tt0)
      pnerr = VMM_GET_VAR(st0)
      pnerr = VMM_GET_VAR(psdt0)
      pnerr = VMM_GET_VAR(tt1)
      pnerr = VMM_GET_VAR(st1)
      pnerr = VMM_GET_VAR(psdt1)

!$omp parallel
*
!$omp do
      do k=1,l_nk+1
      do j= 1, l_nj
      do i= 1, l_ni
         tt0(i,j,k) =  tt1(i,j,k)
         psdt0(i,j,k) =  psdt1(i,j,k)
      end do
      end do
      end do
!$omp end do

!$omp do
      do j= 1 , l_nj
      do i= 1 , l_ni
         st0(i,j)   =  st1(i,j)
      end do
      end do
!$omp end do
*
!$omp end parallel

      pnerr = vmmuld(-1,0)
*
      pnlod=0
      if ( Cori_cornl_L ) then
         pnlkey1(pnlod+1) = VMM_KEY(ut0)
         pnlkey1(pnlod+2) = VMM_KEY(vt0)
         pnlkey1(pnlod+3) = VMM_KEY(ut1)
         pnlkey1(pnlod+4) = VMM_KEY(vt1)
         pnlod=pnlod+4
      endif
      if ( .not. Schm_cptop_L .or. .not. Schm_hydro_L) then
         pnlkey1(pnlod+1) = VMM_KEY(qsect0)
         pnlkey1(pnlod+2) = VMM_KEY(qsect1)
         pnlod = pnlod+2
      endif
      pnerr = vmmlod(pnlkey1,pnlod)

      if ( Cori_cornl_L ) then
         pnerr = VMM_GET_VAR(ut0)
         pnerr = VMM_GET_VAR(ut1)
         pnerr = VMM_GET_VAR(vt0)
         pnerr = VMM_GET_VAR(vt1)
!$omp parallel do
         do k=1,l_nk
         do j= 1 , l_nj
         do i= 1 , l_ni
            ut0(i,j,k)  =  ut1(i,j,k)
            vt0(i,j,k)  =  vt1(i,j,k)
         end do
         end do
         end do
!$omp end parallel do
      endif
*
      if ( .not. Schm_cptop_L .or. .not. Schm_hydro_L) then
         pnerr = VMM_GET_VAR(qsect0)
         pnerr = VMM_GET_VAR(qsect1)
!$omp parallel do
         do k=0,l_nk+1
         do j= 1 , l_nj
         do i= 1 , l_ni
            qsect0(i,j,k) = qsect1(i,j,k)
         end do
         end do
         end do
!$omp end parallel do
      endif
*
c     if (Acid_test_L) tplt0
c    $call glbstat (tpt0,'tpt0',LDIST_DIM,G_nk,8+acid_i0,G_ni-8-acid_in,
c    $                                          8+acid_j0,G_nj-8-acid_jn,1,G_nk)
      pnerr = vmmuld(-1,0)

1000  format(3X,'FIRST GUESS: (S/R FRSTGSS)')

      return
      end


