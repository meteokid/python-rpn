***s/r nlip_2 - compute non-linear portion of momentum, divergence and
*               thermodynamic equations
*               compute non-linear portion of reduced set of equations
*               ( computation and microtasking )
*
***********************************************************************
*
#include <model_macros_f.h>
*
      subroutine nlip_2 ( F_nu, F_nv, F_n1, F_nth, F_n3, F_n3p, 
     $                    F_rheln, F_rhell, F_tpt0, F_tplt0, F_pipt0, 
     $                    F_ncn, F_st0  , F_qt0 , F_fipt0, F_fis  , 
     $                    F_ut0, F_vt0, F_mut0 , F_multx, 
     $                    F_wijk1, F_wijk2, DIST_DIM, Nk )
*
#include "impnone.cdk"
*
      integer DIST_DIM, Nk
      real    F_nu   (DIST_SHAPE,Nk), F_nv   (DIST_SHAPE,Nk),
     %        F_n1   (DIST_SHAPE,Nk), F_nth  (DIST_SHAPE,Nk),
     %        F_n3   (DIST_SHAPE,Nk), F_n3p  (DIST_SHAPE,Nk),
     %        F_rheln(DIST_SHAPE,Nk), F_rhell(DIST_SHAPE,Nk),
     %        F_tpt0 (DIST_SHAPE,Nk), F_tplt0(DIST_SHAPE,Nk),
     %        F_pipt0(DIST_SHAPE,Nk), F_ncn  (DIST_SHAPE,Nk),
     %        F_st0  (DIST_SHAPE)   , F_qt0  (DIST_SHAPE,Nk),
     %        F_fipt0(DIST_SHAPE,Nk), F_fis  (DIST_SHAPE)   ,
     %        F_ut0  (DIST_SHAPE,Nk), F_vt0  (DIST_SHAPE,Nk),
     %        F_mut0 (DIST_SHAPE,Nk), F_multx(DIST_SHAPE,Nk),
     %        F_wijk1(DIST_SHAPE,Nk), F_wijk2(DIST_SHAPE,Nk)
*
*author
*     Alain Patoine - split from nli.ftn
*
*revision
* v2_00 - Desgagne M.       - initial MPI version (from rhs v1_03)
* v2_21 - Lee V.            - modifications for LAM version
* v2_30 - Edouard S.        - adapt for vertical hybrid coordinate
*                             remove F_pptt0 and introduce Ncn
* v3_00 - Qaddouri & Lee    - For LAM, set Nu, Nv values on the boundaries
* v3_00                       of the LAM grid to zeros.
* v3_02 - Edouard S.        - correct non-hydrostatic version (F_mut0,F_tpt0,b2)
*
*object
*
*arguments
*     see documentation of appropriate comdecks
*
*implicits
#include "glb_ld.cdk"
#include "cstv.cdk"
#include "dcst.cdk"
#include "geomg.cdk"
#include "cori.cdk"
#include "schm.cdk"
#include "intuv.cdk"
#include "inuvl.cdk"
#include "ptopo.cdk"
*
      integer i, j, k, i00, inn, j00, jnn, i0, in, j0, jn
      real w1, w2, w3, pd1, pd2, pd3, wk2(DIST_SHAPE)
      real*8  a1, a2, b1, b2, b3, eps, gamma, 
     $       one, half, quarter, aaa, bbb, ccc
      parameter ( one=1.0, half=.5, quarter=.25 )
**
*     __________________________________________________________________
*
*
*     Prepare the nonlinear perturbation q" of log hydro pressure 
*     and the "relative" geopotential ( phi' + phis ) for gradient
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

      i0=1
      in=l_ni
      j0=1
      jn=l_nj
      if (G_lam) then
          if (l_west) i0=1+pil_w -1 
          if (l_east) in=l_ni-pil_e +1
          if (l_south)j0=1+pil_s -1 
          if (l_north)jn=l_nj-pil_n +1 
      endif
*PDIR PARDO FOR=Ptopo_npeOpenMP
      do k=1,l_nk
         bbb = one/Geomg_z_8(k)
         do j= j0, jn
         do i= i0, in
           F_wijk1(i,j,k) = log( one + F_pipt0(i,j,k)*bbb )
     %                     - ( geomg_pib(k)* F_st0(i,j))*bbb
         enddo
         enddo
*
         if (.not. Schm_hydro_L) then
         do j= j0, jn
         do i= i0, in
            F_wijk2(i,j,k) = F_fipt0(i,j,k) + F_fis(i,j) 
         enddo
         enddo
         endif

      enddo

************************************************************
* The nonlinear deviation of horizontal momentum equations *
************************************************************

      aaa = one/( Dcst_rayt_8*Dcst_rayt_8 )
      bbb = Dcst_rgasd_8/( Dcst_rayt_8*Dcst_rayt_8 )


*PDIR SERIAL
      call rpn_comm_xch_halo( F_tpt0 , LDIST_DIM,l_ni,l_nj,G_nk,
     $               G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo( F_qt0  , LDIST_DIM,l_ni,l_nj,G_nk,
     $               G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo( F_wijk1, LDIST_DIM,l_ni,l_nj,G_nk,
     $               G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      if (.not. Schm_hydro_L) then
        call rpn_comm_xch_halo( F_mut0 ,LDIST_DIM,l_ni,l_nj,G_nk,
     $               G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
        call rpn_comm_xch_halo( F_wijk2,LDIST_DIM,l_ni,l_nj,G_nk,
     $               G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      endif
      if (Cori_cornl_L) then
        call rpn_comm_xch_halo( F_ut0 ,LDIST_DIM,l_niu,l_nj,G_nk,
     $               G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
        call rpn_comm_xch_halo( F_vt0 ,LDIST_DIM,l_ni,l_njv,G_nk,
     $               G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      endif
*PDIR ENDSERIAL

*
*PDIR PARDO FOR=Ptopo_npeOpenMP
      do 100 k=1,l_nk


*     Compute Nu for hydrostatic version 
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
      i0 = 1
      in = l_niu
      j0 = 1+pil_s
      jn = l_nj-pil_n
      if (G_lam) then
         if (l_west) i0=1+pil_w
         if (l_east) in=l_niu-pil_e
      endif
*
      do j= j0, jn
      do i= i0, in
         w1 = ( 1. - intuv_c0xxu_8(i) ) * F_tpt0(i  ,j,k) 
     %             + intuv_c0xxu_8(i)   * F_tpt0(i+1,j,k)
         w2 = (   F_qt0(i+1,j,k) -   F_qt0(i,j,k) ) / geomg_hx_8(i)
         w3 = ( F_wijk1(i+1,j,k) - F_wijk1(i,j,k) ) / geomg_hx_8(i)
         F_nu(i,j,k) = bbb * ( w1*w2 + Cstv_tstr_8*w3 )
      end do
      end do
*
      if (Cori_cornl_L) then
*        Set indices for calculating wk2
         i00 = minx
         inn = maxx
         j00 = 1+pil_s
         jnn = l_njv
         if (G_lam) then
             if (l_west) i00 = 1+pil_w -2
             if (l_east) inn = l_niu-pil_e +3
c Don't change
             if (l_north)jnn = l_njv-pil_n +1
         else
             if (l_south) j00 = 3
             if (l_north) jnn = l_njv-1
         endif
*
         do j = j00, jnn
         do i = i00, inn
            wk2(i,j)  = inuvl_wyvy3_8(j,1) * F_vt0(i,j-2,k) 
     %                + inuvl_wyvy3_8(j,2) * F_vt0(i,j-1,k) 
     %                + inuvl_wyvy3_8(j,3) * F_vt0(i,j  ,k) 
     %                + inuvl_wyvy3_8(j,4) * F_vt0(i,j+1,k)          
         end do
         end do
*
         if (.not.G_lam) then 
            if (l_south) then
               do i = i00, inn
                  wk2(i,j00-2)= inuvl_wyvy3_8(j00-2,3)*F_vt0(i,j00-2,k) 
     %                       +  inuvl_wyvy3_8(j00-2,4)*F_vt0(i,j00-1,k) 
                  wk2(i,j00-1)= inuvl_wyvy3_8(j00-1,2)*F_vt0(i,j00-2,k) 
     %                        + inuvl_wyvy3_8(j00-1,3)*F_vt0(i,j00-1,k) 
     %                        + inuvl_wyvy3_8(j00-1,4)*F_vt0(i,j00  ,k) 
               end do
            endif
            if (l_north) then
               do i = i00, inn
                  wk2(i,jnn+2)= inuvl_wyvy3_8(jnn+2,1)*F_vt0(i,jnn  ,k) 
     %                        + inuvl_wyvy3_8(jnn+2,2)*F_vt0(i,jnn+1,k) 
                  wk2(i,jnn+1)= inuvl_wyvy3_8(jnn+1,1)*F_vt0(i,jnn-1,k) 
     %                        + inuvl_wyvy3_8(jnn+1,2)*F_vt0(i,jnn  ,k) 
     %                        + inuvl_wyvy3_8(jnn+1,3)*F_vt0(i,jnn+1,k) 
               end do
            endif
         endif
*
         do j= j0, jn
         do i= i0, in
            F_nu(i,j,k) = F_nu(i,j,k) - Cori_fcoru_8(i,j) *
     %     (inuvl_wxxu3_8(i,1)*wk2(i-1,j)+inuvl_wxxu3_8(i,2)*wk2(i  ,j)
     %    + inuvl_wxxu3_8(i,3)*wk2(i+1,j)+inuvl_wxxu3_8(i,4)*wk2(i+2,j))
         end do
         end do
      endif

*
*     Compute Nv for hydrostatic version 
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
      i0 = 1+pil_w
      in = l_ni-pil_e
      j0 = 1
      jn = l_njv
      if (G_lam) then
         if (l_south) j0=1+pil_s
         if (l_north) jn=l_njv-pil_n
      endif
      do j= j0, jn
      do i= i0, in
         w1 = ( 1. - intuv_c0yyv_8(j) ) * F_tpt0(i,j  ,k) 
     %             + intuv_c0yyv_8(j)   * F_tpt0(i,j+1,k)
         w2 = (F_qt0(i,j+1,k) - F_qt0(i,j,k))
     $        *geomg_cyv2_8(j)/geomg_hsy_8(j)
         w3 = (F_wijk1(i,j+1,k)-F_wijk1(i,j,k))*geomg_cyv2_8(j)
     $                                         /geomg_hsy_8 (j)
         F_nv(i,j,k) = bbb * ( w1*w2 + Cstv_tstr_8*w3 )
      end do
      end do
*
      if (Cori_cornl_L) then
*        Set indices for calculating wk2
         j00 = miny
         jnn = maxy
         i00 = 1+pil_w
         inn = l_niu
         if (G_lam) then
            if (l_south) j00=1+pil_s-2
            if (l_north) jnn=l_njv-pil_n+3
c Don't change
            if (l_east) inn = l_niu-pil_e +1
         endif
*
         do j = j00, jnn
         do i = i00, inn
            wk2(i,j)  = inuvl_wxux3_8(i,1)*F_ut0(i-2,j,k) 
     %                + inuvl_wxux3_8(i,2)*F_ut0(i-1,j,k) 
     %                + inuvl_wxux3_8(i,3)*F_ut0(i  ,j,k) 
     %                + inuvl_wxux3_8(i,4)*F_ut0(i+1,j,k) 
         end do
         end do
*
*        Set indices for calculating Nv
         if (.not.G_lam) then
            if (l_south) j0 = 2
            if (l_north) jn = l_njv-1
         endif
         do j = j0, jn
         do i = i0, in
            F_nv(i,j,k) =   F_nv(i,j,k) + Cori_fcorv_8(i,j) *
     %     (inuvl_wyyv3_8(j,1)*wk2(i,j-1)+inuvl_wyyv3_8(j,2)*wk2(i,j  )
     %    + inuvl_wyyv3_8(j,3)*wk2(i,j+1)+inuvl_wyyv3_8(j,4)*wk2(i,j+2))
         end do
         end do
*
         if (.not.G_lam) then
            if (l_south) then
               do i = i0, in
                  F_nv(i,1,k) = F_nv(i,1,k) + Cori_fcorv_8(i,1)
     %       * (inuvl_wyyv3_8(1,2)*wk2(i,1)+inuvl_wyyv3_8(1,3)*wk2(i,2) 
     %                                     +inuvl_wyyv3_8(1,4)*wk2(i,3))
               end do 
            endif 
*
            if (l_north) then
               do i = i0, in
                F_nv(i,l_njv,k)=F_nv(i,l_njv,k)+Cori_fcorv_8(i,l_njv) *
     %         (   inuvl_wyyv3_8(l_njv,1)*wk2(i,l_njv-1) 
     %           + inuvl_wyyv3_8(l_njv,2)*wk2(i,l_njv  ) 
     %           + inuvl_wyyv3_8(l_njv,3)*wk2(i,l_njv+1) )
               end do 
            endif 
         endif
      endif
*
*
      i0 = 1
      in = l_niu
      j0 = 1+pil_s
      jn = l_nj-pil_n
      if (G_lam) then
          if (l_west) i0 = 1+pil_w
          if (l_east) in = l_niu-pil_e
      endif
      i00 = 1+pil_w
      inn = l_ni-pil_e
      j00 = 1
      jnn = l_njv
      if (G_lam) then
          if (l_south) j00 = 1+pil_s
          if (l_north) jnn = l_njv-pil_n
      endif
      if (.not. Schm_hydro_L) then
*
*        Add nonhydrostatic contributions to Nu 
*        ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         do j= j0, jn
         do i= i0, in
            w1 = ( 1. - intuv_c0xxu_8(i) ) * F_mut0(i  ,j,k) 
     %                + intuv_c0xxu_8(i)   * F_mut0(i+1,j,k)
            w2 = ( F_wijk2(i+1,j,k) -F_wijk2 (i,j,k) ) / geomg_hx_8(i)
            F_nu(i,j,k) = F_nu(i,j,k) + aaa*w1*w2
         end do
         end do
*
*        Add nonhydrostatic contributions to Nv 
*        ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         do j= j00, jnn
         do i= i00, inn
            w1 = ( 1. - intuv_c0yyv_8(j) ) * F_mut0(i,j  ,k) 
     %                + intuv_c0yyv_8(j)   * F_mut0(i,j+1,k)
            w2 = (F_wijk2 (i,j+1,k) - F_wijk2(i,j,k) ) 
     %            *geomg_cyv2_8(j)/geomg_hsy_8(j)
            F_nv(i,j,k) = F_nv(i,j,k) + aaa*w1*w2
         end do
         end do

      endif

 100   continue

* For LAM, set  Nu,Nv values on the boundaries of the LAM grid
*
      if (G_lam) then
         if (l_west) then
             do k=1,l_nk
             do j=1+pil_s,l_nj-pil_n
                F_nu(pil_w,j,k) = 0.
             end do
             enddo
         endif
         if (l_east) then
            do k=1,l_nk
            do j=1+pil_s,l_nj-pil_n
               F_nu(l_ni-pil_e,j,k) = 0.
            end do
            enddo
         endif
         if (l_south) then
            do k=1,l_nk
            do i=1+pil_w,l_ni-pil_e
               F_nv(i,pil_s,k) = 0.
            end do
            enddo
         endif
         if (l_north) then
            do k=1,l_nk
            do i=1+pil_w,l_ni-pil_e
               F_nv(i,l_nj-pil_n,k) = 0.
            end do
            enddo
         endif
      endif

      i0=1
      in=l_ni
      j0=1
      jn=l_nj
      if (G_lam) then
         if (l_west)  i0 = 1+pil_w
         if (l_east)  in = l_ni-pil_e
         if (l_south) j0 = 1+pil_s
         if (l_north) jn = l_nj-pil_n
      endif

**************************************
* Combination of governing equations * 
**************************************

      gamma = one
      if (.not. Schm_hydro_L) then 
           eps =  Schm_nonhy_8 * Dcst_rgasd_8   * Cstv_tstr_8
     %         /( Dcst_cappa_8 * Dcst_grav_8**2 * Cstv_tau_8**2 )
         gamma =  one/( one + eps )
      endif 
      a1 = one/Cstv_tstr_8
      b1 = gamma/Cstv_tau_8 
      b2 = gamma/Cstv_tau_8/Dcst_cappa_8
*
*     compute Ncn
*     ~~~~~~~~~~~
*
      pd3 = one/Cstv_tau_8
      do k =  1, l_nk
      do j = j0, jn
      do i = i0, in
         pd1 = alog( 1. + geomg_dpib(k) * (exp(F_st0(i,j)) - 1. ))
         pd2 = geomg_dpib(k) * F_st0(i,j)
         F_ncn(i,j,k) = pd3 * ( pd1 - pd2 )
      enddo
      enddo
      enddo

*     Compute the nonlinear deviation of horizontal divergence 
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*
*PDIR SERIAL
      call rpn_comm_xch_halo( F_nu, LDIST_DIM,l_niu,l_nj,G_nk,
     $             G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo( F_nv, LDIST_DIM,l_ni,l_njv,G_nk,
     $             G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
*PDIR ENDSERIAL

*
*PDIR PARDO FOR=Ptopo_npeOpenMP
      do k=1,l_nk
*
         if (G_lam) then
             do j= j0,jn
             do i= i0,in
                F_n1(i,j,k) =  ( F_nu(i,j,k) - F_nu(i-1,j,k) ) 
     $               /( geomg_cy2_8(j)*geomg_hxu_8(i-1) )
     $               + ( F_nv(i,j,k) - F_nv(i,j-1,k) )/geomg_hsyv_8(j-1)
             end do
             end do
         else
             call caldiv_2 ( F_n1(minx,miny,k), F_nu(minx,miny,k), 
     $                       F_nv(minx,miny,k), LDIST_DIM, 1 )
         endif

         do j= j0, jn
         do i= i0, in
                     ccc  =log(one+F_tpt0(i,j,k)*a1)-F_tplt0(i,j,k)*a1
              F_nth(i,j,k)=(-Dcst_cappa_8*F_wijk1(i,j,k)+ccc)/Cstv_tau_8
              F_wijk1(i,j,k) = F_n1(i,j,k)  - pd3 * F_ncn(i,j,k)
              F_wijk2(i,j,k) = b2*F_nth(i,j,k) 
         end do
         end do
*
         if (.not. Schm_hydro_L) then
         do j= j0, jn
         do i= i0, in
            F_n3 (i,j,k) = ( F_multx(i,j,k) - F_mut0(i,j,k) )/Cstv_tau_8
            F_n3p(i,j,k) =  F_n3(i,j,k) - eps*F_nth(i,j,k)
            F_wijk1(i,j,k) = F_wijk1(i,j,k) +  b1*F_n3p(i,j,k)
            F_wijk2(i,j,k) = F_wijk2(i,j,k) +  b2*F_n3 (i,j,k)
         end do
         end do
         endif 
*
      end do
*
***********************************************
* The RHS of the nonlinear Helmholtz equation * 
***********************************************
*
      aaa = Dcst_rayt_8*Dcst_rayt_8
*
*PDIR PARDO FOR=Ptopo_npeOpenMP
      do 300 k=1,l_nk
*
      if ( k .eq. 1 ) then
*
         a2 = quarter*Geomg_hz_8(k)
         b2 = half*Geomg_z_8(k)
         b3 = half*Geomg_z_8(k+1)
         do j= j0, jn 
         do i= i0, in
            F_rheln(i,j,k) = aaa * (F_rhell(i,j,k)
     $                   - a2*( F_wijk1(i,j,k) + F_wijk1(i,j,k+1) )
     $                   + b2*F_wijk2(i,j,k)  + b3*F_wijk2(i,j,k+1))
         end do
         end do
*
      elseif ( k .eq. l_nk ) then
*
         a1 = quarter*Geomg_hz_8(k-1)
         b1 = half*Geomg_z_8(k-1)
         b2 = half*Geomg_z_8(k)
         do j= j0, jn
         do i= i0, in
            F_rheln(i,j,k) = aaa * (F_rhell(i,j,k)
     $                   - a1*( F_wijk1(i,j,k-1) + F_wijk1(i,j,k) )
     $                   - b1*F_wijk2(i,j,k-1)  - b2*F_wijk2(i,j,k))
         end do
         end do
*
      else
*
         a1 = quarter*Geomg_hz_8(k-1)
         a2 = quarter*Geomg_hz_8(k)
         b1 = half*Geomg_z_8(k-1)
         b3 = half*Geomg_z_8(k+1)
         do j= j0, jn
         do i= i0, in
            F_rheln(i,j,k) =  aaa * (F_rhell(i,j,k)
     $                   -  a1*( F_wijk1(i,j,k-1) + F_wijk1(i,j,k  ) )
     $                   -  a2*( F_wijk1(i,j,k  ) + F_wijk1(i,j,k+1) )
     $                   -  b1*F_wijk2(i,j,k-1) + b3*F_wijk2(i,j,k+1))
         end do
         end do
*
      endif
 300  continue
*
*     __________________________________________________________________
*
      return
      end

