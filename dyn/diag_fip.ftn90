!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer, 
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms 
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer 
!version 3 or (at your option) any later version that should be found at: 
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software; 
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), 
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
!**s/r diag_fip - Builds model (non-) hydrostatic perturbation and 
!     total geopotential from temperature and "surface pressure" for
!     vertically staggered variables as in Girard formulation 1.
!
#include "model_macros_f.h"
!
      subroutine diag_fip(F_fip,F_s,F_t,F_fis, &
                                        i0,in,j0,jn,nk)
!     
      implicit none
!     
      integer i0,in,j0,jn,nk
      real F_fip(LDIST_SHAPE,0:Nk+1)
      real F_s(LDIST_SHAPE), F_t(LDIST_SHAPE,Nk+2)
      real F_fis(LDIST_SHAPE)
!
!author
!
! Andre Plante july 2006.
!
!revision
!
! v4_00 - Plante & Girard   - Log-hydro-pressure coord on Charney-Phillips grid
!
!object
!       see id section
!
!arguments
!  Name        I/O                 Description
!----------------------------------------------------------------
! F_fip        O    - perturbation hydrostatic geopotential height
!                     Note the virtual levels!
! F_s          I    - log(pis/pistars)
! F_t          I    - temperature
! F_fis        I    - surface geopotential height
! i0,in,j0,jn  I    - index over which computation will be made.
!
!implicits
#include "lun.cdk"
#include "dcst.cdk"
#include "glb_ld.cdk"
#include "geomg.cdk"
#include "cstv.cdk"
#include "type.cdk"
#include "ver.cdk"
#include "schm.cdk"
#include "acid.cdk"
!     
!*
      integer i,j,k,i01,in1,j01,jn1
      real*8  one,pr1_8,yyy
!
      parameter( one = 1.d0 )
!     ---------------------------------------------------------------
!
      If (Lun_debug_L) write (Lun_out,1000)
      i01 = 1+acid_i0
      in1 = G_ni-acid_in
      j01 = 1+acid_j0
      jn1 = G_nj-acid_jn

      If (Acid_test_L) then
         If (Lun_out.gt.0) write(Lun_out,*)'Bef diag_fip'
         call glbstat (F_t  ,' T',LDIST_DIM,G_nk+2,i01,in1 ,j01,jn1 ,1,G_nk+1)
         call glbstat (F_fis,'ME',LDIST_DIM,1,i01,in1 ,j01,jn1 ,1,1)
         call glbstat (F_s  ,'S ',LDIST_DIM,1,i01,in1 ,j01,jn1 ,1,1)
      endif
      
      if( &
           i0.lt.1-G_halox   .or. &
           in.gt.l_ni+G_halox.or. &
           j0.lt.1-G_haloy   .or. &
           jn.gt.l_nj+G_haloy &
           )then
         call handle_error(-1,'diag_fip','diag_fi : dimension error')
      endif

!$omp parallel private (pr1_8,yyy)
!
!$omp do 
      do j=j0,jn
      do i=i0,in
         F_fip(i,j,G_nk+1)=F_fis(i,j)
      end do
      end do
!$omp enddo
!
!$omp do
!     Note that index on F_fip goes to zero.
      do j=j0,jn
         do k= G_nk+1,1,-1
         yyy=Cstv_RTstr_8*Ver_dz_8%t(k)/Cstv_Tstr_8
         do i=i0,in
            pr1_8=one+Ver_dbdz_8%t(k)*F_s(i,j)
            F_fip(i,j,k-1)=F_fip(i,j,k)+yyy*(F_t(i,j,k)*pr1_8-Cstv_Tstr_8)
         end do
         end do
      end do
!$omp enddo
!
!$omp end parallel
      If (Acid_test_L) then
          If (Lun_out.gt.0) write(Lun_out,*)'Aft diag_fip'
      call glbstat (F_fip,'FIP',LDIST_DIM,G_nk+2,i01,in1 ,j01,jn1 ,2,G_nk+2)
      endif
!
1000  format(3X,'COMPUTE FIP: (S/R DIAG_FIP)')
!
!     ---------------------------------------------------------------
!
      return
      end
