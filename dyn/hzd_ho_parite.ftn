***s/r hzd_ho_parite  - applies horizontal diffusion on a given 
*                       set of fields
*
#include "model_macros_f.h"
*
      subroutine hzd_ho_parite (    F_u,    F_v,  F_psd,  F_tp, F_ip,
     %                      F_td,    F_t,   F_it, F_plt,   
     %                     F_pip,    F_w,   F_qp,  F_mu, F_mul,  
     %                     F_xfis, DIST_DIM,Nk)
*
      implicit none
*
      integer DIST_DIM, Nk
*
      real     F_u(DIST_SHAPE,Nk),  F_v(DIST_SHAPE,Nk),
     %       F_psd(DIST_SHAPE,Nk), F_tp(DIST_SHAPE,Nk),
     %       F_ip(DIST_SHAPE,Nk), F_td(DIST_SHAPE,Nk),
     %         F_t(DIST_SHAPE,Nk), F_it(DIST_SHAPE,Nk),
     %       F_plt(DIST_SHAPE,Nk),  
     %       F_pip(DIST_SHAPE,Nk),  F_w(DIST_SHAPE,Nk),
     %        F_qp(DIST_SHAPE,Nk), F_mu(DIST_SHAPE,Nk),
     %       F_mul(DIST_SHAPE,Nk),  F_xfis(DIST_SHAPE)
*
*author
*     Abdessamad Qaddouri
*
*revision
* v3_01 - Qaddouri & Toviessi   - initial version
* v3_10 - Corbeil & Desgagne & Lee - AIXport+Opti+OpenMP
* v3_11 - Corbeil L.        - new RPNCOMM transpose
*
*object
* The diffusion includes: second order(Hzd_pwr=1), 
* fourth order(Hzd_pwr=2), sixth (Hzd_pwr=3) and 
* eightth order (Hzd_pwr=4) diffusion
*
*arguments
*  Name        I/O        Description
*----------------------------------------------------------------
*  F_u         I/0        see subroutine  
*  F_v         I/0            " 
*  F_ip        I/0            " 
*  F_td        I/0            "
*  F_tp        I/0            "
*  F_plt       I/0            "
*  F_t         I/0            "
*  F_pip       I/0            "
*  F_psd       I/0            "
*  F_w         I/0            "
*  F_mu        I/0            "
*  F_mul       I/0            "
*  F_qp        I/0            "
*----------------------------------------------------------------
* 
*implicits
#include "glb_ld.cdk"
#include "ldnh.cdk"
#include "dcst.cdk"
#include "cstv.cdk"
#include "trp.cdk"
#include "hzd.cdk"
#include "opr.cdk"
#include "schm.cdk"
#include "geomg.cdk"
#include "lun.cdk"
#include "ptopo.cdk"
*
      integer i, j, k, dpwr
      real*8 wk1_8
      real   wk2 (LDIST_SHAPE,Nk)
      integer NSTOR,nev,nev1
**
*     __________________________________________________________________
*
      dpwr = Hzd_pwr / 2

      nev= (G_ni+2)/2
      NSTOR = nev + ( 1 - mod(nev,2) )
      if (Lun_debug_L) write(Lun_out,1000) Hzd_pwr
*
      if ( Hzd_cdiff .gt. 0.0 ) then
*
*     Momentum
*     ~~~~~~~~
         nev1= (G_ni+1)/2
         call hzd_solparite_U(F_u, wk1_8, Hzd_evvec_8, Hzd_odvec_8,
     %                    Hzd_au_8,Hzd_cu_8,Hzd_deltau_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn, Hzd_xp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev1)
*
         call hzd_solparite_2(F_v, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_av_8,Hzd_cv_8, Hzd_deltav_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Hzd_yp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
*
*     Vertical motion in pressure coord. 
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         call hzd_solparite_2(F_psd, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
*
*     Total divergence 
*     ~~~~~~~~~~~~~~~~
         call hzd_solparite_2(F_td, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
*
         if (Hzd_uvwdt_L) goto 9988
*
*     Mass related fields
*     ~~~~~~~~~~~~~~~~~~~
         call hzd_solparite_2(F_ip, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8,Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
*
         call hzd_solparite_2(F_it, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8,Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
*
!$omp parallel
!$omp do
         do k = 2, Nk
         do j = 1,l_nj
         do i = 1,l_ni
            F_it(i,j,k) = F_ip(i,j,k) + Cstvr_fistr_8(k) + F_xfis(i,j)
         end do
         end do
         end do
!$omp enddo
*
* updating phi' at the top
*
!$omp do
         do j = 1,l_nj
         do i = 1,l_ni
            F_ip(i,j,1) = F_it(i,j,1) - Cstvr_fistr_8(1) - F_xfis(i,j)
         end do
         end do
!$omp enddo
*
!$omp end parallel
*     The temperature: T', T'lin & hence T 
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         call hzd_solparite_2(F_tp, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
*
         call hzd_solparite_2(F_plt, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
!$omp parallel
*
!$omp do
         do k=1, Nk
         do j=1,l_nj
         do i=1,l_ni 
            F_t(i,j,k) = F_tp(i,j,k) + Cstv_tstr_8
         end do
         end do
         end do
!$omp enddo
*
*     Save pi' for the diffusion of q'
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         if ( (.not.Schm_hydro_L) .and. Schm_difqp_L ) then
!$omp do
            do k = 1, Nk
            do j = 1,l_nj 
            do i = 1,l_ni 
               wk2(i,j,k) = F_pip(i,j,k)
            end do
            end do
            end do
!$omp enddo
         endif 
*
*     The hydrostatic pressure: pi'  
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
!$omp end parallel
         call hzd_solparite_2(F_pip, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)

*
*****************************
*  3. Nonyhydrostatic model * 
*****************************
*
         if ( .not. Schm_hydro_L ) then
*
*     Vertical wind (physical) 
*     ~~~~~~~~~~~~~~~~~~~~~~~~

         call hzd_solparite_2(F_w, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
*
         if ( Schm_difqp_L ) then ! q' & related variables
*

         call hzd_solparite_2(F_mu, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
*
         call hzd_solparite_2(F_mul, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
!$omp parallel
*
*        Indirect diffusion of q' 
*        ~~~~~~~~~~~~~~~~~~~~~~~~
!$omp do
         do k=1, Nk  ! 
         do j=1,l_nj ! p = (pi)exp(q') 
         do i=1,l_ni ! ~~~~~~~~~~~~~~~ 
            wk2(i,j,k) = ( geomg_z_8(k) + dble(wk2(i,j,k)) )
     %                       *exp( dble(F_qp(i,j,k)) )
         end do
         end do
         end do
!$omp enddo
*
!$omp end parallel
         call hzd_solparite_2(wk2, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
!$omp parallel
*
!$omp do
         do k=1, Nk  ! 
         do j=1,l_nj ! q' = log(p/pi) 
         do i=1,l_ni ! ~~~~~~~~~~~~~~
            F_qp(i,j,k) = log(             dble(wk2(i,j,k))
     %                       /( geomg_z_8(k) + dble(F_pip(i,j,k)) )  )
         end do
         end do
         end do
!$omp enddo
!$omp end parallel
*
         endif
         endif
*
      endif
*
 9988 continue
*
 1000 format(/,
     $     3X,'PERFORM DEL-',i1,' HORIZONTAL DIFFUSION: (S/R hzd_ho_parite)')
*     __________________________________________________________________
*
      return
      end
