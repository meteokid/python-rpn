***s/r dynstep -  Control of the dynamical timestep of the model
*
#include <model_macros_f.h>
*
      subroutine dynstep
*
      implicit none
*
*author 
*     Michel Roch - rpn - nov 1993
*
*revision
* v2_00 - Desgagne M.       - initial MPI version
* v2_10 - Tanguay M.        - introduce diffusion at T1 when 4D-Var 
* v2_11 - Desgagne M.       - vertical sponge layer
* v2_21 - Tanguay M.        - ADJ of vertical sponge layer
* v2_31 - Tanguay M.        - adapt ADJ to diffusion in gem_run 
* v3_00 - Desgagne & Lee    - Lam configuration
* v3_03 - Tanguay M.        - Adjoint Lam configuration
* v3_11 - Gravel S.         - provide for variable topography
*
*object
*
*arguments
*       none
*
*implicits
#include "glb_ld.cdk"
#include "lun.cdk"
#include "orh.cdk"
#include "init.cdk"
#include "lctl.cdk"
#include "rstr.cdk"
#include "schm.cdk"
#include "pilot.cdk"
#include "step.cdk"
#include "vtopo.cdk"
*
      integer itraj
      logical first_L, secon_L
*
      logical done_intt_L
      data done_intt_L /.false./
      save done_intt_L
**
*     ---------------------------------------------------------------
      if (Lun_debug_L) write(Lun_out,1000)
*
*     first_L is TRUE  for the first timestep
*           or the first timestep after digital filter initialisation
*
      first_L = (Lctl_step.eq.1).or.(Rstri_idon_L .and. 
     $           Lctl_step.eq.(Init_dfnp+1)/2)
*
*     secon_L is TRUE  for the second timestep
*            or the second timestep after digital filter initialisation
*
      secon_L = (Lctl_step.eq.2).or.(Rstri_idon_L .and. 
     $           Lctl_step.eq.(Init_dfnp+1)/2 + 1)
*
*     Orh_crank_L is TRUE for a time step using C-N procedure
*
      Orh_crank_L= first_L .or. ( secon_L .and. ( Schm_xwvt3 .gt. 0 )) 
     $                     .or. ( mod(Lctl_step-1,Schm_modcn) .eq. 0 )
*
*         -------------------------------------------------------
*C	  Conditionnal:
*C
*C           1) set the positions at th as the positions of points grids
*C           2) iteration number for trajectory computation
*C
*         --------------------------------------------------------
*
      itraj = Schm_itraj
*
      if ( first_L) then
         call pospers( )
         itraj = max( 5, Schm_itraj )
      endif
*
*        ------------------------------------------------------------
*C       Compute 3D winds at time t0-dt/2 (th)
*        ------------------------------------------------------------
*
      if ( G_lam .and. .not.Pil_ctebcs_L ) call nest_intt (done_intt_L) 
*
      if ( Orh_crank_L ) then
*
*C          DUMMY TIME STEPS AND WIND INTERPOLATION
*
*          first guess for winds and positions at th
*
         call wdpers( )
*
         if (Lun_debug_L) write(Lun_out,1005) Schm_itcn-1
         do Orh_icn = 1,Schm_itcn-1
*
*C	        Perform dummy timestep
*
            call tstpdyn( itraj)
*
*C	        Compute the average of the t1 and t0 winds
*
            call wndth( )
*
            itraj = Schm_itraj
*
         end do
         if (Lun_debug_L) write(Lun_out,1006)
*
         Orh_icn=Schm_itcn
*     
      else
*     
         Orh_icn = 1
*
      endif
*
*        ------------------------------------------------------------
*C        Perform the timestep
*        ------------------------------------------------------------
*
      call tstpdyn ( Schm_itraj )
      if (G_lam) call glb_bound ()
*
*         ------------------------------------------------------------
*C	  When the timestep is completed, rename all the
*C        variables at time level t1 -> t0 and rename all the
*C        variables at time level t0 -> t1 for the next timestep
*         ------------------------------------------------------------
*
      call t12t2( )
      call t02t1( )
      call t22t0( )

*
*         ------------------------------------------------------------
*C        If necessary vary topogragphy according to Vtopo_start and 
*C        Vtopo_ndt
*         ------------------------------------------------------------
      if (Vtopo_L .and. (Lctl_step .ge. Vtopo_start)) then
          call var_topo( )
          if ( Schm_phyms_L ) call phycom ('varmtn' ,1 ,1,'set')
      else
          if ( Schm_phyms_L ) call phycom ('varmtn' ,0 ,1,'set')
      endif

*
*     ===================
*     END OF ONE TIMESTEP
*     ===================
*
      if ( G_lam.and.(Lctl_step.eq.Step_total) ) done_intt_L = .true.
*
 1000 format(
     +/,'CONTROL OF DYNAMICAL STEP: (S/R DYNSTEP)',
     +/,'========================================'/)
 1005 format(
     $/3X,'##### Crank-Nicholson iterations: ===> PERFORMING',I3,
     $    ' timestep(s) #####'/)
 1006 format(
     $/3X,'##### Crank-Nicholson iterations: ===> DONE... #####'/)
*
      return
      end
