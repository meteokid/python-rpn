***s/r v4d_getdx - Read increments from 3D-Var and prepare them for GEM 
*
#include <model_macros_f.h>
*
      subroutine v4d_getdx(kstatus)
*
      use v4d_prof, only: Pr_mode_S, Pr_llfrm_L, Pr_dsnooze_8
*
#include "impnone.cdk"
*
      integer, intent(inout):: kstatus
*
*author
*     P. Gauthier
*
*revision
* v3_00 - P. Gauthier        - initial MPI version
* v3_00 - M. Tanguay         - add v4d_gauss2gem_ad/Simon's exchange
*
*object
*     -----------------------------------------------------------
*     1) Proc0: Read increments from 3D-Var
*     2) Proc0: Transfert from Gaussian grid to GEM scalar grid
*     3) All processors: Conversion from 3D-Var units to 
*        GEM units and Staggering
*     -----------------------------------------------------------
*
*arguments
* Name         I/O                 Description
*----------------------------------------------------------------
* kstatus      I                   Status of the job
*----------------------------------------------------------------
*
*implicits
#include "glb_ld.cdk"
#include "lun.cdk"
#include "v4dg.cdk"
#include "vt1.cdk"
#include "vt1m.cdk"
#include "ptopo.cdk"
#include "tr3d.cdk"
#include "path.cdk"
#include <prof_f.h>
*
*     Local variables
*     ---------------
      integer idim(2),istat,ihdlin,icount,nvar,jlon,jlat,jlev,
     %        ierr,pnlkey1(9),err,pnerr,nigauss,njgauss,status
*
      real*8,pointer    :: dlbuff_8(:,:),dlbuff2d_8(:)
      real,  allocatable:: zbuff(:,:,:),zbuff2d(:,:)
      real,  allocatable::  gut1(:,:,:),gvt1(:,:,:),gtpt1(:,:,:),
     %                     ghut1(:,:,:),gst1(:,:)
*
      character*256 pathdwgf_S
*     
      integer  vmmlod,vmmget,vmmuld,fnom,prof_rdrec,longueur
      external vmmlod,vmmget,vmmuld,fnom,prof_rdrec,longueur
*
      integer key1(Tr3d_ntr), key1_, key1m(Tr3d_ntr), key1m_, n 
      real hut1, hut1m
      pointer (pahu1, hut1(LDIST_SHAPE,*)), (pahu1m, hut1m(LDIST_SHAPE,*))
*
      logical plpr_L
*
      integer i,j,k
*     ______________________________________________________
*
      if ( Tr3d_ntr.gt.1 ) call gefstop('v4d_getdx')
*     ______________________________________________________
*
      write(Lun_out,1000)
*
*     Nullify pointers for prof_gvar
*     ------------------------------
      nullify (dlbuff_8,dlbuff2d_8)
*
*     Flag for diagnostics
*     --------------------
      plpr_L=.false.
*
      if(Ptopo_myproc.eq.0) then
         if(.not.allocated(gut1 )) allocate(gut1 (G_ni,G_nj,G_nk), STAT=status)
         if(.not.allocated(gvt1 )) allocate(gvt1 (G_ni,G_nj,G_nk), STAT=status)
         if(.not.allocated(gtpt1)) allocate(gtpt1(G_ni,G_nj,G_nk), STAT=status)
         if(.not.allocated(ghut1)) allocate(ghut1(G_ni,G_nj,G_nk), STAT=status)
         if(.not.allocated(gst1 )) allocate(gst1 (G_ni,G_nj)     , STAT=status)
      endif
*
*     Get fields in memory
*     --------------------
      pnlkey1(1)= VMM_KEY( ut1)
      pnlkey1(2)= VMM_KEY( vt1)
      pnlkey1(3)= VMM_KEY( tpt1)
      pnlkey1(4)= VMM_KEY( st1)
      nvar= 4 
*
      err = vmmlod(pnlkey1,nvar)
*
      err = VMM_GET_VAR( ut1)
      err = VMM_GET_VAR( vt1)
      err = VMM_GET_VAR( tpt1)
      err = VMM_GET_VAR( st1)
*
*     Load humidity field assuming Tr3d_ntr=1
*     ---------------------------------------
      key1_ = VMM_KEY (trt1)
      do n=1,Tr3d_ntr
         key1(n) = key1_ + n
      end do
      if (Tr3d_ntr.gt.0) then
         err = vmmlod(key1,Tr3d_ntr)
         do n=1,Tr3d_ntr
            err = vmmget(key1(n),pahu1,hut1)
         end do
      endif
*     
      kstatus = 0
*
*     ---------------------------
*     Read increments from 3D-Var
*     ---------------------------
      if(Ptopo_myproc.eq.0) then
*
*     1. Open dwgf PROF file
*        -------------------
         write(Lun_out,*) 'Opening file dwgf PROF file'
*
         pathdwgf_S = Path_xchg_S(1:longueur(Path_xchg_S))//'/dwgf.prof'
         ihdlin = prof_open(pathdwgf_S,'READ',Pr_mode_S,Pr_dsnooze_8)
*
*     2. Read Data record (3D-fields)
*        ----------------------------
         if(ihdlin.le.0) then
            write(Lun_out,*) 'Problem with dwgf PROF file'
            kstatus = -99
         else
            write(Lun_out,*) 'Accessing first record with 3D fields...'
            istat = prof_rdrec(ihdlin)
            if (istat.ne.0) kstatus = -99 
         end if
*
         if(kstatus.ne.0) goto 1001
*     
*     3. Get first dynamical field (UU)
*        ------------------------------
         call v4d_getfld('UU', kstatus)
*
      end if
*
 1001 call rpn_comm_bcast(kstatus,1,"MPI_INTEGER",0,"GRID",ierr)
*     
      if(kstatus.ne.0) return 
*
      if(Ptopo_myproc.eq.0) gut1(:,:,:) = zbuff(:,:,:) 
*     
*     4. Get all the other 3D dynamical fields
*        -------------------------------------
*
*        V component of winds
*        --------------------
         if(Ptopo_myproc.eq.0) call v4d_getfld('VV',kstatus)
*     
         call rpn_comm_bcast(kstatus,1,"MPI_INTEGER",0,"GRID",ierr)
*     
         if(kstatus.ne.0) return
*
         if(Ptopo_myproc.eq.0) gvt1(:,:,:) = zbuff(:,:,:) 
*     
*        Temperature
*        -----------
         if(Ptopo_myproc.eq.0) call v4d_getfld('TT',kstatus) 
*     
         call rpn_comm_bcast(kstatus,1,"MPI_INTEGER",0,"GRID",ierr)
*     
         if(kstatus.ne.0) return
*
         if(Ptopo_myproc.eq.0) gtpt1(:,:,:) = zbuff(:,:,:)
*     
*        Specific humidity
*        -----------------
         if(Ptopo_myproc.eq.0) call v4d_getfld('HU',kstatus) 
*     
         call rpn_comm_bcast(kstatus,1,"MPI_INTEGER",0,"GRID",ierr)
*     
         if(kstatus.ne.0) return
*
         if(Ptopo_myproc.eq.0) ghut1(:,:,:) = zbuff(:,:,:) 
*
*     5. Get all 2D dynamical fields
*        ---------------------------
*     
*        Surface pressure
*        ----------------
         if(Ptopo_myproc.eq.0) then
*
            if(associated(dlbuff_8)) deallocate(dlbuff_8)
            if(allocated ( zbuff  )) deallocate( zbuff  )
*
            write(Lun_out,*) 'Accessing second record with 2D fields...'
            istat = prof_rdrec(ihdlin)
            if (istat.ne.0) kstatus = -99 
*
            if(kstatus.ne.0) goto 1002
*
            call v4d_getfld('PS',kstatus)
*
         end if
*     
 1002    call rpn_comm_bcast(kstatus,1,"MPI_INTEGER",0,"GRID",ierr)
*     
         if(kstatus.ne.0) return
*
         if(Ptopo_myproc.eq.0) gst1(:,:) = zbuff2d(:,:) 
*
*     6. Close dwgf PROF file
*        --------------------
         if(Ptopo_myproc.eq.0) then
*
            if(associated(dlbuff2d_8))deallocate(dlbuff2d_8)
            if(allocated ( zbuff2d  ))deallocate( zbuff2d  )
*
            write(Lun_out,*) 'Closing file dwgf PROF file'
            istat = prof_close(ihdlin,Pr_llfrm_L)
            if (istat.ne.0) kstatus = -99 
*
         end if
*
         call rpn_comm_bcast(kstatus,1,"MPI_INTEGER",0,"GRID",ierr)
*
         if(kstatus.ne.0) return 
*
*     -----------------------------------------------
*     Transfert from Gaussian grid to GEM scalar grid
*     -----------------------------------------------
*
*     ---------------------------------------------------------------------------
*     CAUTION: We assume Gaussian grid has the same resolution as GEM scalar grid 
*     ---------------------------------------------------------------------------
      nigauss = G_ni
      njgauss = G_nj
      call v4d_gauss2gem( ut1, vt1, tpt1, hut1, st1, LDIST_DIM,
     %                    gut1,gvt1,gtpt1,ghut1,gst1,nigauss,njgauss,G_nk)
*
      if(plpr_L) then
         if(Ptopo_myproc.eq.0) write(Lun_out,*) 'AFTER GAUSS2GEM'
         call glbstat(ut1 ,'UU',LDIST_DIM,G_nk,1,G_ni,1,G_nj,1,G_nk)
         call glbstat(vt1 ,'VV',LDIST_DIM,G_nk,1,G_ni,1,G_nj-1,1,G_nk)
         call glbstat(tpt1,'TP',LDIST_DIM,G_nk,1,G_ni,1,G_nj,1,G_nk)
         call glbstat(st1 ,'4S',LDIST_DIM,   1,1,G_ni,1,G_nj,1,   1)
         call glbstat(hut1,'HU',LDIST_DIM,G_nk,1,G_ni,1,G_nj,1,G_nk)
         if(Ptopo_myproc.eq.0) write(Lun_out,*) '-----------------------'
      endif
*
      if(Ptopo_myproc.eq.0) then
         if(allocated (gut1      )) deallocate(gut1    )
         if(allocated (gvt1      )) deallocate(gvt1    )
         if(allocated (gtpt1     )) deallocate(gtpt1   )
         if(allocated (ghut1     )) deallocate(ghut1   )
         if(allocated (gst1      )) deallocate(gst1    )
      endif
*
*     --------------------------------------------------------
*     Conversion from 3D-Var units to GEM units and Staggering 
*     --------------------------------------------------------
      if(V4dg_tl_L) then
*
         pnlkey1(1)= VMM_KEY(tpt1m)
         pnlkey1(2)= VMM_KEY( st1m)
         nvar= 2 
         err = vmmlod(pnlkey1,nvar)
*
         err = VMM_GET_VAR( tpt1m)
         err = VMM_GET_VAR( st1m)
*
*        Load TRAJ humidity field assuming Tr3d_ntr=1
*        --------------------------------------------
         key1m_ = VMM_KEY (trt1m)
         do n=1,Tr3d_ntr
            key1m(n) = key1m_ + n
         end do
         if (Tr3d_ntr.gt.0) then
            err = vmmlod(key1m,Tr3d_ntr)
            do n=1,Tr3d_ntr
            err = vmmget(key1m(n),pahu1m,hut1m)
            end do
         endif
*
      end if
*
*     Direct (nonlinear)
*     ------------------ 
      if(V4dg_di_L) then 
         call v4d_varconv(ut1,vt1,tpt1,hut1,st1,LDIST_DIM,l_nk,.TRUE.)
*     
*     TLM 
*     ---
      elseif(V4dg_tl_L) then
         call v4d_varconv_tl(ut1,vt1,tpt1,hut1,st1,
     $                       tpt1m,hut1m,st1m,LDIST_DIM,l_nk,.TRUE.)
      end if
*
      if(plpr_L) then
         if(Ptopo_myproc.eq.0) write(Lun_out,*) 'AFTER VARCONV'
         call glbstat(ut1 ,'UU',LDIST_DIM,G_nk,1,G_ni,1,G_nj,1,G_nk)
         call glbstat(vt1 ,'VV',LDIST_DIM,G_nk,1,G_ni,1,G_nj-1,1,G_nk)
         call glbstat(tpt1,'TP',LDIST_DIM,G_nk,1,G_ni,1,G_nj,1,G_nk)
         call glbstat(st1 ,'4S',LDIST_DIM,   1,1,G_ni,1,G_nj,1,   1)
         call glbstat(hut1,'HU',LDIST_DIM,G_nk,1,G_ni,1,G_nj,1,G_nk)
         if(Ptopo_myproc.eq.0) write(Lun_out,*) '-----------------------'
      endif
*
      pnerr = vmmuld(-1,0)
*
      if(Lun_out.gt.0) write(Lun_out,1003) kstatus
*
 1000 format(/,'V4D_GETDX: Read Model state sent by 3D-Var',
     +       /,'==========================================')
 1003 format(/,'V4D_GETDX: Model state received from 3D-Var --- Status = ',I8,
     +       /,'======================================================')
*
      return
*
*     Host subroutine
*     ---------------
      contains
      subroutine v4d_getfld(cdvar,kstatus)
*
#include "impnone.cdk"
*
      character*2, intent(in):: cdvar
      integer, intent(inout) :: kstatus
*
*author
*     P. Gauthier
*
*revision
* v3_00 - P. Gauthier        - initial MPI version
*
*object
*
*arguments
* Name         I/O                 Description
*----------------------------------------------------------------
* cdvar        I                   Type of profile
* kstatus      I                   Status of the job
*----------------------------------------------------------------
*
*     Read 3D field if request 
*     ------------------------
      select case(cdvar)
      case('UU')
         istat = prof_gvar(ihdlin,dlbuff_8,V3D_UTRU)
      case('VV')
         istat = prof_gvar(ihdlin,dlbuff_8,V3D_VTRU)
      case('TT')
         istat = prof_gvar(ihdlin,dlbuff_8,V3D_TEMP)
      case('HU')
         istat = prof_gvar(ihdlin,dlbuff_8,V3D_SPHU)
*
*     Read 2D field if request 
*     ------------------------
      case('PS')
         istat = prof_gvar(ihdlin,dlbuff2d_8,V2D_PSUR)
      end select
*     
*     Change accuracy and reverse latitude if 3D field   
*     ------------------------------------------------
      select case(cdvar)
      case('UU','VV','TT','HU')
         idim = ubound(dlbuff_8)
         write(Lun_out,*)' - Dimension of ',cdvar,' field... ',idim
         if(istat.ne.0) then
            write(Lun_out,*)'Problem in getting ',cdvar,' field...'
            kstatus = -99
         else
            if(idim(1).ne.G_ni*G_nj.or.idim(2).ne.G_nk) then
               kstatus = -99
            else
*     
*              Transfer real*8 to real
*              -----------------------
               if(.not.allocated(zbuff))allocate(zbuff(G_ni,G_nj,G_nk))
               zbuff(:,:,:) = 0.
               do jlev = 1, G_nk
                  icount = 0
                  do jlat = 1,G_nj
                     do jlon = 1,G_ni
                        icount = icount+1
                        zbuff(jlon,G_nj -jlat+1,jlev) = dlbuff_8(icount,jlev) 
                     end do
                  end do
               end do
            end if
         end if
*
*     Change accuracy and reverse latitude if 2D field   
*     ------------------------------------------------
      case('PS')
         if (istat.ne.0) then
            write(Lun_out,*)'Problem in getting ',cdvar
            kstatus = -99
         else
            write(Lun_out,*)' - Dimension of ',cdvar,' field... ',ubound(dlbuff2d_8)
*
*              Transfer real*8 to real
*              -----------------------
               if(.not.allocated(zbuff2d)) allocate(zbuff2d(G_ni,G_nj))
               zbuff2d(:,:) = 0.
               icount = 0
                  do jlat = 1,G_nj
                     do jlon = 1,G_ni
                        icount = icount+1
                        zbuff2d(jlon,G_nj -jlat+1) = dlbuff2d_8(icount) 
                     end do
                  end do
         end if
      end select
*
      end subroutine v4d_getfld
      end subroutine v4d_getdx
