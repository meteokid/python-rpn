***s/r e_intwind - Wind components horizontal interpolation
*
#include "model_macros_f.h"
*
      subroutine e_intwind 
      implicit none
*
*author       M ROCH     - july 95 - from intvent
*
*revision
* v2_30 - Sandrine Edouard  - adapt for vertical hybrid coordinate
* v2_30 - L. Corbeil        - replaced ecriseq by BMF stuff, 
* v2_30                       removed vertical interpolation
* v2_31 - M. Desgagne       - removed toppu,toppv from calling 
* v2_31                       sequence and corrected date/time recording
* v3_00 - Desgagne & Lee    - Lam configuration
* v3_01 - Lee V.            - new ip1 encoding (kind=5 -- unnormalized)
*
*object
*        see above ID
*
*ARGUMENTS
*
*IMPLICITS
#include "e_option.cdk"
#include "e_fu.cdk"
#include "e_anal.cdk"
#include "e_grids.cdk"
#include "e_cdate.cdk"
#include "e_topo.cdk"
#include "dcst.cdk"
#include "grd.cdk"
#include "bmf.cdk"
#include "hgc.cdk"
#include "e_schm.cdk"
*
      integer  ezqkdef,ezdefset,ezsetopt,ezuvint,
     $         fstinf,fstlir,fstprm,e_rdhint3
      external ezqkdef,ezdefset,ezsetopt,ezuvint,
     $         fstinf,fstlir,fstprm,e_rdhint3
*
      integer i, j, k, src_gid, key1, key2, nic, njc, ni1, nj1, 
     $        nk1,nkc,err,iu,ju,iv,jv,nu,nv
      integer ip2, ip3
      integer dte, det, ipas, p1, p2, p3, g1, g2, g3, g4, bit,
     $        dty, swa, lng, dlf, ubc, ex1, ex2, ex3
      character*1 typ,grd
      character*4 var,var_uu,var_vv
      character*12 lab
      logical flag_ut1
      real uu(niu*nju),vw(niu*nju),vv(niv*njv),uw(niv*njv),
     $     ttu(niu*nju),huu(niu*nju),ttv(niv*njv),huv(niv*njv),
     $     uvw(niu*nju),p0u(niu*nju),p0v(niv*njv),c1
      real, dimension (:), allocatable :: w1,w2
*
* ---------------------------------------------------------------------
*
      nu = niu*nju
      nv = niv*njv
*
      if (anal_hav(1).eq.0) then
*
* ---------------------------------------------------------------------
*        NO INTERPOLATION REQUIRED
*        ANALYSIS and MODEL HAVE SAME GRID,SAME LEVELS,
*                                SAME TOPOGRAPHY, TOP PRESSURE
*---------------------------------------------------------------------
*        
         if (LAM) then
*
            if (e_schm_offline_l) write (6,199)
            do k=1,lv               
               key1 = fstlir(uu,e_fu_anal,iu,ju,nkc,datev,' ',na(k),
     $                                         ip2a,ip3a,tva,'UT1')
               if (key1.lt.0 .or. iu.ne.niu .or. ju.ne.nju) then
                   write(6,*)'e_intwind: UT1  NOT AVAILABLE'
                   goto 55
               endif
               key1 = fstlir(vv,e_fu_anal,iv,jv,nkc,datev,' ',na(k),
     $                                         ip2a,ip3a,tva,'VT1')
               if (key1.lt.0 .or. iv.ne.niv .or. jv.ne.njv) then
                  write(6,*)'e_intwind: VT1  NOT AVAILABLE'
                  goto 55
               endif
               do i=1,niu*nju
                  uu(i) = uu(i) / dcst_knams_8
               end do
               do i=1,niv*njv
                  vv(i) = vv(i) / dcst_knams_8
               end do
               if (e_schm_offline_l) then
                  write (6,120) 'U WIND  ','UU',0,0,1,'A',' CUBIC '
                  write (6,120) 'V WIND  ','VV',0,0,1,'A',' CUBIC '
               endif
               call e_bmfsplitxy2 (uu,niu,nju,'UU  ',k,lv,pniu,0,0,0)
               call e_bmfsplitxy2 (vv,niv,njv,'VV  ',k,lv,pni ,0,0,0)
            end do
*
         else
*
            flag_ut1 = .false.
            var_uu = 'UU  '
            var_vv = 'VV  '
            iu = nifi
            ju = njfi
            iv = iu
            jv = ju
            key1 = fstinf (e_fu_anal,nic,njc,nkc,datev,' ',na(1),
     $                                      ip2a,ip3a,' ',var_uu)
            key2 = fstinf (e_fu_anal,nic,njc,nkc,datev,' ',na(1),
     $                                      ip2a,ip3a,' ',var_vv)
            if ((key1.lt.0).or.(key2.lt.0)) then
               flag_ut1 = .true.
               var_uu = 'UT1 '
               var_vv = 'VT1 '
               iu = niu
               ju = nju
               iv = niv
               jv = njv
               key1 = fstinf (e_fu_anal,nic,njc,nkc,datev,' ',na(1),
     $                                         ip2a,ip3a,' ',var_uu)
               key2 = fstinf (e_fu_anal,nic,njc,nkc,datev,' ',na(1),
     $                                         ip2a,ip3a,' ',var_vv)
            endif
*
            if ((key1.lt.0).or.(key2.lt.0)) then
               write (6,*) 'UU  and/or VV NOT AVAILABLE'
               call e_arret( 'e_intwind' )
            endif
            err = fstprm (key1, DTE, DET, IPAS, ni1, nj1, nk1, BIT, DTY,
     $              P1, P2, P3, TYP, VAR, LAB, GRD, G1, G2, G3, G4, SWA,
     $              LNG, DLF, UBC, EX1, EX2, EX3)
*
            do k=1,lv
               key1 = fstlir (uu, e_fu_anal, i, j, nkc, datev, labanl, 
     $                                  na(k), ip2a,ip3a, tva, var_uu)
               if (key1.lt.0 .or. i.ne.iu  .or. j.ne.ju) then
                  write(6,*)'ERROR: UU NOT AVAILABLE,'
                  call e_arret( 'e_intwind' )
               endif
               key1 = fstlir (vw, e_fu_anal, i, j, nkc, datev, labanl, 
     $                                  na(k), ip2a,ip3a, tva, var_vv)
               if (key1.lt.0 .or. i.ne.iv  .or. j.ne.jv) then
                  write(6,*)'ERROR: VV NOT AVAILABLE,'
                  call e_arret( 'e_intwind' )
               endif
*
               if (e_schm_offline_l) then
                  write (6,120) 'U WIND  ','UU',0,0,1,'A',' CUBIC '
                  write (6,120) 'V WIND  ','VV',0,0,1,'A',' CUBIC '
               endif
*
               if (.not. flag_ut1) then
                  call e_arak (uu, vv, vw, uvw, niu, njfi, nju, njv, 1)
               else
                  do i=1,niu*nju
                     uu(i) = uu(i) / dcst_knams_8
                  end do
                  do i=1,niv*njv
                     vv(i) = vw(i) / dcst_knams_8
                  end do
               endif
               call e_bmfsplitxy2 (uu,niu,nju,'UU  ',k,lv,pniu,0,0,0)
               call e_bmfsplitxy2 (vv,niv,njv,'VV  ',k,lv,pni ,0,0,0)
            end do
*
         endif
*
*---------------------------------------------------------------------
*
*     INTERPOLATION REQUIRED
*
*---------------------------------------------------------------------
*
      else
*
      ip2 = ip2a
      ip3 = ip3a
      if (glecmanl) ip2 = -1
      if (glecmanl) ip3 = int(rna(1))
*
         key1=fstinf(e_fu_anal,nic,njc,nkc,datev,' ',na(1),ip2,ip3,
     $                                                     ' ','UU')
         err= fstprm (key1, DTE, DET, IPAS, ni1, nj1, nk1, BIT, DTY, P1,
     $                P2, P3, TYP, VAR, LAB, GRD, G1, G2, G3, G4, SWA,
     $                LNG, DLF, UBC, EX1, EX2, EX3)
         src_gid = ezqkdef (nic, njc, GRD, g1, g2, g3, g4, e_fu_anal)
         err = ezsetopt ('INTERP_DEGREE', 'CUBIC')
         allocate (w1(nic*njc),w2(nic*njc))
         do k=1,lv
*
            ip2 = ip2a
            ip3 = ip3a
            if (glecmanl) ip2 = -1
            if (glecmanl) ip3 = int(rna(k))
            key1 = fstlir (w1, e_fu_anal, iu, ju, nkc, datev, ' ', 
     $                               na(k), ip2, ip3, ' ', 'UU')
            key2 = fstlir (w2, e_fu_anal, iv, jv, nkc, datev, ' ', 
     $                               na(k), ip2, ip3, ' ', 'VV')
            if (key1.lt.0 .or. iu.ne.nic  .or. ju.ne.njc ) then
               write(6,*)'ERROR: UU NOT AVAILABLE,'
               goto 55
            endif
            if (key1.lt.0 .or. iv.ne.nic  .or. jv.ne.njc ) then
               write(6,*)'ERROR: VV NOT AVAILABLE,'
               goto 55
            endif
*
*         Horizontal Interpolation on U grid and V grids
*
            err = ezdefset ( dstu_gid, src_gid )
            err = ezuvint  ( uu,vw,w1,w2 )
            err = ezdefset ( dstv_gid, src_gid )
            err = ezuvint  ( uw,vv,w1,w2 )
*
            if (e_schm_offline_l) then
               write (6,120) 'U WIND  ','UU',0,0,1,'A',' CUBIC '
               write (6,120) 'V WIND  ','VV',0,0,1,'A',' CUBIC '
            endif
*
            call e_bmfsplitxy2 (uu,niu,nju,'UU  ',k,lv,pniu,0,0,0)
            call e_bmfsplitxy2 (vv,niv,njv,'VV  ',k,lv,pni ,0,0,0)
*
         end do
*
        if (glecmanl) then
*
*       also treat 2m winds (to be used as surface values)
*       store as US and VS
*
            key1 = fstlir (w1, e_fu_anal, iu, ju, nkc, datev, ' ', 
     $                               -1, -1, -1, ' ', 'US')
            key2 = fstlir (w2, e_fu_anal, iv, jv, nkc, datev, ' ', 
     $                               -1, -1, -1, ' ', 'VS')
            if (key1.lt.0 .or. iu.ne.nic  .or. ju.ne.njc ) then
               write(6,*)'ERROR: US NOT AVAILABLE,'
               goto 55
            endif
            if (key1.lt.0 .or. iv.ne.nic  .or. jv.ne.njc ) then
               write(6,*)'ERROR: VS NOT AVAILABLE,'
               goto 55
            endif
*
*         Horizontal Interpolation on U grid and V grids
*
            err = ezdefset ( dstu_gid, src_gid )
            err = ezuvint  ( uu,vw,w1,w2 )
            err = ezdefset ( dstv_gid, src_gid )
            err = ezuvint  ( uw,vv,w1,w2 )
*
            call e_bmfsplitxy2 (uu,niu,nju,'US  ',1,1,pniu,0,0,0)
            call e_bmfsplitxy2 (vv,niv,njv,'VS  ',1,1,pni ,0,0,0)
*
         endif
         deallocate (w1,w2)
*
*---------------------------------------------------------------------
      endif
*---------------------------------------------------------------------
*
      if ( gletaanl .or. glsiganl .or. glhybanl ) then
         write(6,*)'PREPARATION FOR SIGMA/ETA/HYB to HYBRID'
         c1 = 10. * Dcst_grav_8
         if (.not.e_schm_offline_l) then
         if (e_rdhint3 (ttu,dstu_gid,niu,nju,'GZ  ',na(lv),ip2a,ip3a,
     $         ' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
         if (e_rdhint3 (ttv,dstv_gid,niv,njv,'GZ  ',na(lv),ip2a,ip3a,
     $         ' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
         endif
         if (e_rdhint3 (p0u,dstu_gid,niu,nju,'P0  ',   0 ,ip2a,ip3a,
     $         ' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
         if (e_rdhint3 (p0v,dstv_gid,niv,njv,'P0  ',   0 ,ip2a,ip3a,
     $         ' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
         do i=1,nu
            ttu(i) = ttu(i) * c1
            p0u(i) = p0u(i) * 100.
         enddo
         do i=1,nv
            ttv(i) = ttv(i) * c1
            p0v(i) = p0v(i) * 100.
         enddo
*
         call e_bmfsplitxy2 (ttu,niu,nju,'GZU ',1,1,pniu,0,0,0)
         call e_bmfsplitxy2 (ttv,niv,njv,'GZV ',1,1,pni ,0,0,0)
         call e_bmfsplitxy2 (p0u,niu,nju,'APSU',1,1,pniu,0,0,0)
         call e_bmfsplitxy2 (p0v,niv,njv,'APSV',1,1,pni ,0,0,0)
*
         var=vt//'  '
         c1 = Dcst_tcdk_8
         do k=1,lv
            if (e_rdhint3 (ttu,dstu_gid,niu,nju,var,na(k),ip2a,ip3a,
     $         ' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
            if (e_rdhint3 (ttv,dstv_gid,niv,njv,var,na(k),ip2a,ip3a,
     $         ' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
            do i=1,nu
               ttu(i) = ttu(i) + c1
            enddo
            do i=1,nv
               ttv(i) = ttv(i) + c1
            enddo
            if (vt.eq.'TT') then
               if (e_rdhint3 (huu,dstu_gid,niu,nju,'HU  ',na(k),ip2a,
     $            ip3a,' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0)
     $             goto 55
               if (e_rdhint3 (huv,dstv_gid,niv,njv,'HU  ',na(k),ip2a,
     $            ip3a,' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0)
     $             goto 55
               call mfotvt (ttu,ttu,huu,niu*nju,1,niu*nju)
               call mfotvt (ttv,ttv,huv,niv*njv,1,niv*njv)
            endif
            call e_bmfsplitxy2 (ttu,niu,nju,'VTU ',k,lv,pniu,0,0,0)
            call e_bmfsplitxy2 (ttv,niv,njv,'VTV ',k,lv,pni ,0,0,0)
         end do
*
      elseif ( glecmanl ) then

         write(6,*)'PREPARATION FOR ECMWF to HYBRID'
         c1 = 10. * Dcst_grav_8
         if (e_rdhint3 (ttu,dstu_gid,niu,nju,'GZ  ',-1,-1,-1,
     $         ' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
         if (e_rdhint3 (ttv,dstv_gid,niv,njv,'GZ  ',-1,-1,-1,
     $         ' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
*
*        for ECMWF analyses, the log of pressure (pa) is stored in 2P
*
         if (e_rdhint3 (p0u,dstu_gid,niu,nju,'2P  ',   0 ,-1,-1,
     $         ' ',' ',.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
         if (e_rdhint3 (p0v,dstv_gid,niv,njv,'2P  ',   0 ,-1,-1,
     $         ' ',' ',.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
         do i=1,nu
            ttu(i) = ttu(i) * c1
            p0u(i) = exp(p0u(i))
         enddo
         do i=1,nv
            ttv(i) = ttv(i) * c1
            p0v(i) = exp(p0v(i))
         enddo
*
         call e_bmfsplitxy2 (ttu,niu,nju,'GZU ',1,1,pniu,0,0,0)
         call e_bmfsplitxy2 (ttv,niv,njv,'GZV ',1,1,pni ,0,0,0)
         call e_bmfsplitxy2 (p0u,niu,nju,'APSU',1,1,pniu,0,0,0)
         call e_bmfsplitxy2 (p0v,niv,njv,'APSV',1,1,pni ,0,0,0)
*
         c1 = Dcst_tcdk_8
         do k=1,lv
            ip3 = int(rna(k))
            if (e_rdhint3 (ttu,dstu_gid,niu,nju,'TT  ',na(k),-1,ip3,
     $         ' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
            if (e_rdhint3 (ttv,dstv_gid,niv,njv,'TT  ',na(k),-1,ip3,
     $         ' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
            do i=1,nu
               ttu(i) = ttu(i) + c1
            enddo
            do i=1,nv
               ttv(i) = ttv(i) + c1
            enddo
            if (e_rdhint3 (huu,dstu_gid,niu,nju,'HU  ',na(k),-1,
     $          ip3,' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0)
     $          goto 55
            if (e_rdhint3 (huv,dstv_gid,niv,njv,'HU  ',na(k),-1,
     $          ip3,' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0)
     $          goto 55
            call mfotvt (ttu,ttu,huu,niu*nju,1,niu*nju)
            call mfotvt (ttv,ttv,huv,niv*njv,1,niv*njv)
            call e_bmfsplitxy2 (ttu,niu,nju,'VTU ',k,lv,pniu,0,0,0)
            call e_bmfsplitxy2 (ttv,niv,njv,'VTV ',k,lv,pni ,0,0,0)
         end do
*
*     add treatment at the surface, read temperature and dew point,
*     transform to virtual temperature and humidity, write VT as STU 
*     and STV
         if (e_rdhint3 (ttu,dstu_gid,niu,nju,'TS  ',-1,-1,-1,
     $      ' ',' ',.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
         if (e_rdhint3 (ttv,dstv_gid,niv,njv,'TS  ',-1,-1,-1,
     $      ' ',' ',.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
         if (e_rdhint3 (huu,dstu_gid,niu,nju,'TD  ',-1,-1,
     $       -1,' ',' ',.true.,.true.,'CUBIC',e_fu_anal,6).lt.0)
     $       goto 55
         if (e_rdhint3 (huv,dstv_gid,niv,njv,'TD  ',-1,-1,
     $       -1,' ',' ',.true.,.true.,'CUBIC',e_fu_anal,6).lt.0)
     $       goto 55
         do i=1,nu
            huu(i) = ttu(i) - huu(i)
            ttu(i) = ttu(i) + c1
         enddo
         do i=1,nv
            huv(i) = ttv(i) - huv(i)
            ttv(i) = ttv(i) + c1
         enddo
         call mesahu(huu, huu, ttu, 1, p0u, 3, .true., .false., nu, 1, nu)
         call mesahu(huv, huv, ttv, 1, p0v, 3, .true., .false., nv, 1, nv)
         call mfotvt(ttu, ttu, huu, nu, 1, nu)
         call mfotvt(ttv, ttv, huv, nv, 1, nv)
         call e_bmfsplitxy2 (ttu,niu,nju,'STU ',1,1,pniu,0,0,0)
         call e_bmfsplitxy2 (ttv,niv,njv,'STV ',1,1,pni ,0,0,0)
*
      else
*
         write(6,*)'PREPARATION FOR PRESSURE to HYBRID'
         c1 = 10.
         do k=1,lv
            if (e_rdhint3 (ttu,dstu_gid,niu,nju,'GZ  ',na(k),ip2a,ip3a,
     $         ' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
            if (e_rdhint3 (ttv,dstv_gid,niv,njv,'GZ  ',na(k),ip2a,ip3a,
     $         ' ',tva,.true.,.true.,'CUBIC',e_fu_anal,6).lt.0) goto 55
            do i=1,nu
               ttu(i) = ttu(i) * c1
            enddo
            do i=1,nv
               ttv(i) = ttv(i) * c1
            enddo
            call e_bmfsplitxy2 (ttu,niu,nju,'GZU ',k,lv,pniu,0,0,0)
            call e_bmfsplitxy2 (ttv,niv,njv,'GZV ',k,lv,pni ,0,0,0)
         enddo
*
      endif
*
      goto 99
 55   call e_arret( 'e_intwind' )
*
 120  format ('|',1x,a8,'|',1x,a2,'  |',2(i7,' |'),i3,'  |',1x,a3,
     $        ' |',1x,a7,'  |',1x,a16,'|')
 199  format ('|',2x,'   NO INTERPOLATION REQUIRED        ',1x,16x,1x,'|')
* ---------------------------------------------------------------------
*
 99   return
      end
