** s/r e_intgeo  Read geophysic fields and interpolate on model grid.
*
#include <model_macros_f.h>
*
      logical function e_intgeo ()
      implicit none
*
*author
*     Michel Desgagne - January 2001
*
*revision
* v2_21 - Desgagne M.       - initial version
* v3_00 - Desgagne & Lee    - Lam configuration
*
*implicites
#include "e_fu.cdk"
#include "e_grids.cdk"
#include "p_bus.cdk"
#include "e_cdate.cdk"
#include "hgc.cdk"
#include "e_schm.cdk"
*
**
      integer e_rdhint3,e_moclim2
      external e_rdhint3,e_moclim2
*
      character*2 nv
      character*8 etk,typ,interp,seq,dum
      logical anyip
      integer ivar, im, i, longueur, unf, indx, ip1, ip2
      integer, dimension(:),allocatable :: err
*
*----------------------------------------------------------------------
*
      allocate ( err(p_bgeo_top) )
      do ivar = 1, p_bgeo_top
         err(ivar) = -1
      end do
*
*     Loop on variables in geobus 
*
      write (6,1000)
      do ivar = 1, p_bgeo_top
         nv    = geonm(ivar,2)
         if (nv.ne.'00') then
            etk   = geonm(ivar,3)
            typ   = geonm(ivar,4)
            interp= geonm(ivar,5)
            seq   = geonm(ivar,6)
            write(6,1001) nv,etk,typ,interp,seq
 670        if ((seq(1:1).ne."V").and.(seq(1:4).ne." ")) then
               unf = e_fu_anal
               if (seq(1:1).eq."C") unf = e_fu_climat
               if (seq(1:1).eq."G") unf = e_fu_geophy
               ip2 = -1
               anyip = .false.
               if (seq(1:1).eq."C") then
                  ip2 = month
                  if (nv.eq.'AL') anyip = .true.
               endif
               do im=1,geopar(ivar,3)
                  ip1 = -1
                  if (geopar(ivar,3).gt.1) 
     $                 call convip ( ip1, real(im), 3, 1, dum, .false. )
                  indx = geopar(ivar,1) + (im-1)*nifi*njfi
                  err(ivar) = e_rdhint3(geobus(indx),dstf_gid,nifi,njfi,
     $                 nv,ip1,ip2,-1,etk,typ,anyip,.false.,interp,unf,6)
                  if (err(ivar).lt.0) then
                     seq = seq(2:longueur(seq))
                     goto 670
                  endif
               end do
            elseif (seq(1:1).eq."V") then
               do im=1,geopar(ivar,3)
                  ip1 = -1
                  if (geopar(ivar,3).gt.1) 
     $                 call convip ( ip1, real(im), 3, 1, dum, .false. )
                  indx = geopar(ivar,1) + (im-1)*nifi*njfi
                  err(ivar) =  e_moclim2 (geobus(indx),nifi,njfi,nv,
     $                           ip1,-1,month, day,' ',' ',dstf_gid,
     $                                         interp,e_fu_climat,6)
               end do
            endif
         else
            err(ivar) = 0
         endif
      end do
*
      e_intgeo = .true.
      do ivar = 1, p_bgeo_top
         if (err(ivar).lt.0) then
            if (.not.e_Schm_offline_L) e_intgeo = .false.
            write (6,1005) geonm(ivar,2)
         endif
      end do
*
      deallocate (err)
*
 1000 format (/' PROCESSING EVERY ELEMENT IN GEOBUS:')
 1001 format (/'====>',' Processing ',a2,4a8,' <====')
 1005 format (' GEOPHYSICAL FIELD: 'a,' NOT AVAILABLE !!!')
*
*----------------------------------------------------------------------
*
      return
      end
