***s/r wil_epslon
*
      REAL FUNCTION WIL_EPSLON (X)
*
      implicit none
*
      REAL X                      
*
*author Jacob et al. 1992 
*
*revision
* v4_04 - Jacob et al.     - initial version EPSLON 
* v4_04 - Tanguay M.       - Williamson's cases
*
*object
* ESTIMATE MACHINE ARITHMETIC ROUNDOFF IN QUANTITIES OF SIZE X
*
*     THIS PROGRAM SHOULD FUNCTION PROPERLY ON ALL SYSTEMS
*     SATISFYING THE FOLLOWING TWO ASSUMPTIONS,
*        1.  THE BASE USED IN REPRESENTING FLOATING POINT
*            NUMBERS IS NOT A POWER OF THREE.
*        2.  THE QUANTITY  A  IN STATEMENT 10 IS REPRESENTED TO
*            THE ACCURACY USED IN FLOATING POINT VARIABLES
*            THAT ARE STORED IN MEMORY.
*     THE STATEMENT NUMBER 10 AND THE GO TO 10 ARE INTENDED TO
*     FORCE OPTIMIZING COMPILERS TO GENERATE CODE SATISFYING
*     ASSUMPTION 2.
*     UNDER THESE ASSUMPTIONS, IT SHOULD BE TRUE THAT,
*            A  IS NOT EXACTLY EQUAL TO FOUR-THIRDS,
*            B  HAS A ZERO FOR ITS LAST BIT OR DIGIT,
*            C  IS NOT EXACTLY EQUAL TO ONE,
*            EPS  MEASURES THE SEPARATION OF 1.0 FROM
*                 THE NEXT LARGER FLOATING POINT NUMBER.
*
*------------------------------------------------------------------
      REAL A,B,C,EPS
*------------------------------------------------------------------
*
      A = 4.0E0/3.0E0                 
   10 B = A - 1.0E0                 
      C = B + B + B              
      EPS = ABS(C-1.0E0)             
      IF (EPS .EQ. 0.0E0) GO TO 10     
      WIL_EPSLON = EPS*ABS(X)                   
*
      RETURN                    
      END                     
