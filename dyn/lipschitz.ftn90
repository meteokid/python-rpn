!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer, 
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms 
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer 
!version 3 or (at your option) any later version that should be found at: 
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software; 
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), 
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
!**s/p lipschitz - compute lipschitz stability criterion 
!
#include "model_macros_f.h"
!
      subroutine lipschitz(F_u, F_v, F_w, DIST_DIM, Nk, &
                                      F_i0,F_in,F_j0,F_jn)
!
      implicit none
!
      integer DIST_DIM, Nk
      integer F_i0,F_in,F_j0,F_jn
      real F_w(DIST_SHAPE,Nk), F_u(DIST_SHAPE,Nk), F_v(DIST_SHAPE,Nk)
!
!author
!     Claude Girard & Andre Plante - oct 2006 
!
!revision
!
!object
!	compute lipschitz number
!       See Pudykiewicz et al. Atmos.0cean 1985 267-303 Appendix A
!
!***************************************************************************
!
!arguments
!  Name        I/O                 Description
!----------------------------------------------------------------
! F_w          I    - model s vertical velocity (pi* coordinates)
! F_u          I    - model zonal horizontal wind component
! F_v          I    - model meridional horizontal wind component
! F_i0         I    - starting point of calculation on W-E axis
! F_in         I    - ending point of calculation on W-E axis
! F_j0         I    - starting point of calculation on N-S axis
! F_jn         I    - ending point of calculation on N-S axis
!
!implicits
#include "glb_ld.cdk"
#include "geomg.cdk"
#include "type.cdk"
#include "ver.cdk"
#include "cstv.cdk"
!
!*
      integer i,j,k,i0,j0,km, iu,iv,iw, ju,jv,jw, ku,kv,kw
      real pr1, pr2, dudx, dvdy, dwdy,dwdz, LipNOu, LipNOv, LipNOw
!     __________________________________________________________________
!
      i0=F_i0
      j0=F_j0
      if ((G_lam).and.(l_west)) i0 = max(2,F_i0)
      if (l_south) j0 =  max(2,F_j0)
!
      iu=-99
      ju=-99
      ku=-99
      iv=-99
      jv=-99
      kv=-99
      iw=-99
      jw=-99
      kw=-99
      LipNOu=0.0
      LipNOv=0.0
      LipNOw=0.0
!
!$omp do
      do k=1,l_nk
         km=max(k-1,1)
         do j= j0, F_jn
         do i= i0, F_in
            dudx=abs(F_u(i,j,k)-F_u(i-1,j,k))*geomg_invcy2_8(j)*geomg_invDX_8(i)
            if(dudx.gt.LipNOu) then
               iu=i
               ju=j
               ku=k
               LipNOu=dudx
            endif
            dvdy=abs(F_v(i,j,k)-F_v(i,j-1,k))*geomg_invcy2_8(j)*geomg_invDY_8(j)
            if(dvdy.gt.LipNOv) then
               iv=i
               jv=j
               kv=k
               LipNOv=dvdy
            endif
            dwdz=abs(F_w(i,j,k)-F_w(i,j,km))*ver_idz_8%m(km)
            if(dwdz.gt.LipNOw) then
               iw=i
               jw=j
               kw=k
               LipNOw=dwdz
            endif
         end do
         end do
      end do
!$omp enddo
!
      LipNOu=Cstv_dt_8*LipNOu
      LipNOv=Cstv_dt_8*LipNOv
      LipNOw=Cstv_dt_8*LipNOw
!
      write(6,101) LipNOu,iu,ju,ku
      write(6,102) LipNOv,iv,jv,kv
      write(6,103) LipNOw,iw,jw,kw
  101 format(30x,'Lipschitz number(u)=',F6.2,' [',i4,',',i4,',',i3']')
  102 format(30x,'Lipschitz number(v)=',F6.2,' [',i4,',',i4,',',i3']')
  103 format(30x,'Lipschitz number(w)=',F6.2,' [',i4,',',i4,',',i3']')
!     __________________________________________________________________
!
      return
      end 
