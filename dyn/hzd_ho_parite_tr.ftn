***s/r hzd_ho_parite_tr  - Equivalent to hzd_ho_parite_tr for TRAJECTORY 
*                         (F_pip, F_qp (No Hyd) ONLY)
*
#include <model_macros_f.h>
*
      subroutine hzd_ho_parite_tr ( F_pip,F_qp,DIST_DIM,Nk) 
*
      implicit none
*
      integer DIST_DIM, Nk
      real     F_pip(DIST_SHAPE,Nk), F_qp(DIST_SHAPE,Nk)
*
*author
*     M.Tanguay
*
*revision
* v3_02 - Tanguay M.        - initial MPI version
* v3_03 - Tanguay M.        - Adjoint NoHyd configuration
* v3_11 - Tanguay M.        - AIXport+Opti+OpenMP for TLM-ADJ
*
*object
*     see id section
*
*arguments
*
*implicits
#include "glb_ld.cdk"
#include "ldnh.cdk"
#include "dcst.cdk"
#include "cstv.cdk"
#include "trp.cdk"
#include "hzd.cdk"
#include "opr.cdk"
#include "schm.cdk"
#include "geomg.cdk"
#include "lun.cdk"
#include "ptopo.cdk"
*
      integer i, j, k, dpwr
      real*8 wk1_8 
      real   wk2 (LDIST_SHAPE,Nk)
      integer NSTOR,nev
*     __________________________________________________________________
*
      dpwr = Hzd_pwr / 2

      nev= (G_ni+2)/2
      NSTOR = nev + ( 1 - mod(nev,2) )
      if (Lun_debug_L) write(Lun_out,1000) Hzd_pwr
*
      if (Hzd_uvwdt_L) return
*
      if ( Hzd_cdiff .gt. 0.0 ) then
*
*     Save pi' for the diffusion of q'
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         if ( (.not.Schm_hydro_L) .and. Schm_difqp_L ) then
!$omp parallel do
            do k = 1, Nk
            do j = 1,l_nj
            do i = 1,l_ni
               wk2(i,j,k) = F_pip(i,j,k)
            end do
            end do
            end do
!$omp end parallel do
         endif
*
*     The hydrostatic pressure: pi'  
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         call hzd_solparite_2(F_pip, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
*
*****************************
*  3. Nonyhydrostatic model *
*****************************
*
         if ( .not. Schm_hydro_L .and. Schm_difqp_L ) then ! q' & related variables
*
*        Indirect diffusion of q'
*        ~~~~~~~~~~~~~~~~~~~~~~~~
!$omp parallel do
         do k=1, Nk  !
         do j=1,l_nj ! p = (pi)exp(q')
         do i=1,l_ni ! ~~~~~~~~~~~~~~~
            wk2(i,j,k) = ( geomg_z_8(k) + dble(wk2(i,j,k)) )
     %                       *exp( dble(F_qp(i,j,k)) )
         end do
         end do
         end do
!$omp end parallel do
*
         call hzd_solparite_2(wk2, wk1_8, Opr_evvec_8 ,Opr_odvec_8,
     %                    Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                    trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                    trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                    l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                    trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                    Ptopo_npex,Ptopo_npey,NSTOR,nev)
*
!$omp parallel do
         do k=1, Nk  !
         do j=1,l_nj ! q' = log(p/pi)
         do i=1,l_ni ! ~~~~~~~~~~~~~~
            F_qp(i,j,k) = log(             dble(wk2(i,j,k))
     %                       /( geomg_z_8(k) + dble(F_pip(i,j,k)) )  )
         end do
         end do
         end do
!$omp end parallel do
*
         endif
*
      endif
*
 9988 continue
*
 1000 format(/,
     $     3X,'TRAJ of PERFORM DEL-',i1,' HORIZONTAL DIFFUSION: (S/R hzd_ho_parite_tr)')
*     __________________________________________________________________
*
      return
      end
