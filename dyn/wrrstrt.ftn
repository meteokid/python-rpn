*s/r wrrstrt - Write the restart file
*
#include "model_macros_f.h"
*
      subroutine wrrstrt ()
*
      implicit none
*
*author
*     M. Desgagne - Mars 2000
*
*revision
* v2_00 - Desgagne M.       - initial MPI version
* v2_10 - Desgagne M.       - introduce WA files
* v2_21 - Dugas B.          - adapt to climate mode
* v2_30 - Corbeil L.        - Added writing of pres_surf pres_top
* v2_31 - Desgagne M.       - Add Tr2d tracers
* v3_00 - Desgagne & Lee    - Lam configuration
* v3_21 - Valcke, S.        - Oasis coupling: Removed wawrit of c_cplg_step
* v3_21 - Lee V.            - Remove Tr2d tracers
* v3_30 - Desgagne & Winger - Write one global binary restart file if required
* v3_30 - Desgagne M.       - restart for coupling
*
*object
*	
*arguments
*	none
*
*implicits
#include "lun.cdk"
#include "init.cdk"
#include "rstr.cdk"
#include "lctl.cdk"
#include "schm.cdk"
#include "itf_phy_buses.cdk"
#include "itf_cpl_cplg.cdk"
#include "glb_ld.cdk"
#include "pres.cdk"
#include "lam.cdk"
#include "itf_chm_bus.cdk"
#include "bcsmem.cdk"
*
*
*modules
      integer vmmckmx,fnom,fclos,close_db_file
      external vmmckmx,fnom,fclos,close_db_file
*
      integer i,ier,adr,dim,n,current_nest
*
*     ---------------------------------------------------------------
*
      if (Lun_out.gt.0) write(Lun_out,2000) Lctl_step
*
      if (Rstri_glbcol_L) then
C        Write one global binary file for the physics
C        and chemistry restarts for the whole domain
         call wr1rstrt()
*
C        Write one global binary file for the dynamics 
C        restart for the whole domain
         call glb_restart('W')
      else
C        Write one wa-file per tile
         Lun_rstrt = 0
         ier = fnom (Lun_rstrt,'restart','RND',0)
         call waopen (Lun_rstrt)
         dim = p_bper_siz*p_nj
*
         call wawrit (Lun_rstrt,Lctl_step      ,1,1)
         call wawrit (Lun_rstrt,Rstri_idon_L   ,2,1)
         call wawrit (Lun_rstrt,dim            ,3,1)
         adr = 4
*
         if (dim.gt.0) then
            call wawrit (Lun_rstrt,Phy_busper3D,adr,p_bper_siz*p_nj)
            adr = adr + p_bper_siz*p_nj
*
            if ( Init_balgm_L .and.
     $      .not.Rstri_idon_L .and.
     $           Rstri_half_L) then
               call wawrit (Lun_rstrt,Rstri_half_L     ,adr,1)
               adr = adr + 1
               call wawrit (Lun_rstrt,Phy_busper3D_digf,adr,
     $                                p_bper_siz*p_nj)
               adr = adr + p_bper_siz*p_nj              
            else
               call wawrit (Lun_rstrt, .false. ,adr, 1)
               adr = adr + 1
            endif
         endif
*
         dim = chm_bper_siz*chm_nj
         call wawrit (Lun_rstrt,dim            ,adr,1)
         adr = adr + 1
         if (dim>0) then
            call wawrit (Lun_rstrt,Chm_busper3D,adr,chm_bper_siz*chm_nj)
            adr = adr + chm_bper_siz*chm_nj
         endif
*
         if (G_lam) then
            call wawrit( Lun_rstrt,BCS_values,adr,BCS_siz_tot)
            adr = adr + bcs_siz_tot
         endif
*
         call wawrit (Lun_rstrt, pres_surf ,adr, 1)
         adr = adr + 1
         call wawrit (Lun_rstrt, pres_top  ,adr, 1)
         call datp2f (current_nest,Lam_current_S )
         adr = adr + 1
         call wawrit (Lun_rstrt, current_nest  ,adr, 1)
         adr = adr + 1 
*
!        Write global coupling information
!        found in rla_msk and rga_cpl2phy
*
         if (C_coupling_L) then
            call wawrit (Lun_rstrt, rla_msk ,adr, l_ni*l_nj)
            adr = adr + l_ni*l_nj
            dim = l_ni*l_nj*ig_max_inout_dim
            call wawrit (Lun_rstrt, rga_cpl2phy ,adr, dim)
            adr = adr + dim
         endif
*
C        Write Vmm-files
         ier = vmmckmx()
         call waclos(Lun_rstrt)
         ier = fclos(Lun_rstrt)  
*
      endif
*
 2000 format(/,'WRITING A RESTART FILE AT TIMESTEP #',I8,
     +       /,'=========================================')
*
*     ---------------------------------------------------------------
*      
      return
      end
