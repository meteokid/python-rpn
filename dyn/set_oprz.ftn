!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer, 
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms 
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer 
!version 3 or (at your option) any later version that should be found at: 
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software; 
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), 
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
***s/r set_oprz - Computes vertical operators and matrices a,b,c
*                  for the elliptic solver
*
#include "model_macros_f.h"
*
      subroutine set_oprz
*
      implicit none
*
*author
*     M. Desgagne - initial MPI version (from setoprz v1_03)
*
*revision
* v2_00 - Desgagne M.       - initial MPI version
* v4_00 - Plante & Girard   - Log-hydro-pressure coord on Charney-Phillips grid
* v4_02   Qaddouri A.       - Rewrite set_pois for vertical stag version
* v4_05 - Girard C.         - Open top
*
*object
*     see ID section above
*
*arguments
*     None
*
*implicits
#include "glb_pil.cdk"
#include "glb_ld.cdk"
#include "dcst.cdk"
#include "schm.cdk"
#include "opr.cdk"
#include "sol.cdk"
#include "cstv.cdk"
#include "trp.cdk"
#include "type.cdk"
#include "ver.cdk"
#include "ptopo.cdk"
*
*modules
**
      real*8 one, half
      parameter( one  = 1.d0, half = .5d0 )
*
      integer k, k0,k1, AA, BB, CC
      real*8  pdsc, xxt,xxs,wk(G_nk)
      real*8, dimension 
     $((trp_12smax-trp_12smin+1)*(trp_22max-trp_22min+1)*G_nj) :: a,b,c

*
*     ---------------------------------------------------------------
*
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*     Compute the vertical operators: tri-diagnonal matrices
*                  O(AA+k): lower diagonal
*                  O(BB+k):       diagonal
*                  O(CC+k): upper diagonal
*           O(AA+k)*P(k-1)+O(BB+k)*P(k)+O(CC+k)*P(k+1)
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*
      AA=0
      BB=G_nk
      CC=G_nk*2
      k0=1+Glb_pil_t
*
*     ~~~~~~~~~~~~~~~~~
*     Diagonal Operator
*     ~~~~~~~~~~~~~~~~~
*
      do k = 1, G_nk
         Opr_opszp0_8(AA+k) = 0.d0
         Opr_opszp0_8(BB+k) = Ver_dz_8%m(k)
         Opr_opszp0_8(CC+k) = 0.d0
*        Zero in the others to start
         Opr_opszp2_8(AA+k) = 0.d0
         Opr_opszp2_8(BB+k) = 0.d0
         Opr_opszp2_8(CC+k) = 0.d0
         Opr_opszpm_8(AA+k) = 0.d0
         Opr_opszpm_8(BB+k) = 0.d0
         Opr_opszpm_8(CC+k) = 0.d0
         Opr_opszpl_8(AA+k) = 0.d0
         Opr_opszpl_8(BB+k) = 0.d0
         Opr_opszpl_8(CC+k) = 0.d0
      end do
*
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~
*     Second Derivative Operator
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~
*
      Opr_opszp2_8(AA+k0)    = 0.d0
      Opr_opszp2_8(BB+k0)    =-Ver_idz_8%t(k0)*(one-Ver_alfat_8)
     $                        -Ver_idz_8%t(k0+1)
      Opr_opszp2_8(CC+k0)    = Ver_idz_8%t(k0+1)
      do k = k0+1, G_nk-1
         Opr_opszp2_8(AA+k) = Ver_idz_8%t(k)
         Opr_opszp2_8(BB+k) =-Ver_idz_8%t(k)-Ver_idz_8%t(k+1)
         Opr_opszp2_8(CC+k) = Ver_idz_8%t(k+1)
      end do
      Opr_opszp2_8(AA+G_nk) = Ver_idz_8%t(G_nk)
      Opr_opszp2_8(BB+G_nk) =-Ver_idz_8%t(G_nk)
     $                       -Ver_idz_8%t(G_nk+1)*(one-ver_alfas_8)
      Opr_opszp2_8(CC+G_nk) = 0.d0
*
*     ~~~~~~~~~~~~~~~~~~~~~~~
*     Double average operator
*     ~~~~~~~~~~~~~~~~~~~~~~~
*
      Opr_opszpm_8(AA+k0)   = 0.d0
      if(Schm_opentop_L) then
         Opr_opszpm_8(BB+k0)= Ver_wp_8%m(k0)*half*Ver_dz_8%m(k0)
     $                        +(one+Ver_alfat_8)*Ver_dz_8%t(k0)/4.d0
      else
         Opr_opszpm_8(BB+k0)= (Ver_wp_8%m(k0)*half
     $                        +Ver_wm_8%m(k0)*Ver_alfat_8)*Ver_dz_8%m(k0)
      endif
      Opr_opszpm_8(CC+k0)   = Ver_wp_8%m(k0)*half*Ver_dz_8%m(k0)
      do k = k0+1, G_nk-1
         Opr_opszpm_8(AA+k) = half*Ver_dz_8%m(k)*Ver_wm_8%m(k)
         Opr_opszpm_8(BB+k) = half*Ver_dz_8%m(k)
         Opr_opszpm_8(CC+k) = half*Ver_dz_8%m(k)*Ver_wp_8%m(k)
      end do
      Opr_opszpm_8(AA+G_nk) =  Ver_wm_8%m(G_nk)*half*Ver_dz_8%m(G_nk)
      Opr_opszpm_8(BB+G_nk) = (Ver_wm_8%m(G_nk)*half
     $                        +Ver_wp_8%m(G_nk)*Ver_alfas_8)*Ver_dz_8%m(G_nk)
      Opr_opszpm_8(CC+G_nk) = 0.d0
*
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*     First Derivative Operator (non symetric)
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*
      Opr_opszpl_8(AA+k0)   = 0.d0
      if(Schm_opentop_L) then
         Opr_opszpl_8(BB+k0)=-half * Ver_alfat_8
      else
         Opr_opszpl_8(BB+k0)= half - Ver_alfat_8
      endif
      Opr_opszpl_8(CC+k0)   = half
      do k = k0+1, G_nk-1
         Opr_opszpl_8(AA+k) =-half
         Opr_opszpl_8(BB+k) = 0.d0
         Opr_opszpl_8(CC+k) = half
      end do
      Opr_opszpl_8(AA+G_nk) =-half
      Opr_opszpl_8(BB+G_nk) =-half + Ver_alfas_8
      Opr_opszpl_8(CC+G_nk) = 0.d0
*
*     ---------------------------------------------------
*     Compute eigenvalues and eigenvector in the vertical
*     ---------------------------------------------------
*
       call set_pois (Opr_zeval_8, Opr_lzevec_8,Opr_zevec_8,G_nk, G_nk)
*
      do k=1,G_nk
         do k1=1,G_nk
            wk(k) = Opr_zevec_8 ((k-1)*G_nk+k1)
         enddo
         wk(k)= Cstv_hco0_8*Opr_zeval_8(k)
      enddo
*     
      call sol_abc ( wk,G_yg_8(1),Opr_opsyp0_8,
     $               Opr_opsyp2_8,Opr_xeval_8,
     $               trp_12sn0, trp_22n0 , trp_12smin, trp_12smax  ,
     $               trp_22min, trp_22max, trp_12sn  , trp_22n,G_nj,
     $               Sol_ai_8, Sol_bi_8, Sol_ci_8 , a, b, c)
*
*     ---------------------------------------------------------------
*
      return
      end
