! ------------------------------------------------------------------------------------------------------------------------------
! EC-RPN COMM Group License/Disclaimer, version 2, Last Modified: Jan 08th, 2008 , Environment Canada
! 
! This is free software, you can use/redistribute/modify it under the terms of the EC-RPN COMM Group License/Disclaimer version 2 
! or (at your option) any later version that should be found at http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
! 
! This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the EC-RPN COMM Group License/Disclaimer for more details.
! 
! You should have received a copy of the EC-RPN COMM Group License/Disclaimer along with this software; if not, write to the
! EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), CANADA, H9P 1J3 ; or send e-mail to service.rpn@ec.gc.ca
! ------------------------------------------------------------------------------------------------------------------------------
***s/r set_oprz - Computes vertical operators and matrices a,b,c
*                  for the elliptic solver
*
#include "model_macros_f.h"
*
      subroutine set_oprz
*
#include "impnone.cdk"
*
*author
*     M. Desgagne - initial MPI version (from setoprz v1_03)
*
*revision
* v2_00 - Desgagne M.       - initial MPI version
* v4_02   Qaddouri A.       - Rewrite set_pois for vertical stag version
*
*object
*     see ID section above
*
*arguments
*     None
*
*implicits
#include "glb_pil.cdk"
#include "glb_ld.cdk"
#include "dcst.cdk"
#include "schm.cdk"
#include "opr.cdk"
#include "sol.cdk"
#include "cstv.cdk"
#include "trp.cdk"
#include "type.cdk"
#include "ver.cdk"
#include "ptopo.cdk"
*
*modules
**
      real*8 one, half, quarter
      parameter( one  = 1.d0, half = .5d0, quarter = .25d0 )
*
      integer k, k0,longueur
      real*8  pdsc, wk(G_nk)
      real*8, dimension 
     $((trp_12smax-trp_12smin+1)*(trp_22max-trp_22min+1)*G_nj) :: a,b,c

*
*     ---------------------------------------------------------------
*
*     Compute the vertical operators
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*
*     Diagonal Operator
*     ~~~~~~~~~~~~~~~~~
      do k = 1, G_nk
         Opr_opszp0_8(       k)=0.d0
         Opr_opszp0_8(G_nk  +k)=Ver_dz_8%m(k)
         Opr_opszp0_8(G_nk*2+k)=0.d0
      end do
*                                          
*     Second Derivative Operator
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~
      Opr_opszp2_8(     1)=0.d0
      Opr_opszp2_8(G_nk*3)=0.d0
      do k = 1, G_nk-1
         Opr_opszp2_8(       k+1)= Ver_idz_8%t(k+1)
         Opr_opszp2_8(G_nk*2+k  )=+ Opr_opszp2_8(k+1)
         Opr_opszp2_8(G_nk  +k  )=-(Opr_opszp2_8(G_nk*2+k)+Opr_opszp2_8(k))
      end do
      Opr_opszp2_8(G_nk*2       )=-Opr_opszp2_8(G_nk)
*      
*     Adjusted for vertical boundary conditions
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
      Opr_opszp2_8(2*G_nk) = Opr_opszp2_8(2*G_nk)
     $                     + (ver_alphas_8-one)*Ver_idz_8%t(G_nk+1)
      Opr_opszp2_8(G_nk+1) = Opr_opszp2_8(G_nk+1)
     $                     - (one-Ver_alphat_8)*Ver_idz_8%t(1)
*
*     Double average operator
*     ~~~~~~~~~~~~~~~~~~~~~~~
      Opr_opszpm_8(     1)=0.d0
      Opr_opszpm_8(G_nk*3)=0.d0
      do k = 1, G_nk-1
         Opr_opszpm_8(       k+1)= quarter*Ver_dz_8%t(k+1)
         Opr_opszpm_8(G_nk*2+k  )=+ Opr_opszpm_8(k+1)
         Opr_opszpm_8(G_nk  +k  )=+(Opr_opszpm_8(G_nk*2+k)+Opr_opszpm_8(k))
      end do
      Opr_opszpm_8(G_nk*2       )=+Opr_opszpm_8(G_nk)
*        
*     Adjusted for vertical boundary conditions
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
      Opr_opszpm_8(2*G_nk)=Opr_opszpm_8(2*G_nk)
     $                    +(one+ver_alphas_8)*quarter*Ver_dz_8%t(G_nk+1)
      Opr_opszpm_8(G_nk+1)=Opr_opszpm_8(G_nk+1)
     $                    +(one+ver_alphat_8)*quarter*Ver_dz_8%t(1)
*
*     First Derivative Operator (non symetric)
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
      Opr_opszpl_8(     1)=0.d0
      Opr_opszpl_8(G_nk*3)=0.d0
      do k = 1, G_nk-1
         Opr_opszpl_8(       k+1)= -half
         Opr_opszpl_8(G_nk*2+k  )= -Opr_opszpl_8(k+1)
         Opr_opszpl_8(G_nk  +k  )=  .0d0
      end do
      Opr_opszpl_8(G_nk*2       )=  .0d0
*
*     Adjusted for vertical boundary conditions
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
      Opr_opszpl_8(2*G_nk)=Opr_opszpl_8(2*G_nk)+ver_alphas_8*half
      Opr_opszpl_8(G_nk+1)=Opr_opszpl_8(G_nk+1)-ver_alphat_8*half
*
*     Compute eigenvalues and eigenvector in the vertical
*     ---------------------------------------------------
*
       call set_pois (Opr_zeval_8, Opr_lzevec_8,Opr_zevec_8,G_nk, G_nk)
*
      do k=1,G_nk
         do k0=1,G_nk
            wk(k) = Opr_zevec_8 ((k-1)*G_nk+k0)
         enddo
         wk(k)= Cstv_hco0_8*Opr_zeval_8(k)
      enddo
*     
      call sol_abc ( wk,G_yg_8(1),Opr_opsyp0_8,
     $               Opr_opsyp2_8,Opr_xeval_8,
     $               trp_12sn0, trp_22n0 , trp_12smin, trp_12smax  ,
     $               trp_22min, trp_22max, trp_12sn  , trp_22n,G_nj,
     $               Sol_ai_8, Sol_bi_8, Sol_ci_8 , a, b, c)
*
*     ---------------------------------------------------------------
*
      return
      end
