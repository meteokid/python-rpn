!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer, 
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms 
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer 
!version 3 or (at your option) any later version that should be found at: 
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software; 
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), 
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
***s/r adw_main_1_wnd - process winds in preparation for advection
*
#include "model_macros_f.h"
#include "msg.h"
*
      subroutine adw_main_1_wnd ( F_u, F_v, F_w, F_nit, F_njt, F_nk )
*
      implicit none
*
      integer F_nit, F_njt, F_nk
*
      real F_u(F_nit,F_njt,F_nk),
     %     F_v(F_nit,F_njt,F_nk),
     %     F_w(F_nit,F_njt,F_nk+1)
*
*author
*     alain patoine
*
*revision
* v2_31 - Desgagne M.       - removed stkmemw
* v3_00 - Desgagne & Lee    - Lam configuration
* v3_10 - Corbeil & Desgagne & Lee - AIXport+Opti+OpenMP
* v3_21 - Desgagne M.       - Revision OpenMP
*
*language
*     fortran 77
*
*object
*     see id section
*
*arguments
*______________________________________________________________________
*              |                                                 |     |
* NAME         | DESCRIPTION                                     | I/O |
*--------------|-------------------------------------------------|-----|
* F_u          | u components of winds on advection grid         |  o  |
* F_v          | v components of winds on advection grid         |  o  |
* F_w          | w components of winds on advection grid         |  o  |
*              |                                                 |     |
* F_nit        | \ total number of points in x,y direction in    |  i  |
* F_njt        | / advection grid (including halos)              |  i  |
*              |                                                 |     |
* F_nk         | number of levels                                |  i  |
*______________|_________________________________________________|_____|
*
*implicits
#include "gmm.hf"
#include "var_gmm.cdk"
!      type(gmm_metadata) :: meta3d
!      integer :: gmmstat
#include "glb_ld.cdk"
!      logical G_lam, G_periodx, G_periody
!      integer G_ni
!      integer G_halox, G_haloy
!      logical l_north, l_south, l_east, l_west
!      integer l_ni, l_nj, l_niu, l_njv
!      integer l_minx, l_maxx, l_miny, l_maxy
#include "adw.cdk"
!      integer adw_nic,adw_njc, adw_halox, adw_haloy
!      real*8 adw_cy_8(),adw_cx_8(),adw_sx_8(),adw_wx_8(),adw_sy_8()
#include "inuvl.cdk"
!      real*8 inuvl_wxux3_8(XDIST_SHAPE,4)
!      real*8 inuvl_wyvy3_8(YDIST_SHAPE,4)
#include "vth.cdk"
!      real, pointer, dimension (:,:,:) :: uth,vth,zdth
!      character(len=8) :: gmmk_uth_s, gmmk_vth_s, gmmk_zdth_s
c$$$#include "acid.cdk"
!      logical acid_test_l
*
      integer i, j, k, i0, in, j0, jn, n, dest_ni
      real*8 ONE,sc
      parameter( ONE = 1.0 )
*
*     ---------------------------------------------------------------
*
      call msg(MSG_DEBUG,'PREPARE WINDS: (S/R ADW_MAIN_1_WND)')

      gmmstat = gmm_get(gmmk_uth_s,uth,meta3d)
      if (GMM_IS_ERROR(gmmstat)) 
     $     print *,'adw_main_1_wnd ERROR at gmm_get(uth)'
      gmmstat = gmm_get(gmmk_vth_s,vth,meta3d)
      if (GMM_IS_ERROR(gmmstat)) 
     $     print *,'adw_main_1_wnd ERROR at gmm_get(vth)'
      gmmstat = gmm_get(gmmk_zdth_s,zdth,meta3d)
      if (GMM_IS_ERROR(gmmstat)) 
     $     print *,'adw_main_1_wnd ERROR at gmm_get(zdth)'

c$$$      if (Acid_test_L) call acid_stat_adw_main_1_wnd(uth,1)

***********************************************************************
* Interpolate advection winds to geopotential grid
* F_u and F_v are used as work space
***********************************************************************

      call rpn_comm_xch_halo (uth,LDIST_DIM,l_niu,l_nj, F_nk,
     %             G_halox,G_haloy,G_periodx,G_periody,l_ni,0)
      call rpn_comm_xch_halo (vth,LDIST_DIM,l_ni,l_njv,F_nk,
     %             G_halox,G_haloy,G_periodx,G_periody,l_ni,0)
*
!$omp parallel private(i0,in,j0,jn,sc)
!$omp do
      do k=1,F_nk
*
         i0 = 1
         in = l_niu
         j0 = 1
         jn = l_nj
         if (G_lam) then
             if (l_west) i0 = 3
             if (l_east) in = l_niu - 1
         endif
         do j = j0, jn
         do i = i0, in
            F_u(i,j,k) = inuvl_wxux3_8(i,1) * uth(i-2,j,k)
     %                 + inuvl_wxux3_8(i,2) * uth(i-1,j,k)
     %                 + inuvl_wxux3_8(i,3) * uth(i  ,j,k)
     %                 + inuvl_wxux3_8(i,4) * uth(i+1,j,k)
         enddo
         enddo
*
         i0 = 1
         in = l_ni
         jn = l_njv
         if (l_south) j0 = 3
         if (l_north) jn = l_njv - 1
         do j = j0, jn
         do i = i0, in
            F_v(i,j,k) = inuvl_wyvy3_8(j,1) * vth(i,j-2,k)
     %                 + inuvl_wyvy3_8(j,2) * vth(i,j-1,k)
     %                 + inuvl_wyvy3_8(j,3) * vth(i,j  ,k)
     %                 + inuvl_wyvy3_8(j,4) * vth(i,j+1,k)
         enddo
         enddo
         if (.not.G_lam) then
         if (l_south) then
            do i = i0, in
            F_v(i,j0-2,k) = inuvl_wyvy3_8(j0-2,3) * vth(i,j0-2,k)
     %                    + inuvl_wyvy3_8(j0-2,4) * vth(i,j0-1,k)
            F_v(i,j0-1,k) = inuvl_wyvy3_8(j0-1,2) * vth(i,j0-2,k)
     %                    + inuvl_wyvy3_8(j0-1,3) * vth(i,j0-1,k)
     %                    + inuvl_wyvy3_8(j0-1,4) * vth(i,j0,k  )
            enddo
         endif
         if (l_north) then
            do i = i0, in
            F_v(i,jn+2,k) = inuvl_wyvy3_8(jn+2,1) * vth(i,jn  ,k)
     %                    + inuvl_wyvy3_8(jn+2,2) * vth(i,jn+1,k)
            F_v(i,jn+1,k) = inuvl_wyvy3_8(jn+1,1) * vth(i,jn-1,k)
     %                    + inuvl_wyvy3_8(jn+1,2) * vth(i,jn  ,k)
     %                    + inuvl_wyvy3_8(jn+1,3) * vth(i,jn+1,k)
            enddo
         endif
         endif
      enddo
!$omp enddo
***********************************************************************
* Image to component
***********************************************************************
      i0=1
      in=l_ni
      j0=1
      jn=l_nj
      if (G_lam) then
             if (l_south) j0 = 3
             if (l_north) jn = l_njv - 1
      endif
!$omp do
      do k = 1,F_nk
      do j = j0,jn
*
      sc = ONE/Adw_cy_8(j)
*
      do i = i0,in
*
      vth(i,j,k) = sc * F_v(i,j,k)
*
      enddo
      enddo
      enddo
!$omp enddo
      i0=1
      in=l_ni
      j0=1
      jn=l_nj
      if (G_lam) then
             if (l_west) i0 = 3
             if (l_east) in = l_niu - 1
      endif
!$omp do 
      do k = 1,F_nk
      do j = j0,jn
*
      sc = ONE/Adw_cy_8(j)
*
      do i = i0,in
*
      uth(i,j,k) = sc * F_u(i,j,k)
*
      enddo
      enddo
      enddo
!$omp enddo


***********************************************************************
* Adjust wind fields to advection grid
***********************************************************************
      if (G_lam) then
          n=0
          dest_ni=l_ni
      else
          n=999
          dest_ni=G_ni
      endif
*
c$$$      if (Acid_test_L)call acid_stat_adw_main_1_wnd(uth,2)
!$omp single
      call rpn_comm_xch_halox ( uth, LDIST_DIM, l_ni, l_nj, F_nk, 
     %  Adw_halox, Adw_haloy, G_periodx, G_periody, F_u, 1-Adw_halox, 
     %  Adw_nic+Adw_halox, 1-Adw_haloy, Adw_njc+Adw_haloy, dest_ni, n)
*
      call rpn_comm_xch_halox ( vth, LDIST_DIM, l_ni, l_nj, F_nk, 
     %  Adw_halox, Adw_haloy, G_periodx, G_periody, F_v, 1-Adw_halox, 
     %  Adw_nic+Adw_halox, 1-Adw_haloy, Adw_njc+Adw_haloy, dest_ni, n)
*
      call rpn_comm_xch_halox ( zdth, LDIST_DIM, l_ni, l_nj, F_nk+1, 
     %  Adw_halox, Adw_haloy, G_periodx, G_periody, F_w, 1-Adw_halox, 
     %  Adw_nic+Adw_halox, 1-Adw_haloy, Adw_njc+Adw_haloy, dest_ni, n)
!$omp end single

*
      if (.not.G_lam) then
*
         if ( l_south ) then
*
            call adw_polw (F_u,F_v,Adw_cx_8,Adw_sx_8,Adw_wx_8,
     %        Adw_sy_8,0,Adw_nic,Adw_halox,Adw_njc,Adw_haloy,F_nk)
            call adw_pols (F_w,Adw_wx_8,0,Adw_nic,Adw_halox,Adw_njc,
     %        Adw_haloy,F_nk+1)
         endif
*
         if ( l_north ) then
*
            call adw_polw (F_u,F_v,Adw_cx_8,Adw_sx_8,Adw_wx_8,
     %        Adw_sy_8,Adw_njc+1,Adw_nic,Adw_halox,Adw_njc,Adw_haloy,F_nk)
            call adw_pols (F_w,Adw_wx_8,Adw_njc+1,Adw_nic,Adw_halox,
     %        Adw_njc,Adw_haloy,F_nk+1)
         endif

      endif
!$omp end parallel
*
*     ---------------------------------------------------------------
*
      return
      end
