*** s/r e_moclim2 reading and horizontal interpolation
*
#include <model_macros_f.h>
*
      integer function e_moclim2 (  f, ni, nj, nomvar, ip1, ip3, 
     $     month, day, etik, typvar, dest_g, interp, un1, stdo)
*
#include "impnone.cdk"
*
      character* (*) nomvar,interp,etik,typvar
      integer ni, nj, ip1, ip3, month, day, dest_g, un1, stdo
      real  f(*)
*
*author   andre methot - cmc - april 94
*
*revision
*
* v2_21 - Desgagne M.       - major re-organization + use of e_rdhint
* v3_00 - Desgagne & Lee    - Lam configuration
*
*object
*       Return a weighted time averaged climatological field.
*       The average is weighted by the day of the month.
*
*implicites
*
      integer  e_rdhint3
      external e_rdhint3
      integer moa, jj, i, err
      real poa, poam, w1(ni*nj) 
*
*---------------------------------------------------------------------
*
*     Computing the closest neighbouring month and weight
*
      if ( day .gt. 15 ) then
         jj = 32 - day
         if ( jj .gt. 15 ) jj = 15
         moa = month + 1
         if ( month .eq. 12 ) moa = 1
      else
         jj = day
         moa = month - 1
         if ( month .eq. 1 ) moa = 12
      endif
      poa = ( jj + 13 ) / 28.
*
      write (stdo,1000) month,moa
*
      e_moclim2 = -1
      if (e_rdhint3(f, dest_g,ni,nj,nomvar,ip1,month,-1,etik,
     $          typvar,.false.,.false.,interp,un1,stdo ).lt.0) return
*
      if (e_rdhint3(w1,dest_g,ni,nj,nomvar,ip1,moa   ,-1,etik,
     $          typvar,.false.,.false.,interp,un1,stdo ).lt.0) return
*
*     Compute weighted average
*
      poam = 1. - poa
      do i=1,ni*nj
         f(i) = poa*f(i) + poam*w1(i)
      enddo
      e_moclim2 = 0
*
 1000 format ('CLIMATOLOGY: AVERAGING BETWEEN MONTHS ',2i3)
*---------------------------------------------------------------------
*
      return
      end

