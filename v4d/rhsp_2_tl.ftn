!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer, 
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms 
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer 
!version 3 or (at your option) any later version that should be found at: 
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software; 
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), 
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
***s/r rhsp_2_tl - TLM of rhsp_2 
*
#include "model_macros_f.h"
*
      subroutine rhsp_2_tl ( F_oru, F_orv, F_orc,F_ort,F_orw,   F_orf,
     $                       F_ruw1,F_rvw1,F_ru, F_rv, F_nest_u,F_nest_v,
     $                       F_u,   F_v,   F_w,  F_t,  F_s,     F_zd,
     $                       F_BsPq,F_fip, F_mu,
*
     $                       F_oru_m, F_orv_m, F_orc_m,F_ort_m,F_orw_m,   F_orf_m,
     $                       F_ruw1_m,F_rvw1_m,F_ru_m, F_rv_m, F_nest_u_m,F_nest_v_m,
     $                       F_u_m,   F_v_m,   F_w_m,  F_t_m,  F_s_m,     F_zd_m,
     $                       F_BsPq_m,F_fip_m, F_mu_m,
*
     $                       DIST_DIM, Nk )
*
      implicit none
*
      integer DIST_DIM, Nk
*
      real F_oru   (DIST_SHAPE,  Nk)  ,F_orv   (DIST_SHAPE,  Nk)  ,
     $     F_orc   (DIST_SHAPE,  Nk)  ,F_ort   (DIST_SHAPE,  Nk+1),
     $     F_orw   (DIST_SHAPE,  Nk+1),F_orf   (DIST_SHAPE,  Nk+1),
     $     F_ruw1  (DIST_SHAPE,  Nk)  ,F_rvw1  (DIST_SHAPE,  Nk)  ,
     $     F_ru    (DIST_SHAPE,  Nk)  ,F_rv    (DIST_SHAPE,  Nk)  ,
     $     F_nest_u(DIST_SHAPE,  Nk)  ,F_nest_v(DIST_SHAPE,  Nk)  ,
     $     F_u     (DIST_SHAPE,  Nk)  ,F_v     (DIST_SHAPE,  Nk)  ,
     $     F_w     (DIST_SHAPE,  Nk+1),F_t     (DIST_SHAPE,  Nk+1),
     $     F_s     (DIST_SHAPE)       ,F_zd    (DIST_SHAPE,  Nk+1),
     $     F_BsPq  (DIST_SHAPE,0:Nk+1),F_fip   (DIST_SHAPE,0:Nk+1),
     $     F_mu    (DIST_SHAPE,  Nk+1)
*
      real F_oru_m   (DIST_SHAPE,  Nk)  ,F_orv_m   (DIST_SHAPE,  Nk)  ,
     $     F_orc_m   (DIST_SHAPE,  Nk)  ,F_ort_m   (DIST_SHAPE,  Nk+1),
     $     F_orw_m   (DIST_SHAPE,  Nk+1),F_orf_m   (DIST_SHAPE,  Nk+1),
     $     F_ruw1_m  (DIST_SHAPE,  Nk)  ,F_rvw1_m  (DIST_SHAPE,  Nk)  ,
     $     F_ru_m    (DIST_SHAPE,  Nk)  ,F_rv_m    (DIST_SHAPE,  Nk)  ,
     $     F_nest_u_m(DIST_SHAPE,  Nk)  ,F_nest_v_m(DIST_SHAPE,  Nk)  ,
     $     F_u_m     (DIST_SHAPE,  Nk)  ,F_v_m     (DIST_SHAPE,  Nk)  ,
     $     F_w_m     (DIST_SHAPE,  Nk+1),F_t_m     (DIST_SHAPE,  Nk+1),
     $     F_s_m     (DIST_SHAPE)       ,F_zd_m    (DIST_SHAPE,  Nk+1),
     $     F_BsPq_m  (DIST_SHAPE,0:Nk+1),F_fip_m   (DIST_SHAPE,0:Nk+1),
     $     F_mu_m    (DIST_SHAPE,  Nk+1)
*
*author
*     M.Tanguay
*
*revision
* v2_10 - Tanguay M.        - initial MPI version
* v2_21 - Tanguay M.        - replace xfis by topo
* v2_31 - Tanguay M.        - adapt for tracers in tr3d
* v3_03 - Tanguay M.        - Adjoint Lam and NoHyd configuration
* v3_11 - Tanguay M.        - AIXport+Opti+OpenMP for TLM-ADJ
* v3_21 - Tanguay M.        - Revision Openmp
* v4_04 - Tanguay M.        - Staggered version TL/AD 
* v4_12 - Tanguay M.        - OPENMP TL/AD
* v4_12 - Tanguay M.        - Adapt to revised rhsp 
* v4_XX - Tanguay M.        - Adjustments GEM413
*
*object
*     see id section 
*
*implicits
#include "glb_ld.cdk"
#include "cori.cdk"
#include "cstv.cdk"
#include "dcst.cdk"
#include "geomg.cdk"
#include "schm.cdk"
#include "intuv.cdk"
#include "inuvl.cdk"
#include "type.cdk"
#include "ver.cdk"
#include "lun.cdk"
#include "that.cdk"
*
      integer i, j, k, i0, j0, in, jn, nij
      real    tdiv, tdiv_m
      real*8  c1_8,c2_8,c3_8,BsPqbarz_8,fipbarz_8,
     $        wk1_8(DIST_SHAPE),wk2_8(DIST_SHAPE),barz_8,barzp_8,ww1_8,
     $        xxx_8,yyy_8,xlog_8(l_ni,l_nj),ylog_8(l_ni,l_nj)
*
      real*8  BsPqbarz_m_8,fipbarz_m_8,
     $        wk1_m_8(DIST_SHAPE),wk2_m_8(DIST_SHAPE),
     $        barz_m_8, barzp_m_8,ww1_m_8,
     $        xlog_m_8(l_ni,l_nj),ylog_m_8(l_ni,l_nj)
*
      real*8  ONE_8
      parameter( ONE_8=1.d0 )
*
*     __________________________________________________________________
*
      if (.not.Cori_cornl_L) call handle_error(-1,'rhsp_2_tl','rhsp_2_TL : NOT Cori_cornl_L NOT DONE')
*
*     Common coefficients 
*     -------------------
      c1_8  = Cstv_Beta_8 * Dcst_rgasd_8 / ( Dcst_rayt_8 * Dcst_rayt_8 )
      c2_8  = Cstv_Beta_8 / ( Dcst_rayt_8 * Dcst_rayt_8 )
*
      if (Cori_cornl_L) then
         c3_8 = Cstv_Beta_8
      else
         c3_8 = Cstv_Beta_8 - ONE_8 
      endif
* 
*     Exchanging halos for derivatives & interpolation 
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*
*     TRAJECTORY
*     ----------
      call rpn_comm_xch_halo( F_u_m , LDIST_DIM,l_niu,l_nj,G_nk,
     $            G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo( F_v_m , LDIST_DIM,l_ni,l_njv,G_nk,
     $            G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo( F_t_m , LDIST_DIM,l_ni,l_nj,G_nk+1,
     $            G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo(F_BsPq_m,LDIST_DIM,l_ni,l_nj,G_nk+2,
     $            G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo( F_fip_m, LDIST_DIM,l_ni,l_nj,G_nk+2,
     $            G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      if (.not. Schm_hydro_L) then
         call rpn_comm_xch_halo( F_mu_m, LDIST_DIM,l_ni,l_nj,G_nk+1,
     $               G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      endif
*
*     TLM 
*     ---
      call rpn_comm_xch_halo( F_u , LDIST_DIM,l_niu,l_nj,G_nk,
     $            G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo( F_v , LDIST_DIM,l_ni,l_njv,G_nk,
     $            G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo( F_t , LDIST_DIM,l_ni,l_nj,G_nk+1,
     $            G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo(F_BsPq,LDIST_DIM,l_ni,l_nj,G_nk+2,
     $            G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo( F_fip, LDIST_DIM,l_ni,l_nj,G_nk+2,
     $            G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      if (.not. Schm_hydro_L) then
         call rpn_comm_xch_halo( F_mu, LDIST_DIM,l_ni,l_nj,G_nk+1,
     $               G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      endif 
*
      nij = l_ni*l_nj
*
!$omp parallel private(i,j,i0,j0,jn,in,BsPqbarz_8,fipbarz_8,
!$omp$          tdiv,xlog_8,ylog_8,wk1_8,wk2_8,
!$omp$          ww1_8,xxx_8,yyy_8,barz_8,barzp_8,
!$omp$          tdiv_m,xlog_m_8,ylog_m_8,wk1_m_8,wk2_m_8,
!$omp$          barz_m_8,barzp_m_8)
*
!$omp do 
      do 1000 k = 1,l_nk 
*
*********************************
* Compute Ru: RHS of U equation *
*********************************
*
*     Set wk1 to 1 in the hydrostatic case as mu=0
*     --------------------------------------------
      if (Schm_hydro_L) then
         do j = 1, l_nj
         do i = 1, l_ni
*
*           TRAJECTORY
*           ----------
            wk1_m_8(i,j) = ONE_8 
*
*
*           TLM
*           ---
            wk1_8(i,j) = 0.0 
*
         end do
         end do
      endif
*
*     Set indices to compute Ru
*     -------------------------
      i0 = 1
      in = l_niu
      j0 = 1
      jn = l_nj
*
*     Compute (1 + mu) barx barz in wk1
*     ---------------------------------
      if (.not.Schm_hydro_L) then
         do j = j0, jn
         do i = i0, in
*
*           TRAJECTORY
*           ----------
            barz_m_8  = Ver_wp_8%m(k)*F_mu_m(i  ,j,k+1)
     $                 +Ver_wm_8%m(k)*F_mu_m(i  ,j,k)
            barzp_m_8 = Ver_wp_8%m(k)*F_mu_m(i+1,j,k+1)
     $                 +Ver_wm_8%m(k)*F_mu_m(i+1,j,k)
            wk1_m_8(i,j) = ONE_8 + ( ONE_8 - intuv_c0xxu_8(i) ) * barz_m_8
     $                                     + intuv_c0xxu_8(i)   * barzp_m_8
*
*           TLM 
*           ---
            barz_8  = Ver_wp_8%m(k)*F_mu(i  ,j,k+1)
     $               +Ver_wm_8%m(k)*F_mu(i  ,j,k)
            barzp_8 = Ver_wp_8%m(k)*F_mu(i+1,j,k+1)
     $               +Ver_wm_8%m(k)*F_mu(i+1,j,k)
            wk1_8(i,j) =  ( ONE_8 - intuv_c0xxu_8(i) ) * barz_8
     $                            + intuv_c0xxu_8(i)   * barzp_8
*
         end do
         end do
      endif
*
      if ( abs(c3_8) .lt. 1.0e-6 ) then
*
*     NOT DONE 
*
      else
*
*        Compute Ru with the Coriolis factor
*        ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ 
*
         if (G_lam) then
*           Reset indices
            if (l_west ) i0 = 2
            if (l_east ) in = l_niu-1
            if (l_south) j0 = 3
            if (l_north) jn = l_njv-1
         endif
*
*        Compute V barX in wk2
*        ~~~~~~~~~~~~~~~~~~~~~
         do j = j0, jn
         do i = i0-1, in+2
*
*           TRAJECTORY
*           ----------
            wk2_m_8(i,j)  = inuvl_wyvy3_8(j,1) * F_v_m(i,j-2,k) 
     $                    + inuvl_wyvy3_8(j,2) * F_v_m(i,j-1,k) 
     $                    + inuvl_wyvy3_8(j,3) * F_v_m(i,j  ,k) 
     $                    + inuvl_wyvy3_8(j,4) * F_v_m(i,j+1,k)          
*
*           TLM
*           ---
            wk2_8  (i,j)  = inuvl_wyvy3_8(j,1) * F_v(i,j-2,k) 
     $                    + inuvl_wyvy3_8(j,2) * F_v(i,j-1,k) 
     $                    + inuvl_wyvy3_8(j,3) * F_v(i,j  ,k) 
     $                    + inuvl_wyvy3_8(j,4) * F_v(i,j+1,k)          
*
         end do
         end do
*
*        Ru with the Coriolis factor
*        ~~~~~~~~~~~~~~~~~~~~~~~~~~~
         do j= j0, jn
         do i= i0, in
*
*           TRAJECTORY
*           ----------
            barz_m_8  = Ver_wp_8%m(k)*F_t_m(i  ,j,k+1)
     %                 +Ver_wm_8%m(k)*F_t_m(i  ,j,k)
            barzp_m_8 = Ver_wp_8%m(k)*F_t_m(i+1,j,k+1)
     %                 +Ver_wm_8%m(k)*F_t_m(i+1,j,k)
*
*           TLM
*           ---
            barz_8  = Ver_wp_8%m(k)*F_t(i  ,j,k+1)
     %               +Ver_wm_8%m(k)*F_t(i  ,j,k)
            barzp_8 = Ver_wp_8%m(k)*F_t(i+1,j,k+1)
     %               +Ver_wm_8%m(k)*F_t(i+1,j,k)
*
*           TRAJECTORY
*           ----------
            F_oru_m(i,j,k) = Cstv_invT_8 * F_u_m(i,j,k)
     $               - c1_8 * ( ( ONE_8 - intuv_c0xxu_8(i) )* barz_m_8
     $                                  + intuv_c0xxu_8(i)  * barzp_m_8 )
     $                      * ( F_BsPq_m(i+1,j,k) - F_BsPq_m(i,j,k) )
     $                      * Geomg_invDXu_8(i) 
     $               - c2_8 * wk1_m_8(i,j) * ( F_fip_m(i+1,j,k) - F_fip_m(i,j,k) )
     $                      * Geomg_invDXu_8(i) 
     $               + c3_8 * Cori_fcoru_8(i,j)
     $                      * ( inuvl_wxxu3_8(i,1)*wk2_m_8(i-1,j)
     $                        + inuvl_wxxu3_8(i,2)*wk2_m_8(i  ,j)
     $                        + inuvl_wxxu3_8(i,3)*wk2_m_8(i+1,j)
     $                        + inuvl_wxxu3_8(i,4)*wk2_m_8(i+2,j) )
*
*           TLM
*           ---
            F_oru(i,j,k) = Cstv_invT_8 * F_u(i,j,k)
*
     $               - c1_8 * ( ( ONE_8 - intuv_c0xxu_8(i) )* barz_m_8
     $                                  + intuv_c0xxu_8(i)  * barzp_m_8 )
     $                      * ( F_BsPq  (i+1,j,k) - F_BsPq  (i,j,k) )
     $                      * Geomg_invDXu_8(i) 
*
     $               - c1_8 * ( ( ONE_8 - intuv_c0xxu_8(i) )* barz_8
     $                                  + intuv_c0xxu_8(i)  * barzp_8 )
     $                      * ( F_BsPq_m(i+1,j,k) - F_BsPq_m(i,j,k) )
     $                      * Geomg_invDXu_8(i) 
*
     $               - c2_8 * wk1_m_8(i,j) 
     $                      * ( F_fip  (i+1,j,k) - F_fip  (i,j,k) )
     $                      * Geomg_invDXu_8(i) 
*
     $               - c2_8 * wk1_8(i,j) 
     $                      * ( F_fip_m(i+1,j,k) - F_fip_m(i,j,k) )
     $                      * Geomg_invDXu_8(i) 
*
     $               + c3_8 * Cori_fcoru_8(i,j)
     $                      * ( inuvl_wxxu3_8(i,1)*wk2_8(i-1,j)
     $                        + inuvl_wxxu3_8(i,2)*wk2_8(i  ,j)
     $                        + inuvl_wxxu3_8(i,3)*wk2_8(i+1,j)
     $                        + inuvl_wxxu3_8(i,4)*wk2_8(i+2,j) )

*
         end do
         end do
*
      endif
*
*********************************
* Compute Rv: RHS of V equation *
*********************************
*
*     Set indices to compute Rv
*     -------------------------
      i0 = 1
      in = l_ni
      j0 = 1
      jn = l_njv
*
*     Compute (1 + mu) bary barz in wk1
*     ---------------------------------
      if (.not. Schm_hydro_L) then
         do j = j0, jn
         do i = i0, in
*
*           TRAJECTORY
*           ----------
            barz_m_8  = Ver_wp_8%m(k)*F_mu_m(i,j  ,k+1)
     %                 +Ver_wm_8%m(k)*F_mu_m(i,j  ,k)
            barzp_m_8 = Ver_wp_8%m(k)*F_mu_m(i,j+1,k+1)
     %                 +Ver_wm_8%m(k)*F_mu_m(i,j+1,k)
            wk1_m_8(i,j) = ONE_8 + ( ONE_8 - intuv_c0yyv_8(j) ) * barz_m_8 
     $                                     + intuv_c0yyv_8(j)   * barzp_m_8
*
*           TLM
*           ---
            barz_8  = Ver_wp_8%m(k)*F_mu(i,j  ,k+1)
     %               +Ver_wm_8%m(k)*F_mu(i,j  ,k)
            barzp_8 = Ver_wp_8%m(k)*F_mu(i,j+1,k+1)
     %               +Ver_wm_8%m(k)*F_mu(i,j+1,k)
            wk1_8(i,j) =  ( ONE_8 - intuv_c0yyv_8(j) ) * barz_8
     $                            + intuv_c0yyv_8(j)   * barzp_8
*
         end do
         end do
      endif
*
      if ( abs(c3_8) .lt. 1.0e-6 ) then
*
*     NOT DONE
*
      else
*
*        Compute Rv with the Coriolis factor
*        ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         if (G_lam) then
*           Reset indices to compute Rv
            if (l_west ) i0 = 3
            if (l_east ) in = l_niu-1
            if (l_south) j0 = 2
            if (l_north) jn = l_njv-1
         endif
*
*        Compute U barY in wk2
*        ~~~~~~~~~~~~~~~~~~~~~
         do j = j0-1, jn+2
         do i = i0, in
*
*           TRAJECTORY
*           ----------
            wk2_m_8(i,j)  = inuvl_wxux3_8(i,1)*F_u_m(i-2,j,k) 
     $                    + inuvl_wxux3_8(i,2)*F_u_m(i-1,j,k) 
     $                    + inuvl_wxux3_8(i,3)*F_u_m(i  ,j,k) 
     $                    + inuvl_wxux3_8(i,4)*F_u_m(i+1,j,k) 
*
*           TLM
*           ---
            wk2_8  (i,j)  = inuvl_wxux3_8(i,1)*F_u(i-2,j,k) 
     $                    + inuvl_wxux3_8(i,2)*F_u(i-1,j,k) 
     $                    + inuvl_wxux3_8(i,3)*F_u(i  ,j,k) 
     $                    + inuvl_wxux3_8(i,4)*F_u(i+1,j,k) 
         end do
         end do
*
*        Rv with the Coriolis factor
*        ~~~~~~~~~~~~~~~~~~~~~~~~~~~
         do j = j0, jn
         do i = i0, in
*
*           TRAJECTORY
*           ----------
            barz_m_8  = Ver_wp_8%m(k)*F_t_m(i,j  ,k+1)
     %                 +Ver_wm_8%m(k)*F_t_m(i,j  ,k)
            barzp_m_8 = Ver_wp_8%m(k)*F_t_m(i,j+1,k+1)
     %                 +Ver_wm_8%m(k)*F_t_m(i,j+1,k)
*
*           TLM
*           ---
            barz_8  = Ver_wp_8%m(k)*F_t(i,j  ,k+1)
     %               +Ver_wm_8%m(k)*F_t(i,j  ,k)
            barzp_8 = Ver_wp_8%m(k)*F_t(i,j+1,k+1)
     %               +Ver_wm_8%m(k)*F_t(i,j+1,k)
*
*           TRAJECTORY
*           ----------
            F_orv_m(i,j,k) = Cstv_invT_8 * F_v_m(i,j,k)
     $               - c1_8 *( ( ONE_8 - intuv_c0yyv_8(j) ) * barz_m_8
     $                                 + intuv_c0yyv_8(j)   * barzp_m_8 )
     $                      * ( F_BsPq_m(i,j+1,k) - F_BsPq_m (i,j,k) )
     $                      * Geomg_invDYv_8(j) 
     $               - c2_8 * wk1_m_8(i,j) * ( F_fip_m(i,j+1,k) - F_fip_m(i,j,k) )
     $                      * Geomg_invDYv_8(j) 
     $               - c3_8 * Cori_fcorv_8(i,j)
     $                      * ( inuvl_wyyv3_8(j,1)*wk2_m_8(i,j-1)
     $                        + inuvl_wyyv3_8(j,2)*wk2_m_8(i,j  )
     $                        + inuvl_wyyv3_8(j,3)*wk2_m_8(i,j+1)
     $                        + inuvl_wyyv3_8(j,4)*wk2_m_8(i,j+2) )
*
*           TLM
*           ---
            F_orv(i,j,k) = Cstv_invT_8 * F_v(i,j,k)
*
     $               - c1_8 *( ( ONE_8 - intuv_c0yyv_8(j) ) * barz_m_8
     $                                 + intuv_c0yyv_8(j)   * barzp_m_8 )
     $                      * ( F_BsPq(i,j+1,k) - F_BsPq (i,j,k) )
     $                      * Geomg_invDYv_8(j) 
*
     $               - c1_8 *( ( ONE_8 - intuv_c0yyv_8(j) ) * barz_8
     $                                 + intuv_c0yyv_8(j)   * barzp_8 )
     $                      * ( F_BsPq_m(i,j+1,k) - F_BsPq_m (i,j,k) )
     $                      * Geomg_invDYv_8(j) 
*
     $               - c2_8 * wk1_m_8(i,j) 
     $                      * ( F_fip(i,j+1,k) - F_fip(i,j,k) )
     $                      * Geomg_invDYv_8(j) 
*
     $               - c2_8 * wk1_8(i,j) 
     $                      * ( F_fip_m(i,j+1,k) - F_fip_m(i,j,k) )
     $                      * Geomg_invDYv_8(j) 
*
     $               - c3_8 * Cori_fcorv_8(i,j)
     $                      * ( inuvl_wyyv3_8(j,1)*wk2_8(i,j-1)
     $                        + inuvl_wyyv3_8(j,2)*wk2_8(i,j  )
     $                        + inuvl_wyyv3_8(j,3)*wk2_8(i,j+1)
     $                        + inuvl_wyyv3_8(j,4)*wk2_8(i,j+2) )
*
         end do
         end do
*
      endif
*
 1000 continue
!$omp enddo
*
*     Indices for computing all fields on mass grid at routine output
*     ---------------------------------------------------------------
      i0 = 1
      in = l_ni
      j0 = 1
      jn = l_nj
      if (G_lam) then
         if (l_west)  i0 = 4
         if (l_east)  in = l_niu-2
         if (l_south) j0 = 4
         if (l_north) jn = l_njv-2
      endif
*
!$omp do
      do 2000 k = 1,l_nk+1
*
*********************************************
* Compute Rt: RHS of thermodynamic equation *
* Compute Rf: RHS of FI equation            *
*********************************************
*
      ww1_8 = ONE_8 / Cstv_tstr_8
      do j = j0, jn
      do i = i0, in
*
*        TRAJECTORY
*        ----------
         xlog_m_8(i,j) = F_t_m(i,j,k) * ww1_8
*
*        TLM
*        ---
         xlog_8  (i,j) = F_t  (i,j,k) * ww1_8
*
      end do
      end do
*
*     TRAJECTORY
*     ----------
      call vlog( ylog_m_8, xlog_m_8, nij )
*
*     TLM
*     ---
      do j= j0, jn
      do i= i0, in
         ylog_8 (i,j) = xlog_8(i,j)/xlog_m_8(i,j)
      end do
      end do
*
                         xxx_8=Dcst_cappa_8
      if(Schm_autobar_L) xxx_8=0.d0
*
      do j= j0, jn
      do i= i0, in
*
*        TRAJECTORY
*        ----------
         BsPqbarz_m_8 = Ver_wp_8%t(k)*F_BsPq_m(i,j,k)+Ver_wm_8%t(k)*F_BsPq_m(i,j,k-1)
         fipbarz_m_8  = Ver_wp_8%t(k)* F_fip_m(i,j,k)+Ver_wm_8%t(k)* F_fip_m(i,j,k-1)
*
*        TLM
*        --- 
         BsPqbarz_8 = Ver_wp_8%t(k)*F_BsPq(i,j,k)+Ver_wm_8%t(k)*F_BsPq(i,j,k-1)
         fipbarz_8  = Ver_wp_8%t(k)* F_fip(i,j,k)+Ver_wm_8%t(k)* F_fip(i,j,k-1)
*
*        TRAJECTORY
*        ---------- 
         F_ort_m (i,j,k) = Cstv_invT_8 * ( ylog_m_8(i,j)-ln_that%t(k) - xxx_8 * BsPqbarz_m_8 )
     $                   + Cstv_Beta_8 * (Dcst_cappa_8-dln_that%t(k)) * F_zd_m(i,j,k)
         F_orf_m (i,j,k) = Cstv_invT_8 * fipbarz_m_8 
     $                   + Cstv_Beta_8 * Cstv_RTstr_8 * F_zd_m(i,j,k)
     $                   + Cstv_Beta_8 * Dcst_grav_8 * F_w_m(i,j,k)
*
*        TLM
*        --- 
         F_ort (i,j,k) = Cstv_invT_8 * ( ylog_8(i,j) - xxx_8 * BsPqbarz_8 )
     $                 + Cstv_Beta_8 * (Dcst_cappa_8-dln_that%t(k)) * F_zd(i,j,k)
         F_orf (i,j,k) = Cstv_invT_8 * fipbarz_8 
     $                 + Cstv_Beta_8 * Cstv_RTstr_8 * F_zd(i,j,k)
     $                 + Cstv_Beta_8 * Dcst_grav_8 * F_w(i,j,k)
*
      end do
      end do
*
**********************************
* Compute Rw: RHS of  w equation *
**********************************
*
      if (.not. Schm_hydro_L) then
         do j= j0, jn
         do i= i0, in
*
*           TRAJECTORY
*           ----------
            F_orw_m (i,j,k) = Cstv_invT_8 * F_w_m(i,j,k)
     $                      + Cstv_Beta_8 * Dcst_grav_8 * F_mu_m(i,j,k)
*
*           TLM
*           ---
            F_orw (i,j,k) = Cstv_invT_8 * F_w(i,j,k)
     $                    + Cstv_Beta_8 * Dcst_grav_8 * F_mu(i,j,k)
*
         end do
         end do
      endif
*
 2000 continue
!$omp enddo
*
******************************************
* Compute Rc: RHS of Continuity equation *
******************************************
*
!$omp do
      do 3000 k = 1,l_nk
*
      do j = j0, jn
      do i = i0, in
*
*        TRAJECTORY
*        ----------
         xlog_m_8(i,j) = ONE_8 + Ver_dbdz_8%m(k) * F_s_m(i,j)
*
*        TLM
*        ---
         xlog_8(i,j) = Ver_dbdz_8%m(k) * F_s(i,j)
*
      end do
      end do
*
*     TRAJECTORY
*     ----------
      call vlog( ylog_m_8, xlog_m_8, nij)
*
*     TLM
*     ---
      do j= j0, jn
      do i= i0, in
         ylog_8 (i,j) = xlog_8(i,j)/xlog_m_8(i,j)
      end do
      end do
*
                         xxx_8=ONE_8
      if(Schm_autobar_L) xxx_8=0.d0
*
      do j= j0, jn
      do i= i0, in
*
*         TRAJECTORY
*         ----------
          tdiv_m = Geomg_invcy2_8(j) * ( 
     $           (F_u_m(i,j,k)-F_u_m(i-1,j,k))*Geomg_invDX_8(i) 
     $         + (F_v_m(i,j,k)-F_v_m(i,j-1,k))*Geomg_invDY_8(j) )
*
*         TLM
*         ---
          tdiv = Geomg_invcy2_8(j) * ( 
     $           (F_u(i,j,k)-F_u(i-1,j,k))*geomg_invDX_8(i) 
     $         + (F_v(i,j,k)-F_v(i,j-1,k))*geomg_invDY_8(j) )
*
*         TRAJECTORY
*         ----------
          tdiv_m = tdiv_m
     $         + ( F_zd_m(i,j,k+1) - F_zd_m(i,j,k) ) * Ver_idz_8%m(k)
     $         + Ver_wp_8%m(k) * F_zd_m(i,j,k+1)
     $         + Ver_wm_8%m(k) * F_zd_m(i,j,k)
*
*         TLM
*         ---
          tdiv = tdiv
     $         + ( F_zd(i,j,k+1) - F_zd(i,j,k) ) * Ver_idz_8%m(k)
     $         + Ver_wp_8%m(k) * F_zd(i,j,k+1)
     $         + Ver_wm_8%m(k) * F_zd(i,j,k)
*
*         TRAJECTORY
*         ----------
          barz_m_8 = Ver_wp_8%m(k)*(Ver_wp_8%t(k+1)*F_BsPq_m(i,j,k+1)
     $                             +Ver_wm_8%t(k+1)*F_BsPq_m(i,j,k))
     $              +Ver_wm_8%m(k)*(Ver_wp_8%t(k)  *F_BsPq_m(i,j,k)
     $                             +Ver_wm_8%t(k)  *F_BsPq_m(i,j,k-1))
          F_orc_m (i,j,k) = Cstv_invT_8 * ( xxx_8 * barz_m_8 + ylog_m_8(i,j) )
     $                    - Cstv_Beta_8 * tdiv_m
*
*         TLM
*         --- 
          barz_8 = Ver_wp_8%m(k)*(Ver_wp_8%t(k+1)*F_BsPq(i,j,k+1)
     $                           +Ver_wm_8%t(k+1)*F_BsPq(i,j,k))
     $            +Ver_wm_8%m(k)*(Ver_wp_8%t(k)  *F_BsPq(i,j,k)
     $                           +Ver_wm_8%t(k)  *F_BsPq(i,j,k-1))
          F_orc (i,j,k) = Cstv_invT_8 * ( xxx_8 * barz_8 + ylog_8(i,j) )
     $                  - Cstv_Beta_8 * tdiv
*
      end do
      end do
*
 3000 continue
!$omp enddo
*
!$omp single
*
*******************************************************
* Interpolate Ru, Rv from U-, V-grid to G-grid, resp. *
*******************************************************
*
*     TRAJECTORY
*     ----------
      call rpn_comm_xch_halo ( F_oru_m, LDIST_DIM,l_niu,l_nj,G_nk,
     $              G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo ( F_orv_m, LDIST_DIM,l_ni,l_njv,G_nk,
     $              G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
*
*     TLM
*     ---
      call rpn_comm_xch_halo ( F_oru, LDIST_DIM,l_niu,l_nj,G_nk,
     $              G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
      call rpn_comm_xch_halo ( F_orv, LDIST_DIM,l_ni,l_njv,G_nk,
     $              G_halox,G_haloy,G_periodx,G_periody,l_ni,0 )
!$omp end single
*
!$omp do
      do k=1,l_nk
         do j = j0, jn
         do i = i0, in
*
*           TRAJECTORY
*           ----------
            F_ruw1_m(i,j,k) =  inuvl_wxux3_8(i,1) * F_oru_m(i-2,j,k)
     $                       + inuvl_wxux3_8(i,2) * F_oru_m(i-1,j,k)
     $                       + inuvl_wxux3_8(i,3) * F_oru_m(i  ,j,k)
     $                       + inuvl_wxux3_8(i,4) * F_oru_m(i+1,j,k)
*
*           TLM
*           ---
            F_ruw1  (i,j,k) =  inuvl_wxux3_8(i,1) * F_oru(i-2,j,k)
     $                       + inuvl_wxux3_8(i,2) * F_oru(i-1,j,k)
     $                       + inuvl_wxux3_8(i,3) * F_oru(i  ,j,k)
     $                       + inuvl_wxux3_8(i,4) * F_oru(i+1,j,k)
*           TRAJECTORY
*           ----------
            F_rvw1_m(i,j,k) =  inuvl_wyvy3_8(j,1) * F_orv_m(i,j-2,k)
     $                       + inuvl_wyvy3_8(j,2) * F_orv_m(i,j-1,k)
     $                       + inuvl_wyvy3_8(j,3) * F_orv_m(i,j  ,k)
     $                       + inuvl_wyvy3_8(j,4) * F_orv_m(i,j+1,k)         
*
*           TLM
*           ---
            F_rvw1  (i,j,k) =  inuvl_wyvy3_8(j,1) * F_orv(i,j-2,k)
     $                       + inuvl_wyvy3_8(j,2) * F_orv(i,j-1,k)
     $                       + inuvl_wyvy3_8(j,3) * F_orv(i,j  ,k)
     $                       + inuvl_wyvy3_8(j,4) * F_orv(i,j+1,k)         
         end do
         end do
      end do
!$omp enddo
*
***********************************************
* Apply HORIZONTAL BOUNDARY CONDITIONS if LAM *
***********************************************
*
      if (G_lam) then
          if (l_west) then
!$omp do
             do k=1,l_nk
             do j= 1+pil_s, l_nj-pil_n
*
*               TRAJECTORY
*               ----------
                F_ru_m (pil_w,j,k) = Cstv_invT_8 * F_nest_u_m(pil_w,j,k)
*
*               TLM
*               ---
                F_ru (pil_w,j,k) = Cstv_invT_8 * F_nest_u(pil_w,j,k)
*
             enddo
             enddo
!$omp enddo
          endif
          if (l_east) then
!$omp do
             do k=1,l_nk
             do j= 1+pil_s, l_nj-pil_n
*
*               TRAJECTORY
*               ----------
                F_ru_m (l_ni-pil_e,j,k) = Cstv_invT_8 * F_nest_u_m(l_ni-pil_e,j,k)
*
*               TLM
*               ---
                F_ru (l_ni-pil_e,j,k) = Cstv_invT_8 * F_nest_u(l_ni-pil_e,j,k)
*
             enddo
             enddo
!$omp enddo
          endif
          if (l_south) then
!$omp do
             do k=1,l_nk
             do i= 1+pil_w, l_ni-pil_e
*
*               TRAJECTORY
*               ----------
                F_rv_m (i,pil_s,k) = Cstv_invT_8 * F_nest_v_m(i,pil_s,k)
*
*               TLM
*               ---
                F_rv (i,pil_s,k) = Cstv_invT_8 * F_nest_v(i,pil_s,k)
*
             enddo
             enddo
!$omp enddo
          endif
          if (l_north) then
!$omp do
             do k=1,l_nk
             do i= 1+pil_w, l_ni-pil_e
*
*               TRAJECTORY
*               ----------
                F_rv_m (i,l_nj-pil_n,k) = Cstv_invT_8 * F_nest_v_m(i,l_nj-pil_n,k)
*
*               TLM
*               ---
                F_rv (i,l_nj-pil_n,k) = Cstv_invT_8 * F_nest_v(i,l_nj-pil_n,k)
*
             enddo
             enddo
!$omp enddo
          endif
      endif
*
!$omp end parallel
*
      return
      end
