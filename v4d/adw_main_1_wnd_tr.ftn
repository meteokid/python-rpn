! ------------------------------------------------------------------------------------------------------------------------------
! EC-RPN COMM Group License/Disclaimer, version 2, Last Modified: Jan 08th, 2008 , Environment Canada
! 
! This is free software, you can use/redistribute/modify it under the terms of the EC-RPN COMM Group License/Disclaimer version 2 
! or (at your option) any later version that should be found at http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
! 
! This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the EC-RPN COMM Group License/Disclaimer for more details.
! 
! You should have received a copy of the EC-RPN COMM Group License/Disclaimer along with this software; if not, write to the
! EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), CANADA, H9P 1J3 ; or send e-mail to service.rpn@ec.gc.ca
! ------------------------------------------------------------------------------------------------------------------------------
***s/r adw_main_1_wnd_tr - Equivalent to adw_main_1_wnd for TRAJECTORY 
*
#include "model_macros_f.h"
*
      subroutine adw_main_1_wnd_tr ( F_u_m, F_v_m, F_w_m, F_nit, F_njt, F_nk )
*
#include "impnone.cdk"
*
      integer F_nit, F_njt, F_nk
*
      real F_u_m(F_nit,F_njt,F_nk),
     %     F_v_m(F_nit,F_njt,F_nk),
     %     F_w_m(F_nit,F_njt,F_nk+1)
*
*author
*     monique tanguay
*
*revision
* v2_31 - Tanguay M.        - initial MPI version
* v3_00 - Tanguay M.        - adapt to restructured adw_main 
* v3_21 - Tanguay M.        - Revision Openmp
* v4_04 - Tanguay M.        - Staggered version TL/AD 
*
*language
*     fortran 77
*
*object
*     see id section
*
*TRAJECTORY of
*arguments
*______________________________________________________________________
*              |                                                 |     |
* NAME         | DESCRIPTION                                     | I/O |
*--------------|-------------------------------------------------|-----|
* F_u          | u components of winds on advection grid         |  o  |
* F_v          | v components of winds on advection grid         |  o  |
* F_w          | w components of winds on advection grid         |  o  |
*              |                                                 |     |
* F_nit        | \ total number of points in x,y direction in    |  i  |
* F_njt        | / advection grid (including halos)              |  i  |
*              |                                                 |     |
* F_nk         | number of levels                                |  i  |
*______________|_________________________________________________|_____|
*
*implicits
#include "glb_ld.cdk"
#include "glb_pil.cdk"
#include "adw.cdk"
#include "lun.cdk"
#include "inuvl.cdk"
#include "vth_m.cdk"
*
*modules
      integer  vmmlod, vmmget, vmmuld
      external vmmlod, vmmget, vmmuld
************************************************************************
      integer pnerr, pnlkey1(30), pnlod
*
      integer i, j, k, i0, in, j0, jn, n, dest_ni
      real*8 ONE_8,sc_8
      parameter( ONE_8 = 1.0 )
************************************************************************
      if (Lun_debug_L) write (Lun_out,1000)
      pnlkey1(1)  = VMM_KEY(uth_m)
      pnlkey1(2)  = VMM_KEY(vth_m)
      pnlkey1(3)  = VMM_KEY(zdth_m)
*
      pnerr = vmmlod(pnlkey1,3)
*
      pnerr = VMM_GET_VAR(uth_m)
      pnerr = VMM_GET_VAR(vth_m)
      pnerr = VMM_GET_VAR(zdth_m)
*
***********************************************************************
* Interpolate advection winds to geopotential grid
* F_u and F_v are used as work space
***********************************************************************
      call rpn_comm_xch_halo (uth_m,LDIST_DIM,l_niu,l_nj, F_nk,
     %             G_halox,G_haloy,G_periodx,G_periody,l_ni,0)
      call rpn_comm_xch_halo (vth_m,LDIST_DIM,l_ni,l_njv,F_nk,
     %             G_halox,G_haloy,G_periodx,G_periody,l_ni,0)
*
C!$omp parallel private(i0,in,j0,jn,sc_8)
C!$omp do
      do k=1,F_nk
*
         i0 = 1
         in = l_niu
         j0 = 1
         jn = l_nj
         if (G_lam) then
             if (l_west) i0 = 3
             if (l_east) in = l_niu - 1
         endif
         do j = j0, jn
         do i = i0, in
            F_u_m(i,j,k) = inuvl_wxux3_8(i,1) * uth_m(i-2,j,k)
     %                   + inuvl_wxux3_8(i,2) * uth_m(i-1,j,k)
     %                   + inuvl_wxux3_8(i,3) * uth_m(i  ,j,k)
     %                   + inuvl_wxux3_8(i,4) * uth_m(i+1,j,k)
         enddo
         enddo
*
         i0 = 1
         in = l_ni
         jn = l_njv
         if (l_south) j0 = 3
         if (l_north) jn = l_njv - 1
         do j = j0, jn
         do i = i0, in
            F_v_m(i,j,k) = inuvl_wyvy3_8(j,1) * vth_m(i,j-2,k)
     %                   + inuvl_wyvy3_8(j,2) * vth_m(i,j-1,k)
     %                   + inuvl_wyvy3_8(j,3) * vth_m(i,j  ,k)
     %                   + inuvl_wyvy3_8(j,4) * vth_m(i,j+1,k)
         enddo
         enddo
         if (.not.G_lam) then
         if (l_south) then
            do i = i0, in
            F_v_m(i,j0-2,k) = inuvl_wyvy3_8(j0-2,3) * vth_m(i,j0-2,k)
     %                      + inuvl_wyvy3_8(j0-2,4) * vth_m(i,j0-1,k)
            F_v_m(i,j0-1,k) = inuvl_wyvy3_8(j0-1,2) * vth_m(i,j0-2,k)
     %                      + inuvl_wyvy3_8(j0-1,3) * vth_m(i,j0-1,k)
     %                      + inuvl_wyvy3_8(j0-1,4) * vth_m(i,j0,k  )
            enddo
         endif
         if (l_north) then
            do i = i0, in
            F_v_m(i,jn+2,k) = inuvl_wyvy3_8(jn+2,1) * vth_m(i,jn  ,k)
     %                      + inuvl_wyvy3_8(jn+2,2) * vth_m(i,jn+1,k)
            F_v_m(i,jn+1,k) = inuvl_wyvy3_8(jn+1,1) * vth_m(i,jn-1,k)
     %                      + inuvl_wyvy3_8(jn+1,2) * vth_m(i,jn  ,k)
     %                      + inuvl_wyvy3_8(jn+1,3) * vth_m(i,jn+1,k)
            enddo
         endif
         endif
      enddo
C!$omp enddo
***********************************************************************
* Image to component
***********************************************************************
      i0=1
      in=l_ni
      j0=1
      jn=l_nj
      if (G_lam) then
             if (l_south) j0 = 3
             if (l_north) jn = l_njv - 1
      endif
C!$omp do
      do k = 1,F_nk
      do j = j0,jn
*
      sc_8 = ONE_8/Adw_cy_8(j)
*
      do i = i0,in
*
      vth_m(i,j,k) = sc_8 * F_v_m(i,j,k)
*
      enddo
      enddo
      enddo
C!$omp enddo
      i0=1
      in=l_ni
      j0=1
      jn=l_nj
      if (G_lam) then
             if (l_west) i0 = 3
             if (l_east) in = l_niu - 1
      endif
C!$omp do 
      do k = 1,F_nk
      do j = j0,jn
*
      sc_8 = ONE_8/Adw_cy_8(j)
*
      do i = i0,in
*
      uth_m(i,j,k) = sc_8 * F_u_m(i,j,k)
*
      enddo
      enddo
      enddo
C!$omp enddo

***********************************************************************
* Adjust wind fields to advection grid
***********************************************************************
      if (G_lam) then
          n=0
          dest_ni=l_ni
      else
          n=999
          dest_ni=G_ni
      endif

C!$omp single
      call rpn_comm_xch_halox ( uth_m, LDIST_DIM, l_ni, l_nj, F_nk, 
     %  Adw_halox, Adw_haloy, G_periodx, G_periody, F_u_m, 1-Adw_halox, 
     %  Adw_nic+Adw_halox, 1-Adw_haloy, Adw_njc+Adw_haloy, dest_ni, n)
*
      call rpn_comm_xch_halox ( vth_m, LDIST_DIM, l_ni, l_nj, F_nk, 
     %  Adw_halox, Adw_haloy, G_periodx, G_periody, F_v_m, 1-Adw_halox, 
     %  Adw_nic+Adw_halox, 1-Adw_haloy, Adw_njc+Adw_haloy, dest_ni, n)
*
      call rpn_comm_xch_halox ( zdth_m, LDIST_DIM, l_ni, l_nj, F_nk+1, 
     %  Adw_halox, Adw_haloy, G_periodx, G_periody, F_w_m, 1-Adw_halox, 
     %  Adw_nic+Adw_halox, 1-Adw_haloy, Adw_njc+Adw_haloy, dest_ni, n)
C!$omp end single
*
      if (.not.G_lam) then
*
         if ( l_south ) then
*
            call adw_polw (F_u_m,F_v_m,Adw_cx_8,Adw_sx_8,Adw_wx_8,
     %        Adw_sy_8,0,Adw_nic,Adw_halox,Adw_njc,Adw_haloy,F_nk)
            call adw_pols (F_w_m,Adw_wx_8,0,Adw_nic,Adw_halox,Adw_njc,
     %        Adw_haloy,F_nk+1)
         endif
*
         if ( l_north ) then
*
            call adw_polw (F_u_m,F_v_m,Adw_cx_8,Adw_sx_8,Adw_wx_8,
     %        Adw_sy_8,Adw_njc+1,Adw_nic,Adw_halox,Adw_njc,Adw_haloy,F_nk)
            call adw_pols (F_w_m,Adw_wx_8,Adw_njc+1,Adw_nic,Adw_halox,
     %        Adw_njc,Adw_haloy,F_nk+1)
         endif

      endif
C!$omp end parallel
*
 1000  format(3X,'TRAJ of PREPARE WINDS: (S/R ADW_MAIN_1_WND_TR)')
      return
      end
