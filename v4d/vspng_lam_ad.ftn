***s/r vspng_lam_ad - ADJ of vspng_lam
*
#include <model_macros_f.h>
*
      subroutine vspng_lam_ad ( F_champ, DIST_DIM, pni, pnj )
*
#include "impnone.cdk"
*
      integer DIST_DIM,pni,pnj
      real F_champ(DIST_SHAPE,*)
*
*author
*     M.Tanguay
*
*revision
* v3_03 - Tanguay M.        - initial MPI version
*
*object
*     see id section
**
*implicit
#include "glb_ld.cdk"
#include "glb_pil.cdk"
#include "vspng.cdk"
*
      integer i,j,k,iter
      real*8 c1_8,c2_8,c3_8,ONE_8,TWO_8,FOUR_8
      parameter(ONE_8=1.d0,TWO_8=2.d0,FOUR_8=4.d0)
      real wk(l_ni,l_nj,G_nk)
*
      real*8, parameter :: ZERO_8 = 0.0
*---------------------------------------------------------------------
*
*     Zero adjoint variables
*     ----------------------
      do k=1,G_nk
      do j=1,l_nj
      do i=1,l_ni
      wk(i,j,k) = ZERO_8
      enddo
      enddo
      enddo
*
      do iter = Vspng_niter,1,-1
*
         do k=Vspng_nk,1,-1
*
            do j=1+pil_s, pnj-pil_n
            do i=1+pil_w, pni-pil_e
               wk(i,j,k) = F_champ(i,j,k) + wk(i,j,k) 
            end do
            end do
*
            c1_8= (ONE_8-TWO_8*Vspng_nu(k))*Vspng_nu(k)
            c2_8= Vspng_nu(k)**TWO_8
            c3_8= FOUR_8*(Vspng_nu(k)-ONE_8)*Vspng_nu(k)
            do j=l_nj,1,-1
            do i=l_ni,1,-1
               F_champ(i  ,j+1,k) = c1_8*(wk(i,j,k)) + F_champ(i  ,j+1,k)
               F_champ(i+1,j  ,k) = c1_8*(wk(i,j,k)) + F_champ(i+1,j  ,k)
               F_champ(i  ,j-1,k) = c1_8*(wk(i,j,k)) + F_champ(i  ,j-1,k)
               F_champ(i-1,j  ,k) = c1_8*(wk(i,j,k)) + F_champ(i-1,j  ,k)
*
               F_champ(i-1,j+1,k) = c2_8*(wk(i,j,k)) + F_champ(i-1,j+1,k)
               F_champ(i+1,j+1,k) = c2_8*(wk(i,j,k)) + F_champ(i+1,j+1,k) 
               F_champ(i-1,j-1,k) = c2_8*(wk(i,j,k)) + F_champ(i-1,j-1,k) 
               F_champ(i+1,j-1,k) = c2_8*(wk(i,j,k)) + F_champ(i+1,j-1,k)
*
               F_champ(i  ,j  ,k) = c3_8*(wk(i,j,k)) + F_champ(i  ,j  ,k) 
            end do
            end do
*
         end do
*
         call rpn_comm_adj_halo (F_champ,LDIST_DIM,l_ni,l_nj,
     $                 G_nk,G_halox,G_haloy,G_periodx,G_periody,l_ni,0)
*
         call v4d_zerohalo (F_champ,l_ni,l_nj,LDIST_DIM, l_nk)
*
      end do
*
*----------------------------------------------------------------------
      return
      end

