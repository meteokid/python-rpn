***s/r hzd_hof_lam_ad - ADJ of hzd_hof_lam_tl 
*
#include "model_macros_f.h"
*
      subroutine hzd_hof_lam_ad ( F_u   , F_v  , F_psd , F_tp  , F_ip ,
     %                            F_td  , F_t  , F_it  , F_plt ,   
     %                            F_pip , F_w  , F_qp  , F_mu  , F_mul,  
     %                            F_pipm, F_qpm, DIST_DIM,Nk)
*
      implicit none
*
      integer DIST_DIM, Nk
*
      real   F_u  (DIST_SHAPE,Nk), F_v (DIST_SHAPE,Nk),
     %       F_psd(DIST_SHAPE,Nk), F_tp(DIST_SHAPE,Nk),
     %       F_ip (DIST_SHAPE,Nk), F_td(DIST_SHAPE,Nk),
     %       F_t  (DIST_SHAPE,Nk), F_it(DIST_SHAPE,Nk),
     %       F_plt(DIST_SHAPE,Nk),  
     %       F_pip(DIST_SHAPE,Nk), F_w (DIST_SHAPE,Nk),
     %       F_qp (DIST_SHAPE,Nk), F_mu(DIST_SHAPE,Nk),
     %       F_mul(DIST_SHAPE,Nk)
*
      real  F_pipm(DIST_SHAPE,Nk), F_qpm(DIST_SHAPE,Nk)
*
*author
*     M.Tanguay
*
*revision
* v3_03 - Tanguay M.        - initial MPI version
* v3_11 - Tanguay M.        - AIXport+Opti+OpenMP for TLM-ADJ
*                           - Remove F_xfis 
* v3_30 - Tanguay M.        - Validation for LAM Nonhyd 
*
*object
*     see id section
*
*arguments
*
*implicits
#include "fft.cdk"
#include "glb_ld.cdk"
#include "ldnh.cdk"
#include "dcst.cdk"
#include "cstv.cdk"
#include "trp.cdk"
#include "hzd.cdk"
#include "opr.cdk"
#include "schm.cdk"
#include "geomg.cdk"
#include "lun.cdk"
#include "ptopo.cdk"
*
      integer i, j, k, dpwr
      real*8 wk1_8(LDIST_SHAPE,Nk)
      real   wk2(LDIST_SHAPE,Nk), wk2_m(LDIST_SHAPE,Nk)
*
      real*8 ZERO_8
      parameter (ZERO_8=0.0)
*     ______________________________________________________
*
      dpwr = Hzd_pwr / 2
      if (Lun_debug_L) write(Lun_out,1000) Hzd_pwr
*
*     Zero adjoint variables
*     ----------------------
!$omp parallel do
      do k=1,l_nk
      do j=l_miny,l_maxy
      do i=l_minx,l_maxx
      wk1_8(i,j,k) = ZERO_8
      wk2  (i,j,k) = ZERO_8
      enddo
      enddo
      enddo
!$omp end parallel do
*
      if ( Hzd_cdiff .gt. 0.0 ) then
*
      if (Hzd_uvwdt_L) goto 9988
*
*     TRAJECTORY
*     Save pi' for the diffusion of q'
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*
         if ( (.not.Schm_hydro_L) .and. Schm_difqp_L ) then
*
!$omp parallel
*
!$omp do 
            do k = 1, Nk
            do j = 1+pil_s,l_nj-pil_n
            do i = 1+pil_w,l_ni-pil_e
*
               wk2_m(i,j,k) = F_pipm(i,j,k)
*
            end do
            end do
            end do
!$omp end do 
*
*****************************
*  ADJ of                   *
*  3. Nonyhydrostatic model * 
*****************************
*
*        ----------------
*        START TRAJECTORY
*        ----------------
*
*        Indirect diffusion of q'
*        ~~~~~~~~~~~~~~~~~~~~~~~~
!$omp do 
         do k=1, Nk  !
         do j=1+pil_s,l_nj-pil_n ! p = (pi)exp(q')
         do i=1+pil_w,l_ni-pil_e ! ~~~~~~~~~~~~~~~
*
            wk2_m(i,j,k) = ( Geomg_z_8(k) + dble(wk2_m(i,j,k)) )
     %                       *exp( dble(F_qpm(i,j,k)) )
*
         end do
         end do
         end do
!$omp end do 
*
!$omp end parallel
*
         call hzd_solfft_lam(wk2_m, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
*        --------------
*        END TRAJECTORY
*        --------------
*
*        START ADJOINT CALCULATIONS
*        --------------------------
!$omp parallel do
         do k=1, Nk  !
         do j=1+pil_s,l_nj-pil_n ! q' = log(p/pi)
         do i=1+pil_w,l_ni-pil_e ! ~~~~~~~~~~~~~~
*
            wk2   (i,j,k) = ( dble(F_qp (i,j,k))*( Geomg_z_8(k) + dble(F_pipm(i,j,k))) )
     %                     /( dble(wk2_m(i,j,k))*( Geomg_z_8(k) + dble(F_pipm(i,j,k))) ) + wk2   (i,j,k)
            F_pip (i,j,k) = (-dble(wk2_m(i,j,k))*(                dble(F_qp  (i,j,k))) )
     %                     /( dble(wk2_m(i,j,k))*( Geomg_z_8(k) + dble(F_pipm(i,j,k))) ) + F_pip (i,j,k)
            F_qp  (i,j,k) = ZERO_8
*
         end do
         end do
         end do
!$omp end parallel do
*
         call hzd_solfft_lam_ad(wk2, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
*        Indirect diffusion of q'
*        ~~~~~~~~~~~~~~~~~~~~~~~~
!$omp parallel do
         do k=1, Nk  !
         do j=1+pil_s,l_nj-pil_n ! p = (pi)exp(q')
         do i=1+pil_w,l_ni-pil_e ! ~~~~~~~~~~~~~~~
*
            F_qp(i,j,k) = ( Geomg_z_8(k) + dble(wk2_m (i,j,k)) )
     %                       *exp( dble(F_qpm(i,j,k)) ) * dble(wk2(i,j,k)) + F_qp(i,j,k)
*
            wk2 (i,j,k) = (                dble(wk2   (i,j,k)) )
     %                       *exp( dble(F_qpm(i,j,k)) )
*
         end do
         end do
         end do
!$omp end parallel do
*
         call hzd_solfft_lam_ad(F_mul, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
         call hzd_solfft_lam_ad(F_mu, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
*        END ADJOINT CALCULATIONS
*        ------------------------
*
         endif
*
         if ( .not. Schm_hydro_L ) then
*
*     ADJ of
*     Vertical wind (physical)
*     ~~~~~~~~~~~~~~~~~~~~~~~~
*
         call hzd_solfft_lam_ad(F_w, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
         endif
*
*     ADJ of
*     The hydrostatic pressure: pi'
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*
         call hzd_solfft_lam_ad(F_pip, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
!$omp parallel
*
*     ADJ of
*     Save pi' for the diffusion of q'
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         if ( (.not.Schm_hydro_L) .and. Schm_difqp_L ) then
!$omp do 
            do k = 1, Nk
            do j = 1+pil_s,l_nj-pil_n
            do i = 1+pil_w,l_ni-pil_e
*
               F_pip(i,j,k) = wk2(i,j,k) + F_pip(i,j,k)
                wk2 (i,j,k) = ZERO_8
*
            end do
            end do
            end do
!$omp end do
         endif
*
*     ADJ of
*     The temperature: T', T'lin & hence T 
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
!$omp do
         do k=1, Nk
         do j=1+pil_s,l_nj-pil_n
         do i=1+pil_w,l_ni-pil_e
*
           F_tp(i,j,k) = F_t(i,j,k) + F_tp(i,j,k)
            F_t(i,j,k) = ZERO_8
*
         end do
         end do
         end do
!$omp end do
*
!$omp end parallel
*
         call hzd_solfft_lam_ad(F_plt, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
         call hzd_solfft_lam_ad(F_tp, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
!$omp parallel 
*
* ADJ of
* updating phi' at the top only relevant if none hydrostatic
*
         if (.not. Schm_hydro_L) then
!$omp do  
         do j = 1+pil_s,l_nj-pil_n
         do i = 1+pil_w,l_ni-pil_e
*
            F_it(i,j,1) = F_ip(i,j,1) + F_it(i,j,1)
            F_ip(i,j,1) = ZERO_8
*
         end do
         end do
!$omp enddo  
         endif
*
*     ADJ of
*     Mass related fields
*     ~~~~~~~~~~~~~~~~~~~
*
!$omp do  
         do k = 2, Nk
         do j = 1+pil_s,l_nj-pil_n
         do i = 1+pil_w,l_ni-pil_e
            F_ip(i,j,k) = F_it(i,j,k) + F_ip(i,j,k)
            F_it(i,j,k) = ZERO_8
         end do
         end do
         end do
!$omp enddo  
*
!$omp end parallel  
*
         call hzd_solfft_lam_ad(F_it, wk1_8, Fft_pri_8,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx,l_miny,l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
         call hzd_solfft_lam_ad(F_ip, wk1_8, Fft_pri_8,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx,l_miny,l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
 9988 continue
*
*     ADJ of
*     Vertical motion in pressure coord.
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*
         call hzd_solfft_lam_ad(F_td, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
         call hzd_solfft_lam_ad(F_psd, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
*     ADJ of
*     Momentum
*     ~~~~~~~~
*
         call hzd_solfft_lam_ad(F_v, wk1_8, Fft_pri_8 ,
     %                   Hzd_av_8,Hzd_cv_8,Hzd_deltav_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx,l_miny,l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Hzd_yp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
         call hzd_solfft_lam_ad(F_u, wk1_8, Fft_pri_8,
     %                   Hzd_au_8,Hzd_cu_8,Hzd_deltau_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx,l_miny,l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Hzd_xp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
      endif
*
 1000 format(/,
     $  3X,'ADJ of PERFORM DEL-',i1,' FFT HORIZONTAL DIFFUSION: (S/R HZD_HOF_LAM_AD)')
*     __________________________________________________________________
*
      return
      end
