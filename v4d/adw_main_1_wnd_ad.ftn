! ------------------------------------------------------------------------------------------------------------------------------
! EC-RPN COMM Group License/Disclaimer, version 2, Last Modified: Jan 08th, 2008 , Environment Canada
! 
! This is free software, you can use/redistribute/modify it under the terms of the EC-RPN COMM Group License/Disclaimer version 2 
! or (at your option) any later version that should be found at http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
! 
! This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the EC-RPN COMM Group License/Disclaimer for more details.
! 
! You should have received a copy of the EC-RPN COMM Group License/Disclaimer along with this software; if not, write to the
! EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), CANADA, H9P 1J3 ; or send e-mail to service.rpn@ec.gc.ca
! ------------------------------------------------------------------------------------------------------------------------------
***s/r adw_main_1_wnd_ad - ADJ of adw_main_1_wnd 
*
#include "model_macros_f.h"
*
      subroutine adw_main_1_wnd_ad ( F_u, F_v, F_w, F_nit, F_njt, F_nk )
*
      implicit none
*
      integer F_nit, F_njt, F_nk
*
      real F_u(F_nit,F_njt,F_nk),
     %     F_v(F_nit,F_njt,F_nk),
     %     F_w(F_nit,F_njt,F_nk+1)
*
*author
*     monique tanguay
*
*revision
* v2_31 - Tanguay M.        - initial MPI version
* v3_00 - Tanguay M.        - adapt to restructured adw_main 
* v3_11 - Tanguay M.        - AIXport+Opti+OpenMP for TLM-ADJ
* v3_21 - Tanguay M.        - Revision Openmp
* v3_30 - Tanguay M.        - Validation for LAM version 
* v4_04 - Tanguay M.        - Staggered version TL/AD 
*
*language
*     fortran 77
*
*object
*     see id section
*
*ADJ of
*arguments
*______________________________________________________________________
*              |                                                 |     |
* NAME         | DESCRIPTION                                     | I/O |
*--------------|-------------------------------------------------|-----|
* F_u          | u components of winds on advection grid         |  o  |
* F_v          | v components of winds on advection grid         |  o  |
* F_w          | w components of winds on advection grid         |  o  |
*              |                                                 |     |
* F_nit        | \ total number of points in x,y direction in    |  i  |
* F_njt        | / advection grid (including halos)              |  i  |
*              |                                                 |     |
* F_nk         | number of levels                                |  i  |
*______________|_________________________________________________|_____|
*
*implicits
#include "glb_ld.cdk"
#include "glb_pil.cdk"
#include "adw.cdk"
#include "lun.cdk"
#include "inuvl.cdk"
#include "vth.cdk"
*
*modules
      integer  vmmlod, vmmget, vmmuld
      external vmmlod, vmmget, vmmuld
************************************************************************
      integer pnerr, pnlkey1(30), pnlod
*
      integer i, j, k, i0, in, j0, jn, n, dest_ni
      real*8 ONE_8,sc_8,ZERO_8
      parameter(  ONE_8 = 1.0 )
      parameter( ZERO_8 = 0.0 )
*
      real, dimension(:,:,:)  ,allocatable :: in0_1,in0_2,in0_3
*
************************************************************************
      if (Lun_debug_L) write (Lun_out,1000)
*
      if (G_lam) allocate ( in0_1(LDIST_SHAPE,F_nk)   )
      if (G_lam) allocate ( in0_2(LDIST_SHAPE,F_nk)   )
      if (G_lam) allocate ( in0_3(LDIST_SHAPE,F_nk+1) )
*
      pnlkey1(1)  = VMM_KEY(uth)
      pnlkey1(2)  = VMM_KEY(vth)
      pnlkey1(3)  = VMM_KEY(zdth)
*
      pnerr = vmmlod(pnlkey1,3)
*
      pnerr = VMM_GET_VAR(uth)
      pnerr = VMM_GET_VAR(vth)
      pnerr = VMM_GET_VAR(zdth)
*
!$omp parallel private(i0,in,j0,jn,sc_8)
*
***********************************************************************
* ADJ of
* Adjust wind fields to advection grid
***********************************************************************
      if (.not.G_lam) then
*
         if ( l_north ) then
*
            call adw_pols_ad (F_w,Adw_wx_8,Adw_njc+1,Adw_nic,Adw_halox,
     %        Adw_njc,Adw_haloy,F_nk+1)
*
            call adw_polw_ad (F_u,F_v,Adw_cx_8,Adw_sx_8,Adw_wx_8,
     %        Adw_sy_8,Adw_njc+1,Adw_nic,Adw_halox,Adw_njc,Adw_haloy,F_nk)
*
         endif
*
         if ( l_south ) then
*
            call adw_pols_ad (F_w,Adw_wx_8,0,Adw_nic,Adw_halox,Adw_njc,
     %        Adw_haloy,F_nk+1)
*
            call adw_polw_ad (F_u,F_v,Adw_cx_8,Adw_sx_8,Adw_wx_8,
     %        Adw_sy_8,0,Adw_nic,Adw_halox,Adw_njc,Adw_haloy,F_nk)
*
         endif
*
      endif
*
      if (G_lam) then
          n=0
          dest_ni=l_ni
      else
          n=999
          dest_ni=G_ni
      endif
*
        if (G_lam) then
*
            in0_1 = uth
            in0_2 = vth
            in0_3 = zdth
*
        endif
*
!$omp single
      call rpn_comm_adj_halox ( zdth, LDIST_DIM, l_ni, l_nj, F_nk+1,
     %  Adw_halox, Adw_haloy, G_periodx, G_periody, F_w, 1-Adw_halox,
     %  Adw_nic+Adw_halox, 1-Adw_haloy, Adw_njc+Adw_haloy, dest_ni, n)
*
      call rpn_comm_adj_halox ( vth, LDIST_DIM, l_ni, l_nj, F_nk,
     %  Adw_halox, Adw_haloy, G_periodx, G_periody, F_v, 1-Adw_halox,
     %  Adw_nic+Adw_halox, 1-Adw_haloy, Adw_njc+Adw_haloy, dest_ni, n)
*
      call rpn_comm_adj_halox ( uth, LDIST_DIM, l_ni, l_nj, F_nk,
     %  Adw_halox, Adw_haloy, G_periodx, G_periody, F_u, 1-Adw_halox,
     %  Adw_nic+Adw_halox, 1-Adw_haloy, Adw_njc+Adw_haloy, dest_ni, n)
!$omp end single
*
         if (G_lam) then
!$omp do
             do k=1,F_nk+1
             do j=l_miny,l_maxy
             do i=l_minx,l_maxx
              zdth(i,j,k) = in0_3(i,j,k) +  zdth(i,j,k)
C            in0_3(i,j,k) = ZERO_8
             end do
             end do
             end do
!$omp end do
*
!$omp do
             do k=1,F_nk
             do j=l_miny,l_maxy
             do i=l_minx,l_maxx
               vth(i,j,k) = in0_2(i,j,k) +   vth(i,j,k)
               uth(i,j,k) = in0_1(i,j,k) +   uth(i,j,k)
C            in0_2(i,j,k) = ZERO_8
C            in0_1(i,j,k) = ZERO_8
             end do
             end do
             end do
!$omp end do
         endif
*
*     Zero adjoint variables
*     ----------------------
!$omp do
      do k = 1, F_nk+1
      do j = 1, F_njt
      do i = 1, F_nit
         F_w(i,j,k) = ZERO_8
      enddo
      enddo
      enddo
!$omp enddo
*
!$omp do
      do k = 1, F_nk
      do j = 1, F_njt
      do i = 1, F_nit
         F_v(i,j,k) = ZERO_8
         F_u(i,j,k) = ZERO_8
      enddo
      enddo
      enddo
!$omp enddo
*
***********************************************************************
* ADJ of
* Image to component
***********************************************************************
      i0=1
      in=l_ni
      j0=1
      jn=l_nj
      if (G_lam) then
             if (l_west) i0 = 3
             if (l_east) in = l_niu - 1
      endif
*
!$omp do
      do k = 1,F_nk
      do j = j0,jn
*
      sc_8 = ONE_8/Adw_cy_8(j)
*
      do i = i0,in
*
      F_u(i,j,k) = sc_8 * uth(i,j,k) + F_u(i,j,k)
      uth(i,j,k) = ZERO_8
*
      enddo
      enddo
      enddo
!$omp enddo
*
      i0=1
      in=l_ni
      j0=1
      jn=l_nj
      if (G_lam) then
             if (l_south) j0 = 3
             if (l_north) jn = l_njv - 1
      endif
*
!$omp do
      do k = 1,F_nk
      do j = j0,jn
*
      sc_8 = ONE_8/Adw_cy_8(j)
*
      do i = i0,in
*
      F_v(i,j,k) = sc_8 * vth(i,j,k) + F_v(i,j,k)
      vth(i,j,k) = ZERO_8
*
      enddo
      enddo
      enddo
!$omp enddo
*
***********************************************************************
* ADJ of
* Interpolate advection winds to geopotential grid
* F_u and F_v are used as work space
***********************************************************************
!$omp do
      do k=1,F_nk
*
         i0 = 1
         in = l_ni
         j0 = 1
         jn = l_njv
         if (l_south) j0 = 3
         if (l_north) jn = l_njv - 1
*
         if (.not.G_lam) then
*
         if (l_north) then
            do i = i0, in
         vth(i,jn+1,k) = inuvl_wyvy3_8(jn+1,3) * F_v(i,jn+1,k) + vth(i,jn+1,k)
         vth(i,jn  ,k) = inuvl_wyvy3_8(jn+1,2) * F_v(i,jn+1,k) + vth(i,jn  ,k)
         vth(i,jn-1,k) = inuvl_wyvy3_8(jn+1,1) * F_v(i,jn+1,k) + vth(i,jn-1,k)
         F_v(i,jn+1,k) = ZERO_8
*
         vth (i,jn+1,k)= inuvl_wyvy3_8(jn+2,2) * F_v(i,jn+2,k) + vth(i,jn+1,k)
         vth (i,jn  ,k)= inuvl_wyvy3_8(jn+2,1) * F_v(i,jn+2,k) + vth(i,jn  ,k)
         F_v(i,jn+2,k) = ZERO_8
            end do
         endif
         if (l_south) then
            do i = i0, in
         vth(i,j0,  k) = inuvl_wyvy3_8(j0-1,4) * F_v(i,j0-1,k) + vth(i,j0,  k)
         vth(i,j0-1,k) = inuvl_wyvy3_8(j0-1,3) * F_v(i,j0-1,k) + vth(i,j0-1,k)
         vth(i,j0-2,k) = inuvl_wyvy3_8(j0-1,2) * F_v(i,j0-1,k) + vth(i,j0-2,k)
         F_v(i,j0-1,k) = ZERO_8
*
         vth(i,j0-1,k) = inuvl_wyvy3_8(j0-2,4) * F_v(i,j0-2,k) + vth(i,j0-1,k)
         vth(i,j0-2,k) = inuvl_wyvy3_8(j0-2,3) * F_v(i,j0-2,k) + vth(i,j0-2,k)
         F_v(i,j0-2,k) = ZERO_8
            end do
         endif
*
         endif
*
         do j = jn, j0, -1
         do i = in, i0, -1
         vth(i,j+1,k) = inuvl_wyvy3_8(j,4) * F_v(i,j,k) + vth(i,j+1,k)
         vth(i,j  ,k) = inuvl_wyvy3_8(j,3) * F_v(i,j,k) + vth(i,j  ,k)
         vth(i,j-1,k) = inuvl_wyvy3_8(j,2) * F_v(i,j,k) + vth(i,j-1,k)
         vth(i,j-2,k) = inuvl_wyvy3_8(j,1) * F_v(i,j,k) + vth(i,j-2,k)
         F_v(i,j,  k) = ZERO_8
         end do
         end do
*
         i0 = 1
         in = l_niu
         j0 = 1
         jn = l_nj
         if (G_lam) then
             if (l_west) i0 = 3
             if (l_east) in = l_niu - 1
         endif
*
         do j = jn,j0,-1
         do i = in,i0,-1
         uth(i+1,j,k) = inuvl_wxux3_8(i,4) * F_u(i,j,k) + uth(i+1,j,k)
         uth(i  ,j,k) = inuvl_wxux3_8(i,3) * F_u(i,j,k) + uth(i  ,j,k)
         uth(i-1,j,k) = inuvl_wxux3_8(i,2) * F_u(i,j,k) + uth(i-1,j,k)
         uth(i-2,j,k) = inuvl_wxux3_8(i,1) * F_u(i,j,k) + uth(i-2,j,k)
         F_u(i,  j,k) = ZERO_8
         end do
         end do
*
      end do
!$omp enddo
*
!$omp single 
      call rpn_comm_adj_halo (vth,LDIST_DIM,l_ni,l_njv,F_nk,
     %             G_halox,G_haloy,G_periodx,G_periody,l_ni,0)
!$omp end single 
*   
*     Zero vth halo
*     -------------
!$omp do
      do k=1,F_nk
         do j=l_miny,0
         do i=l_minx,l_maxx
            vth(i,j,k) = ZERO_8
         end do
         end do
         do j=l_njv+1,l_maxy
         do i=l_minx ,l_maxx
            vth(i,j,k) = ZERO_8
         end do
         end do
         do i=l_minx,0
         do j=l_miny,l_maxy
            vth(i,j,k) = ZERO_8
         end do
         end do
         do i=l_ni+1,l_maxx
         do j=l_miny,l_maxy
            vth(i,j,k) = ZERO_8
         end do
         end do
      end do
!$omp enddo
*
!$omp single 
      call rpn_comm_adj_halo (uth,LDIST_DIM,l_niu,l_nj, F_nk,
     %             G_halox,G_haloy,G_periodx,G_periody,l_ni,0)
!$omp end single 
*
*     Zero uth halo
*     -------------
!$omp do
      do k=1,F_nk
         do j=l_miny,0
         do i=l_minx,l_maxx
            uth(i,j,k) = ZERO_8
         end do
         end do
         do j=l_nj+1,l_maxy
         do i=l_minx,l_maxx
            uth(i,j,k) = ZERO_8
         end do
         end do
         do i=l_minx,0
         do j=l_miny,l_maxy
            uth(i,j,k) = ZERO_8
         end do
         end do
         do i=l_niu+1,l_maxx
         do j=l_miny ,l_maxy
            uth(i,j,k) = ZERO_8
         end do
         end do
      end do
!$omp enddo
*
!$omp end parallel
*
      pnerr = vmmuld(pnlkey1,3)
*
      if (G_lam) deallocate ( in0_1  )
      if (G_lam) deallocate ( in0_2  )
      if (G_lam) deallocate ( in0_3  )
*
 1000  format(3X,'ADJ of PREPARE WINDS: (S/R ADW_MAIN_1_WND_AD)')
      return
      end
