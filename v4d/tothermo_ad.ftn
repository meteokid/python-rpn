!copyright (C) 2001  MSC-RPN COMM  %%%RPNPHY%%%
***S/P TOHERMO
*
      SUBROUTINE TOTHERMO_AD (FMOM,FTHE,AT2T,AT2M,NI,NJ,NK,MO2TH)
*
#include "impnone.cdk"
      INTEGER NI,NJ,NK
      LOGICAL MO2TH
      REAL FMOM(NI,NK), FTHE(NI,NK)
      REAL AT2T(NI,NJ), AT2M(NI,NJ)
*
*Author
*          L. Spacek (Oct 2008)
*
*Revision
*
*Object
*          vertical interpolation of T/Q to momentum or
*          ttermo levels (adjoint to tothermo.ftn)
*
*Arguments
*
*          - Input/Output -
* FMOM     variable at sigma momentum levels
* FTHE     variable at sigma thermo levels
*
*          - Input -
* AT2T     coefficients for interpolation of T,Q to thermo levels
* AT2M     coefficients for interpolation of T,Q to momentum levels
* NI       horizontal dimension
* NJ       vertical dimension
* NK       loop length
* MO2TH    .true.  mommentum -> thermo
*          .false. thermo    -> momentum 
*
**
      INTEGER K, I, KP1, KM1
*
*
      IF(MO2TH)THEN
         DO K=NK,1,-1
            KP1=MIN(NK,K+1)
            DO I=1,NI
               FMOM(I,K)  = FTHE(I,K)*(1.-AT2T(I,K))+FMOM(I,K)
               FMOM(I,KP1)= FTHE(I,K)*AT2T(I,K)+FMOM(I,KP1)
               FTHE(I,K)  = 0.
            ENDDO
         ENDDO
       ELSE
         DO K=1,NK
            KM1=MAX(1,K-1)
            DO I=1,NI
               FTHE(I,K)   = FMOM(I,K)*(1-AT2M(I,K))+FTHE(I,K)
               FTHE(I,KM1) = FMOM(I,K)*AT2M(I,K)+FTHE(I,KM1)
               FMOM(I,K)   = 0.
            ENDDO
         ENDDO
      ENDIF
*
      RETURN
      END
