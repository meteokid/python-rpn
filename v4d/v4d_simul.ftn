! ------------------------------------------------------------------------------------------------------------------------------
! EC-RPN COMM Group License/Disclaimer, version 2, Last Modified: Jan 08th, 2008 , Environment Canada
! 
! This is free software, you can use/redistribute/modify it under the terms of the EC-RPN COMM Group License/Disclaimer version 2 
! or (at your option) any later version that should be found at http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
! 
! This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the EC-RPN COMM Group License/Disclaimer for more details.
! 
! You should have received a copy of the EC-RPN COMM Group License/Disclaimer along with this software; if not, write to the
! EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), CANADA, H9P 1J3 ; or send e-mail to service.rpn@ec.gc.ca
! ------------------------------------------------------------------------------------------------------------------------------
***s/r  v4d_simul - Controls 4-D variational job
*
#include "model_macros_f.h"
*
      subroutine v4d_simul (F_indic,Ndim,F_px,F_pj,F_pgraj)
*
      implicit none
*
      integer F_indic,Ndim
      real F_pj,F_px(Ndim),F_pgraj(Ndim)
*
*author
*     M.Tanguay
*
*revision
* v2_10 - Tanguay M.        - initial MPI version
* v2_21 - Desgagne M.       - rpn_comm stooge for MPI
* v2_31 - Tanguay M.        - adapt for tracers in tr3d
* v3_00 - Laroche S.        - adapt for simplified physics
* v3_01 - Morneau J.        - run NLM trajectory when V4dg_sensib_L
*                             and v4dg_status=5
* v3_02 - M.Tanguay         - integration stops at Lctl_steplast
* v3_03 - M.Tanguay         - introduce V4dg_imguv_L  
*                           - replace v4d_procdyn by indata  
* v3_11   M Tanguay         - Extend TRAJ for conversion for DYNOUT2
*                           - ADJ of digital filter
* v3_20   M Tanguay         - Replace v4d_dynout by out_dyn_ad 
* v3_30 - Tanguay M.        - adapt TL/AD to out_dyn 
* v4_04 - Tanguay M.        - Staggered version TL/AD 
*
*object
*     Simulator 4D (in minimization language).
*     Controls running NLM or TLM and the adjoint.
*	
*arguments
*  Name        I/O                 Description
*----------------------------------------------------------------
* F_indic      I                   If=4 :set F_pj and F_pgraj   
*                                  If=98:set F_pgraj only    
*                                  If=99:set F_pj only    
* Ndim         I                   Dimension of F_px  
* F_px         I                   Control variable at initial time    
* F_pj         O                   Cost function value      
* F_pgraj      O                   Gradient at initial time  
*----------------------------------------------------------------
*
*implicits
#include "glb_ld.cdk"
#include "lun.cdk"
#include "v4dg.cdk"
#include "tr3d.cdk"
#include "v4dj.cdk"
#include "v4dm.cdk"
#include "lctl.cdk"
#include "ptopo.cdk"
#include "step.cdk"
*
*modules
      integer  vmmlod,vmmget,vmmuld
      external vmmlod,vmmget,vmmuld
*
      integer  ierr,k
      real plocal
*
*     ----------------
*     ADJOINT run only
*     ----------------
      if (F_indic.eq.98) goto 1000
*
*     ---------------
*     Initializations 
*     ---------------
*
*        Update V4dm_nsim 
*        ----------------
         if (V4dg_conf/100.eq.1.and.V4dg_status.eq.999) 
     %       V4dm_nsim = V4dm_nsim + 1
*
*        Initialize cost function
*        ------------------------
         if (V4dg_conf/100.eq.1.and.V4dg_status.ne.5) V4dj_pj = 0 
*
*        Set initial and final time step
*        -------------------------------
         Lctl_step = 0
         V4dg_steplast = Step_total
*
*        Initialize all variables to zero 
*        --------------------------------
         call v4d_zero()
*
*        Initialize model var. from control var. F_px
*        --------------------------------------------
         call v4d_cain (Ndim,F_px)
*
*     ----------------------
*     Run DIRECT integration
*     ----------------------
*
      if( .not.V4dg_tlm_L .or. (V4dg_sensib_L.and.V4dg_status.eq.5)) then
*        ---------------
*        NLM integration
*        ---------------
*
*        Set non-linear direct run 
*        -------------------------
         V4dg_ds_L = .true.
         V4dg_nl_L = .true.
         V4dg_di_L =      V4dg_ds_L.and.     V4dg_nl_L
         V4dg_tl_L =      V4dg_ds_L.and..not.V4dg_nl_L
         V4dg_ad_L = .not.V4dg_ds_L.and..not.V4dg_nl_L
*
*        Set WRITE option on TRAJ WA file
*        --------------------------------
         V4dg_rwtr = 1  
*
*        Complete preprocessing
*        ----------------------
         V4dg_part = 3
         call indata()
*
         if (V4dg_output_L) call v4d_out_dyn (.true.,-1)
*
*        Run NLM model
*        -------------
         call gem_ctrl()
*
      else
*        ---------------
*        TLM integration
*        ---------------
*
*        Set linear direct run
*        ---------------------
         V4dg_ds_L = .true.
         V4dg_nl_L = .false.
         V4dg_di_L =      V4dg_ds_L.and.     V4dg_nl_L
         V4dg_tl_L =      V4dg_ds_L.and..not.V4dg_nl_L
         V4dg_ad_L = .not.V4dg_ds_L.and..not.V4dg_nl_L
*
*        Set READ option on TRAJ WA file
*        -------------------------------
         V4dg_rwtr = 0
*
*        Complete preprocessing
*        ----------------------
         V4dg_part = 3
         call indata_tl()
*
         if (V4dg_output_L) call v4d_out_dyn (.true.,-1)
*
*        Run TLM model
*        -------------
         call gem_ctrl_tl()
*
      endif
*
*     ------------------------------
*     Synthesis of the cost function
*     ------------------------------
      if( V4dg_conf/100.eq.1.and.V4dg_status.ne.5 ) then
*
         F_pj   = V4dj_pj
         plocal = F_pj
         F_pj   = 0.0
         call rpn_comm_Allreduce (plocal,F_pj,1,"MPI_REAL","MPI_SUM",
     %                                                   "grid",ierr)
*
      endif
*
      if(F_indic.eq.99) return
*
      if(F_indic.ne. 4) call gem_stop('v4d_simul',-1) 
*
*     ----------------------------------------------------------------
*     Evaluate gradient of cost function with respect to initial state
*     ----------------------------------------------------------------
*
*        Set gradients (F_pgraj) to zero
*        -------------------------------
         do k = 1,Ndim
            F_pgraj(k) = 0.
         end do
*
 1000 continue
*
*        Set initial and final time step
*        -------------------------------
         Lctl_step = Step_total 
         V4dg_steplast = 0 
*
*        Set adjoint variables to zero
*        -----------------------------
         call v4d_zero()
*
*        Initialize ADJ variables from control F_pgradj 
*        ----------------------------------------------
         call v4d_cainin_ad (Ndim,F_pgraj)
*
*        Set ADJ integration 
*        -------------------
         V4dg_ds_L = .false.
         V4dg_nl_L = .false.
         V4dg_di_L =      V4dg_ds_L.and.     V4dg_nl_L
         V4dg_tl_L =      V4dg_ds_L.and..not.V4dg_nl_L
         V4dg_ad_L = .not.V4dg_ds_L.and..not.V4dg_nl_L
*
*        Set READ option on TRAJ WA file
*        -------------------------------
         V4dg_rwtr = 0
*
*        Run ADJ model
*        -------------
         call gem_ctrl_ad()
*
*        ADJOINT of
*        Complete preprocessing
*        ----------------------
         V4dg_part = 3
         call indata_ad()
*
*        Output adjoint fields knowing that u,v are true winds
*        -----------------------------------------------------         
         if (V4dg_output_L) then
*
             V4dg_imguv_L = .false.
*
             if (V4dg_output_L) call v4d_blocstat ()
             if (V4dg_output_L) call v4d_out_dyn (.true.,-1)
*
             V4dg_imguv_L = .true.
*
         endif
*
*        Initialise control F_pgraj from ADJ variables
*        ---------------------------------------------
         call v4d_cain_ad (Ndim,F_pgraj)
*
      return
      end
