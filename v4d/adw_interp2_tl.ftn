! ------------------------------------------------------------------------------------------------------------------------------
! EC-RPN COMM Group License/Disclaimer, version 2, Last Modified: Jan 08th, 2008 , Environment Canada
! 
! This is free software, you can use/redistribute/modify it under the terms of the EC-RPN COMM Group License/Disclaimer version 2 
! or (at your option) any later version that should be found at http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
! 
! This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the EC-RPN COMM Group License/Disclaimer for more details.
! 
! You should have received a copy of the EC-RPN COMM Group License/Disclaimer along with this software; if not, write to the
! EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), CANADA, H9P 1J3 ; or send e-mail to service.rpn@ec.gc.ca
! ------------------------------------------------------------------------------------------------------------------------------
***s/r adw_interp2_tl - TLM of adw_interp2 when Adw_lag3d_L=.TRUE. 
*
#include "model_macros_f.h"
*
      subroutine adw_interp2_tl( F_out,  F_in,  F_u,  F_v,
     %                           F_out_m,F_in_m,F_u_m,F_v_m,
     %                           F_wind_L, F_mono_L, DIST_DIM, F_nk,
     %                           i0,in,j0,jn, F_lev_S )
*
      implicit none
*
      character*1 F_lev_S
*
      logical F_wind_L, F_mono_L
*
      integer DIST_DIM, F_nk, i0,in,j0,jn
*
      real F_out (DIST_SHAPE, F_nk),
     %     F_in  (DIST_SHAPE, F_nk)
      real F_u(*), F_v(*)
*
      real F_out_m(DIST_SHAPE, F_nk),
     %     F_in_m (DIST_SHAPE, F_nk)
      real F_u_m(*), F_v_m(*)
*
*author
*     monique tanguay
*
*revision
* v2_31 - Tanguay M.        - initial MPI version
* v3_00 - Tanguay M.        - adapt to restructured Adw_main 
* v3_11 - Tanguay M.        - AIXport+Opti+OpenMP for TLM-ADJ
* v3_20 - Tanguay M.        - Lagrange 3D
* v3_21 - Tanguay M.        - Revision Openmp
* v3_30 - Tanguay M.        - adjust OPENMP for LAM 
* v4_04 - Tanguay M.        - Staggered version TL/AD 
*
*language
*     fortran 77
*
*object
*     see id section
*
*arguments
*______________________________________________________________________
*              |                                                 |     |
* NAME         | DESCRIPTION                                     | I/O |
*--------------|-------------------------------------------------|-----|
*              |                                                 |     |
* F_out        | interpolated field                              |  o  |
* F_in         | field to interpolate                            |  i  |
* F_wind_L     | switch: .true. : field to interpolate is a wind |  i  |
*              |                  like quantity                  |  i  |
* F_mono_L     | switch: .true. : monotonic interpolation        |  i  |
*______________|_________________________________________________|_____|
*
*implicits
#include "glb_ld.cdk"
#include "adw.cdk"
#include "adw_m.cdk"
*
************************************************************************
      integer i, j, k, nij, nijk, nijkag, n, dest_ni
*
      real dummy
*
      nij    = l_ni   *l_nj
      nijk   = l_ni   *l_nj   *F_nk
      nijkag = Adw_nit*Adw_njt*F_nk
*
************************************************************************
*
* Adjust field to advection grid
*
* Compute extension beyond the pole if appropriate
*
************************************************************************
!$omp single
      if (G_lam) then
          n=0
          dest_ni=l_ni
      else
          n=999
          dest_ni=G_ni
      endif
*
*     TRAJECTORY
*     ----------
      call rpn_comm_xch_halox (F_in_m, LDIST_DIM, l_ni, l_nj, F_nk,
     % Adw_halox, Adw_haloy, G_periodx, G_periody, F_u_m, -Adw_halox+1,
     % Adw_nic+Adw_halox, -Adw_haloy+1, Adw_njc+Adw_haloy, dest_ni, n)
*
*     TLM
*     ---
      call rpn_comm_xch_halox (F_in, LDIST_DIM, l_ni, l_nj, F_nk,
     % Adw_halox, Adw_haloy, G_periodx, G_periody, F_u, -Adw_halox+1,
     % Adw_nic+Adw_halox, -Adw_haloy+1, Adw_njc+Adw_haloy, dest_ni, n)
!$omp end single
*
      if (.not.G_lam) then
*
*     TRAJECTORY
*     ----------
      if ( l_south ) then
*
         if ( F_wind_L ) then
            call adw_pol0 (F_u_m, 0, Adw_nic,Adw_halox,Adw_njc,
     %                     Adw_haloy,F_nk)
         else
            call adw_pols (F_u_m,Adw_wx_8, 0, Adw_nic,Adw_halox,
     %                     Adw_njc,Adw_haloy,F_nk)
         endif
            call adw_polx (F_u_m,Adw_xg_8,.true.,Adw_nic,Adw_halox,
     %                     Adw_njc,Adw_haloy,F_nk)
      endif
*
      if ( l_north ) then
*
         if ( F_wind_L ) then
            call adw_pol0 (F_u_m,Adw_njc+1,Adw_nic,Adw_halox,Adw_njc,
     %                     Adw_haloy,F_nk)
         else
            call adw_pols (F_u_m,Adw_wx_8, Adw_njc+1,Adw_nic,Adw_halox,
     %                     Adw_njc,Adw_haloy,F_nk)
         endif
            call adw_polx (F_u_m,Adw_xg_8,.false.,Adw_nic,Adw_halox,
     %                     Adw_njc,Adw_haloy,F_nk)
      endif
*
*     TLM
*     ---
      if ( l_south ) then
*
         if ( F_wind_L ) then
            call adw_pol0 (F_u, 0, Adw_nic,Adw_halox,Adw_njc,
     %                     Adw_haloy,F_nk)
         else
            call adw_pols (F_u,Adw_wx_8, 0, Adw_nic,Adw_halox,
     %                     Adw_njc,Adw_haloy,F_nk)
         endif
            call adw_polx (F_u,Adw_xg_8,.true.,Adw_nic,Adw_halox,
     %                     Adw_njc,Adw_haloy,F_nk)
      endif
*
      if ( l_north ) then
*
         if ( F_wind_L ) then
            call adw_pol0 (F_u,Adw_njc+1,Adw_nic,Adw_halox,Adw_njc,
     %                     Adw_haloy,F_nk)
         else
            call adw_pols (F_u,Adw_wx_8, Adw_njc+1,Adw_nic,Adw_halox,
     %                     Adw_njc,Adw_haloy,F_nk)
         endif
            call adw_polx (F_u,Adw_xg_8,.false.,Adw_nic,Adw_halox,
     %                     Adw_njc,Adw_haloy,F_nk)
      endif
*
      endif
************************************************************************
*
* Compute second derivative for cubic spline in the vertical
*
************************************************************************
*
************************************************************************
*
* Interpolate
*
************************************************************************
*
      call adw_tricub_lag3d_tl(Adw_wrkc,   F_u,
     %                         Adw_capx1,  Adw_capy1,  Adw_capz1,
     %                         Adw_wrkc_m, F_u_m,
     %                         Adw_capx1_m,Adw_capy1_m,Adw_capz1_m,
     %                         nijk, F_mono_L,i0,in,j0,jn,F_nk,F_lev_S)
*
!$omp do
      do k = 1, F_nk
      do j = j0,jn
      do i = i0,in
*
*     TRAJECTORY
*     ----------
      F_out_m(i,j,k) = Adw_wrkc_m ( (k-1)*nij+(j-1)*l_ni+i )
*
*     TLM
*     ---
      F_out(i,j,k) = Adw_wrkc ( (k-1)*nij+(j-1)*l_ni+i )
*
      enddo
      enddo
      enddo
!$omp enddo
*
      return
      end
