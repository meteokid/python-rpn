! ------------------------------------------------------------------------------------------------------------------------------
! EC-RPN COMM Group License/Disclaimer, version 2, Last Modified: Jan 08th, 2008 , Environment Canada
! 
! This is free software, you can use/redistribute/modify it under the terms of the EC-RPN COMM Group License/Disclaimer version 2 
! or (at your option) any later version that should be found at http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
! 
! This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the EC-RPN COMM Group License/Disclaimer for more details.
! 
! You should have received a copy of the EC-RPN COMM Group License/Disclaimer along with this software; if not, write to the
! EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), CANADA, H9P 1J3 ; or send e-mail to service.rpn@ec.gc.ca
! ------------------------------------------------------------------------------------------------------------------------------
***s/r itf_phy_apply_tl - TLM of apply consistency with related dynamical variables
*
#include "model_macros_f.h"
*
      subroutine itf_phy_apply_tl ( F_tdu,F_tdv,F_tdt,F_trp,F_trm,
     $                        F_tp,DIST_DIM, nkphy,F_apply_L)
*
      implicit none
*
      logical,intent(IN) :: F_apply_L
      integer,intent(IN) :: DIST_DIM,nkphy
      real,dimension(DIST_SHAPE,nkphy),intent(INOUT) :: F_tdu, F_tdv, F_tdt, F_tp
      real,dimension(DIST_SHAPE,nkphy,*),intent(INOUT) :: F_trp, F_trm
*
*author 
*     Stephane Laroche        Janvier 2001
*
*revision
* v3_00 - Laroche S.        - initial MPI version
* v3_20 - Laroche S.        - slight modification to rpn_comm_xch_halo call
* v3_21 - Tanguay M.        - Revision Openmp 
* v3_30 - Tanguay M.        - adapt TL/AD to itf/new tendencies
*                           - Validation for LAM version
* v4_03 - Spacek L.         - Staggered version
*
*object
*       TLM of apply consistency of the tendencies on physics variables
*       with related dynamical variables. Interpolate wind
*       tendancies toward theirs respective grids.
*
*arguments
*  Name        I/O                 Description
*----------------------------------------------------------------
* F_tcond       I    -
*	
*implicits
#include "glb_ld.cdk"
#include "cstv.cdk"
#include "lun.cdk"
#include "hblen.cdk"
#include "dcst.cdk"
#include "schm.cdk"
#include "geomg.cdk"
#include "lam.cdk"
#include "intuv.cdk"
#include "inuvl.cdk"
#include "vt0.cdk"
#include "vt1.cdk"
#include "vtx.cdk"
#include "vtx_m.cdk"
#include "vt0_m.cdk"
#include "p_geof.cdk"
#include "itf_phy_busind.cdk"
#include "v4dg.cdk"
#include "lctl.cdk"
*
*modules
      integer  vmmlod,vmmget,vmmuld
      external vmmlod,vmmget,vmmuld
*
      integer*8 pnt_trp(phyt_ntr),pnt_trm(phyt_ntr)
      integer i, j, k, n, err, key1(11),
     $     keyp(phyt_ntr), keym(phyt_ntr), keyp_, keym_
      integer i0,in,j0, pnload
      real,dimension(LDIST_SHAPE,nkphy) :: wk1, wk2
      real,dimension(LDIST_SHAPE,nkphy) :: F_trpm, F_tpm, F_trmm, F_tdtm
      real trp, trm
      pointer (patrp, trp(LDIST_SHAPE,*)),(patrm, trm(LDIST_SHAPE,*))
*
*notes
*	Consistency is applied according to diagnostic relationships
*	used at initial time in predat1 and predat2. If changes are
*	made to the relations used in these two modules, they should
*	be made accordingly here. (xrp**t1 are used as workfields)
**
*     __________________________________________________________________
*
      if(      Schm_wload_L              ) call gem_stop('P_APPLY_TL 1',-1)
      if(.not. Schm_pcsty_L              ) call gem_stop('P_APPLY_TL 2',-1)
      if(.not. Schm_pheat_L              ) call gem_stop('P_APPLY_TL 3',-1)
*
      keyp_ = VMM_KEY (trt1)
      if (phyt_ntr.gt.0) then
         do n=1,phyt_ntr
            keyp(n) = keyp_ + n
         end do
         err = vmmlod(keyp,phyt_ntr)
         do n=1,phyt_ntr
            err = vmmget(keyp(n),patrp,trp)
            pnt_trp(n) = patrp
         end do
      endif
*
      key1(1) = VMM_KEY(st1  )
      key1(2) = VMM_KEY(ut1)
      key1(3) = VMM_KEY(vt1)
      key1(4) = VMM_KEY(tt1)
      key1(5) = VMM_KEY(topo)
      key1(6) = VMM_KEY(fiptx)
      key1(7) = VMM_KEY(st0)
      key1(8) = VMM_KEY(tt0)
      key1(9) = VMM_KEY(fiptx_m)
      key1(10)= VMM_KEY(st0_m)
      key1(11)= VMM_KEY(tt0_m)
      err = vmmlod (key1,11)
      err = VMM_GET_VAR(st1  )
      err = VMM_GET_VAR(ut1)
      err = VMM_GET_VAR(vt1)
      err = VMM_GET_VAR(tt1)
      err = VMM_GET_VAR(topo)
      err = VMM_GET_VAR(fiptx)
      err = VMM_GET_VAR(st0)
      err = VMM_GET_VAR(tt0)
      err = VMM_GET_VAR(fiptx_m)
      err = VMM_GET_VAR(st0_m)
      err = VMM_GET_VAR(tt0_m)
*

*
*     Read TRAJ for the simplified physics
*     -------------------------------------
      call v4d_rwtraj(20,F_tdtm,F_trmm)
      call v4d_rwtraj(24,F_tpm,F_trpm)
*
*  1. Interpolation of the wind associated tendencies
*  ---------------------------------------------------
*
      call itf_phy_uvgridscal ( F_tdu, F_tdv, LDIST_DIM, nkphy, .false. )
*
*
*   2.  Compute VIRTUAL temperature tendency from:  temperature tendency,
*        specific humidity, virtual temperature & specific humidity tendency
*   ------------------------------------------------------------------------
*

      patrp = pnt_trp(hucond)
*
!$omp parallel

      wk1 = 0.
      wk2 = 0.

!$omp do
      do k=1,nkphy
      do j= 1+pil_s, l_nj-pil_n 
      do i= 1+pil_w, l_ni-pil_e 
*
*       TLM
*       ---
        F_tdt(i,j,k) =  F_tdt(i,j,k)*( 1. + Dcst_delta_8*F_trpm(i,j,k) )
     $                +          trp(i,j,k)*Dcst_delta_8*F_tdtm(i,j,k)
     $                +         F_tp(i,j,k)*Dcst_delta_8*F_trmm(i,j,k)
     $                + F_trm(i,j,k,hucond)*Dcst_delta_8*F_tpm(i,j,k)
*
        wk1(i,j,k) = F_tdt(i,j,k)
*
      end do
      end do
      end do
!$omp enddo

*
*     ---------------------------------------------------

!$omp single
      if ( Lam_0ptend_L .and. G_lam .and.
     $   ((Hblen_x.gt.0).or.(Hblen_y.gt.0)) ) then
         call nesajr (wk1  , wk2, LDIST_DIM,nkphy,0,0,Hblen_x,Hblen_y)
         call nesajr (F_tdu, wk2, LDIST_DIM,nkphy,1,0,Hblen_x,Hblen_y)
         call nesajr (F_tdv, wk2, LDIST_DIM,nkphy,0,1,Hblen_x,Hblen_y)
      endif
!$omp end single

*
*     3. de-stagger wind tendencies in the vertical
*     ------------------------------------------
*
!$omp do
      do j= 1+pil_s, l_nj-pil_n
         do k=1,l_nk+1
         do i= 1+pil_w, l_niu-pil_e
         F_tdu(i,j,k) =F_tdu(i,j,k+1)
         end do
         end do
      end do
!$omp enddo
!$omp do
      do j= 1+pil_s, l_njv-pil_n
         do k=1,l_nk+1
         do i= 1+pil_w, l_ni-pil_e
         F_tdv(i,j,k) =F_tdv(i,j,k+1)
         end do
         end do
      end do
!$omp enddo
*
*C       4.     apply tendencies to primary variables
*               -------------------------------------
*
*
!$omp do
      do k=1,l_nk+2
         do j= 1+pil_s, l_nj-pil_n 
         do i= 1+pil_w, l_ni-pil_e 
            tt1 (i,j,k) =  tt1 (i,j,k) + Cstv_dt_8*wk1   (i,j,k)
         end do
         end do
      enddo
!$omp enddo
*
!$omp do
      do k=1,l_nk+1
         do j= 1+pil_s, l_nj-pil_n 
         do i= 1+pil_w, l_niu-pil_e 
            ut1 (i,j,k) =  ut1 (i,j,k) + Cstv_dt_8*F_tdu (i,j,k)
         end do
         end do
         do j= 1+pil_s, l_njv-pil_n 
         do i= 1+pil_w, l_ni-pil_e 
            vt1 (i,j,k) =  vt1 (i,j,k) + Cstv_dt_8*F_tdv (i,j,k)
         end do
         end do
      end do
!$omp enddo
!$omp end parallel
*
      call diag_fip_tl ( fiptx,  st0,  tt0,  
     $                   fiptx_m,st0_m,tt0_m,topo,
     $                   1+pil_w,l_ni-pil_e,1+pil_s,l_nj-pil_n,l_nk)
*
      do n=1,phyt_ntr
         if (phyt_ind(3,n).gt.0) then
            patrp = pnt_trp(n)
            if ( Lam_0ptend_L .and. G_lam .and.
     $         ((Hblen_x.gt.0).or.(Hblen_y.gt.0)) ) then
               call nesajr (F_trm(minx,miny,1,n), wk2, LDIST_DIM,nkphy,
     $                                          0,0,Hblen_x,Hblen_y)
               call nesajr (F_trp(minx,miny,1,n), wk2, LDIST_DIM,nkphy,
     $                                          0,0,Hblen_x,Hblen_y)
            endif
*
!$omp parallel
!$omp do
            do k=1,l_nk+2
            do j= 1+pil_s, l_nj-pil_n 
            do i= 1+pil_w, l_ni-pil_e
               trp(i,j,k) = trp(i,j,k) + Cstv_dt_8*F_trm(i,j,k,n)
            end do
            end do
            end do
!$omp end do
            if (n.ne.hucond) then
               do j= 1+pil_s, l_nj-pil_n
               do i= 1+pil_w, l_ni-pil_e
                  trp(i,j,l_nk+2) = trp(i,j,l_nk+1)
               end do
               end do
            endif
!$omp end parallel 

         endif
      end do
*
cstl      else
cstl*
cstl*     NO INTIALIZATION FOR THE TLM AT F_stepno.gt.0 --> F_apply_L=.true.
cstl*
cstl      endif
*
*     __________________________________________________________________
*
      return
      end
