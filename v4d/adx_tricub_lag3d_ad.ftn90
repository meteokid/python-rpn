!-------------------------------------- LICENCE BEGIN -------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer, 
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms 
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer 
!version 3 or (at your option) any later version that should be found at: 
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software; 
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), 
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END ---------------------------
#include "msg.h"

!/**
subroutine adx_tricub_lag3d2_ad()
   print *,'ERROR: called a stub (adx_tricub_lag3d2_ad)'
   call flush()
   stop
end subroutine adx_tricub_lag3d2_ad

!/**
subroutine adx_tricub_lag3d3_ad(F_out  , F_in  , F_x  , F_y  , F_z  , &
                                         F_in_m, F_x_m, F_y_m, F_z_m, &
                                F_mono_L,i0,in,j0,jn, &
                                F_ni,F_nj,F_aminx,F_amaxx,F_aminy,F_amaxy, &
                                F_nk,F_nk_in,F_lev_S)
   implicit none
#include "adx_dims.cdk"
   !@objective ADJ of adx_tricub_lag3d3 
   !@arguments
   character(len=*) :: F_lev_S !I, m/t : Momemtum/thermo level
   integer :: F_nk_in          !I, number of vertical levels (input field)
   integer :: F_ni, F_nj, F_nk !I, dims of output field
   integer :: F_aminx,F_amaxx,F_aminy,F_amaxy !I, F_in array bounds
   integer :: i0,in,j0,jn      !I, scope of operator
   logical :: F_mono_L         !I, .true. monotonic interpolation
   real,dimension(F_ni,F_nj,F_nk) :: &
        F_x  , F_y  , F_z, &   !I, interpolation target x,y,z coordinates
        F_x_m, F_y_m, F_z_m    !I, interpolation target x,y,z coordinates TRAJ
   real,dimension(F_aminx:F_amaxx,F_aminy:F_amaxy,F_nk_in) :: &
        F_in, &                !I, field to interpolate 
        F_in_m                 !I, field to interpolate TRAJ 
   real,dimension(F_ni,F_nj,F_nk) :: &
        F_out                  !O, result of interpolation
   !@author monique tanguay
   !@revisions
   ! v4_XX - Tanguay M.        - Adjustments GEM413
!**/
#include "adx_grid.cdk"
#include "adx_interp.cdk"
#include "adx_interp_m.cdk"
   integer :: i,j,k,iimax,jjmax,kkmax,ii,jj,kk
   integer :: i2, j2, k2
   logical :: zcubic_L
   real    :: prmin, prmax, prmin_m, prmax_m, F_out_m, &
              prmin_1_m,prmin_2_m,prmin_3_m,prmin_4_m,prmin_5_m,prmin_6_m,prmin_7_m,prmin_8_m, &
              prmax_1_m,prmax_2_m,prmax_3_m,prmax_4_m,prmax_5_m,prmax_6_m,prmax_7_m,prmax_8_m
   real*8, dimension(4)  :: pp_8    , aa_8    , bb_8    , cc_8   , dd_8  , &
                            pp_m_8  , aa_m_8  , bb_m_8  , cc_m_8 , dd_m_8, pp_y_m_8, pp_z_m_8, &
                            aa_z_m_8, bb_z_m_8, cc_z_m_8, dd_z_m_8

   integer,dimension(:),pointer :: p_lcz
   real*8  :: p_z00_8
   real*8, dimension(:),pointer :: p_bsz_8, p_zbc_8, p_zabcd_8
   real*8, dimension(:),pointer :: p_zbacd_8, p_zcabd_8, p_zdabc_8

   real*8 :: rri_8  ,rrj_8  ,rrk_8  ,ra_8,rb_8,rc_8,rd_8  
   real*8 :: rri_m_8,rrj_m_8,rrk_m_8
   real*8, parameter :: ZERO_8 = 0.0

!TODO: all intermediate computations should be real*8 (not the present mix)... does not reproduce bit pattern

#define TRIPROD(ZA,ZB,ZC,ZD) ((ZA-ZB)*(ZA-ZC)*(ZA-ZD))
#define TRIPROD_ad(ZA,ZA_M,ZB,ZC,ZD) (ZA*(ZA_M-ZC)*(ZA_M-ZD)+(ZA_M-ZB)*ZA*(ZA_M-ZD)+(ZA_M-ZB)*(ZA_M-ZC)*ZA) 
#define INT_CUB_I2(F3D,AA,BB,CC,DD,II,JJ,KK) (AA*F3D(II-1,JJ,KK) + BB*F3D(II,JJ,KK) + CC*F3D(II+1,JJ,KK) + DD*F3D(II+2,JJ,KK))
#define INT_CUB_I1(F3D,AA,II,JJ,KK) INT_CUB_I2(F3D,AA(1),AA(2),AA(3),AA(4),II,JJ,KK)
#define INT_CUB_I2_48(F3D4,AA8,BB8,CC8,DD8,II,JJ,KK) (AA8*dble(F3D4(II-1,JJ,KK)) + BB8*dble(F3D4(II,JJ,KK)) + CC8*dble(F3D4(II+1,JJ,KK)) +
DD8*dble(F3D4(II+2,JJ,KK)))
#define INT_CUB_I1_48(F3D4,AA8,II,JJ,KK) INT_CUB_I2(F3D4,AA8(1),AA8(2),AA8(3),AA8(4),II,JJ,KK)
   !---------------------------------------------------------------------
   if (F_lev_S == 'm') then
      kkmax   = adx_lnkm - 1
      p_z00_8 = adx_verZ_8%m(1)
      p_lcz     => adx_lcz%m
      p_bsz_8   => adx_bsz_8%m
      p_zabcd_8 => adx_zabcd_8%m
      p_zbacd_8 => adx_zbacd_8%m
      p_zcabd_8 => adx_zcabd_8%m
      p_zdabc_8 => adx_zdabc_8%m
      p_zbc_8   => adx_zbc_8%m
   else
      kkmax   = adx_lnkm
      p_z00_8 = adx_verZ_8%t(1)
      p_lcz     => adx_lcz%t
      p_bsz_8   => adx_bsz_8%t
      p_zabcd_8 => adx_zabcd_8%t
      p_zbacd_8 => adx_zbacd_8%t
      p_zcabd_8 => adx_zcabd_8%t
      p_zdabc_8 => adx_zdabc_8%t
      p_zbc_8   => adx_zbc_8%t
   endif

   iimax = adx_gni+2*adx_halox-2
   jjmax = adx_gnj+adx_haloy


   !Zero adjoint variables
   !----------------------
   if (F_mono_L) then
       prmin = ZERO_8
       prmax = ZERO_8
   endif
   pp_8 = 0. 
   aa_8 = 0. 
   bb_8 = 0. 
   cc_8 = 0. 
   dd_8 = 0. 
   rri_8= 0. 
   rrj_8= 0. 
   rrk_8= 0. 

   do k=F_nk,1,-1
      do j=jn,j0,-1
         do i=in,i0,-1

            !------------------
            !TRAJECTORY (START)
            !------------------

            rri_m_8= F_x_m(i,j,k)
            ii = (rri_m_8 - adx_x00_8) * adx_ovdx_8
            ii = adx_lcx(ii+1) + 1
            if (rri_m_8 < adx_bsx_8(ii)) ii = ii - 1
            ii = max(2,min(ii,iimax))

            rrj_m_8= F_y_m(i,j,k)
            jj = (rrj_m_8 - adx_y00_8) * adx_ovdy_8
            jj = adx_lcy(jj+1) + 1
            if (rrj_m_8 < adx_bsy_8(jj)) jj = jj - 1
            jj = max(adx_haloy,min(jj,jjmax))

            rrk_m_8= F_z_m(i,j,k)
            kk = (rrk_m_8 - p_z00_8) * adx_ovdz_8
            kk = p_lcz(kk+1)
            if (rrk_m_8 < p_bsz_8(kk)) kk = kk - 1
            kk = min(kkmax-1,max(0,kk))

            zcubic_L = (kk > 0) .and. (kk < kkmax-1)

            !- x interpolation
            !- --------------- 

            ra_8 = adx_bsx_8(ii-1)
            rb_8 = adx_bsx_8(ii  )
            rc_8 = adx_bsx_8(ii+1)
            rd_8 = adx_bsx_8(ii+2)

            pp_m_8(1) = TRIPROD(rri_m_8,rb_8,rc_8,rd_8) * adx_xabcd_8(ii)
            pp_m_8(2) = TRIPROD(rri_m_8,ra_8,rc_8,rd_8) * adx_xbacd_8(ii)
            pp_m_8(3) = TRIPROD(rri_m_8,ra_8,rb_8,rd_8) * adx_xcabd_8(ii)
            pp_m_8(4) = TRIPROD(rri_m_8,ra_8,rb_8,rc_8) * adx_xdabc_8(ii)

            !- lmin shift needed since indexes are computed with array starting at 1
            i2 = ii - (adx_li0-1) + (adx_lminx-1)
            j2 = jj - (adx_lj0-1) + (adx_lminy-1)
            k2 = kk

            bb_m_8(1) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2-1,k2+1)
            bb_m_8(2) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2,  k2+1)
            bb_m_8(3) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2+1,k2+1)
            bb_m_8(4) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2+2,k2+1)

            cc_m_8(1) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2-1,k2+2)
            cc_m_8(2) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2,  k2+2)
            cc_m_8(3) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2+1,k2+2)
            cc_m_8(4) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2+2,k2+2)

            if (zcubic_L) then

               aa_m_8(1) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2-1,k2)
               aa_m_8(2) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2,  k2)
               aa_m_8(3) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2+1,k2)
               aa_m_8(4) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2+2,k2)

               dd_m_8(1) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2-1,k2+3)
               dd_m_8(2) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2,  k2+3)
               dd_m_8(3) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2+1,k2+3)
               dd_m_8(4) = INT_CUB_I1(F_in_m,pp_m_8,i2,j2+2,k2+3)

            endif

            !- y interpolation
            !- ---------------

            ra_8 = adx_bsy_8(jj-1)
            rb_8 = adx_bsy_8(jj  )
            rc_8 = adx_bsy_8(jj+1)
            rd_8 = adx_bsy_8(jj+2)

            pp_y_m_8(1) = TRIPROD(rrj_m_8,rb_8,rc_8,rd_8) * adx_yabcd_8(jj)
            pp_y_m_8(2) = TRIPROD(rrj_m_8,ra_8,rc_8,rd_8) * adx_ybacd_8(jj)
            pp_y_m_8(3) = TRIPROD(rrj_m_8,ra_8,rb_8,rd_8) * adx_ycabd_8(jj)
            pp_y_m_8(4) = TRIPROD(rrj_m_8,ra_8,rb_8,rc_8) * adx_ydabc_8(jj)

            bb_z_m_8(1) = pp_y_m_8(1)*bb_m_8(1) + pp_y_m_8(2)*bb_m_8(2) + pp_y_m_8(3)*bb_m_8(3) + pp_y_m_8(4)*bb_m_8(4)
            cc_z_m_8(1) = pp_y_m_8(1)*cc_m_8(1) + pp_y_m_8(2)*cc_m_8(2) + pp_y_m_8(3)*cc_m_8(3) + pp_y_m_8(4)*cc_m_8(4)

            !- z interpolation
            !- --------------- 
            if (zcubic_L) then

               aa_z_m_8(1) = pp_y_m_8(1)*aa_m_8(1) + pp_y_m_8(2)*aa_m_8(2) + pp_y_m_8(3)*aa_m_8(3) + pp_y_m_8(4)*aa_m_8(4)
               dd_z_m_8(1) = pp_y_m_8(1)*dd_m_8(1) + pp_y_m_8(2)*dd_m_8(2) + pp_y_m_8(3)*dd_m_8(3) + pp_y_m_8(4)*dd_m_8(4)

               ra_8 = p_bsz_8(kk-1)
               rb_8 = p_bsz_8(kk  )
               rc_8 = p_bsz_8(kk+1)
               rd_8 = p_bsz_8(kk+2)

               !- TRAJECTORY
               !- ---------- 
               pp_z_m_8(1) = TRIPROD(rrk_m_8,rb_8,rc_8,rd_8) * p_zabcd_8(kk+1)
               pp_z_m_8(2) = TRIPROD(rrk_m_8,ra_8,rc_8,rd_8) * p_zbacd_8(kk+1)
               pp_z_m_8(3) = TRIPROD(rrk_m_8,ra_8,rb_8,rd_8) * p_zcabd_8(kk+1)
               pp_z_m_8(4) = TRIPROD(rrk_m_8,ra_8,rb_8,rc_8) * p_zdabc_8(kk+1)

               F_out_m = pp_z_m_8(1)*aa_z_m_8(1) + pp_z_m_8(2)*bb_z_m_8(1) + pp_z_m_8(3)*cc_z_m_8(1) + pp_z_m_8(4)*dd_z_m_8(1)

            else

               pp_z_m_8(3) = (rrk_m_8-p_bsz_8(kk))*p_zbc_8(kk+1)
               pp_z_m_8(2) = 1. - pp_z_m_8(3)

               F_out_m = pp_z_m_8(2)*bb_z_m_8(1) + pp_z_m_8(3)*cc_z_m_8(1)

            endif

            if (F_mono_L) then

               prmax_m = F_in_m(i2,j2,  k2+1)
               prmax_1_m = prmax_m
               if (F_in_m(i2+1,j2 ,k2+1).gt.prmax_m) then
                  prmax_m = F_in_m(i2+1,j2,  k2+1)
               endif
               prmax_2_m = prmax_m
               if(F_in_m(i2  ,j2+1,k2+1).gt.prmax_m) then
                  prmax_m = F_in_m(i2  ,j2+1,k2+1)
               endif
               prmax_3_m = prmax_m
               if(F_in_m(i2+1,j2+1,k2+1).gt.prmax_m) then
                  prmax_m = F_in_m(i2+1,j2+1,k2+1)
               endif
               prmax_4_m = prmax_m
               if(F_in_m(i2  ,j2  ,k2+2).gt.prmax_m) then
                  prmax_m = F_in_m(i2  ,j2  ,k2+2)
               endif
               prmax_5_m = prmax_m
               if(F_in_m(i2+1,j2  ,k2+2).gt.prmax_m) then
                  prmax_m = F_in_m(i2+1,j2  ,k2+2)
               endif
               prmax_6_m = prmax_m
               if(F_in_m(i2  ,j2+1,k2+2).gt.prmax_m) then
                  prmax_m = F_in_m(i2  ,j2+1,k2+2)
               endif
               prmax_7_m = prmax_m
               if(F_in_m(i2+1,j2+1,k2+2).gt.prmax_m) then
                  prmax_m = F_in_m(i2+1,j2+1,k2+2)
               endif
               prmax_8_m = prmax_m

               prmin_m = F_in_m(i2,j2,  k2+1)
               prmin_1_m = prmin_m
               if(F_in_m(i2+1,j2  ,k2+1).lt.prmin_m) then
                  prmin_m = F_in_m(i2+1,j2  ,k2+1)
               endif
               prmin_2_m = prmin_m
               if(F_in_m(i2  ,j2+1,k2+1).lt.prmin_m) then
                  prmin_m = F_in_m(i2  ,j2+1,k2+1)
               endif
               prmin_3_m = prmin_m
               if(F_in_m(i2+1,j2+1,k2+1).lt.prmin_m) then
                  prmin_m = F_in_m(i2+1,j2+1,k2+1)
               endif
               prmin_4_m = prmin_m
               if(F_in_m(i2  ,j2  ,k2+2).lt.prmin_m) then
                  prmin_m = F_in_m(i2  ,j2  ,k2+2)
               endif
               prmin_5_m = prmin_m
               if(F_in_m(i2+1,j2  ,k2+2).lt.prmin_m) then
                  prmin_m = F_in_m(i2+1,j2  ,k2+2)
               endif
               prmin_6_m = prmin_m
               if(F_in_m(i2  ,j2+1,k2+2).lt.prmin_m) then
                  prmin_m = F_in_m(i2  ,j2+1,k2+2)
               endif
               prmin_7_m = prmin_m
               if(F_in_m(i2+1,j2+1,k2+2).lt.prmin_m) then
                  prmin_m = F_in_m(i2+1,j2+1,k2+2)
               endif
               prmin_8_m = prmin_m

            endif

            !------------------
            !TRAJECTORY (END)
            !------------------

            !--------------------
            !ADJOINT CALCULATIONS
            !--------------------

            if (F_mono_L) then

               if(F_out_m.lt.prmin_8_m) then
                  prmin        = F_out(i,j,k) + prmin 
                  F_out(i,j,k) = ZERO_8
               endif
               if(F_out_m.gt.prmax_8_m) then
                  prmax        = F_out(i,j,k) + prmax 
                  F_out(i,j,k) = ZERO_8
               endif

               if(F_in_m(i2+1,j2+1,k2+2).lt.prmin_7_m) then
                  F_in  (i2+1,j2+1,k2+2) =  prmin + F_in(i2+1,j2+1,k2+2)
                  prmin                  = ZERO_8 
               endif
               if(F_in_m(i2  ,j2+1,k2+2).lt.prmin_6_m) then
                  F_in  (i2  ,j2+1,k2+2) =  prmin + F_in(i2  ,j2+1,k2+2)
                  prmin                  = ZERO_8 
               endif
               if(F_in_m(i2+1,j2  ,k2+2).lt.prmin_5_m) then
                  F_in  (i2+1,j2  ,k2+2) =  prmin + F_in(i2+1,j2  ,k2+2) 
                  prmin                  = ZERO_8 
               endif
               if(F_in_m(i2  ,j2  ,k2+2).lt.prmin_4_m) then
                  F_in  (i2  ,j2  ,k2+2) =  prmin + F_in(i2  ,j2  ,k2+2) 
                  prmin                  = ZERO_8 
               endif
               if(F_in_m(i2+1,j2+1,k2+1).lt.prmin_3_m) then
                  F_in  (i2+1,j2+1,k2+1) =  prmin + F_in(i2+1,j2+1,k2+1) 
                  prmin                  = ZERO_8 
               endif
               if(F_in_m(i2  ,j2+1,k2+1).lt.prmin_2_m) then
                  F_in  (i2  ,j2+1,k2+1) =  prmin + F_in(i2  ,j2+1,k2+1) 
                  prmin                  = ZERO_8 
               endif
               if(F_in_m(i2+1,j2 ,k2+1).lt.prmin_1_m) then
                  F_in  (i2+1,j2 ,k2+1)  =  prmin + F_in(i2+1,j2  ,k2+1) 
                  prmin                  = ZERO_8 
               endif
                  F_in  (i2  ,j2 ,k2+1)  = prmin  + F_in(i2  ,j2  ,k2+1)
                  prmin                  = ZERO_8 


               if(F_in_m(i2+1,j2+1,k2+2).gt.prmax_7_m) then
                  F_in  (i2+1,j2+1,k2+2) =  prmax + F_in(i2+1,j2+1,k2+2)
                  prmax                  = ZERO_8
               endif
               if(F_in_m(i2  ,j2+1,k2+2).gt.prmax_6_m) then
                  F_in  (i2  ,j2+1,k2+2) =  prmax + F_in(i2  ,j2+1,k2+2)
                  prmax                  = ZERO_8
               endif
               if(F_in_m(i2+1,j2  ,k2+2).gt.prmax_5_m) then
                  F_in  (i2+1,j2  ,k2+2) =  prmax + F_in(i2+1,j2  ,k2+2)
                  prmax                  = ZERO_8
               endif
               if(F_in_m(i2  ,j2  ,k2+2).gt.prmax_4_m) then
                  F_in  (i2  ,j2  ,k2+2) =  prmax + F_in(i2  ,j2  ,k2+2)
                  prmax                  = ZERO_8
               endif
               if(F_in_m(i2+1,j2+1,k2+1).gt.prmax_3_m) then
                  F_in  (i2+1,j2+1,k2+1) =  prmax + F_in(i2+1,j2+1,k2+1)
                  prmax                  = ZERO_8
               endif
               if(F_in_m(i2  ,j2+1,k2+1).gt.prmax_2_m) then
                  F_in  (i2  ,j2+1,k2+1) =  prmax + F_in(i2  ,j2+1,k2+1)
                  prmax                  = ZERO_8
               endif
               if (F_in_m(i2+1,j2 ,k2+1).gt.prmax_1_m) then
                  F_in   (i2+1,j2 ,k2+1) =  prmax + F_in(i2+1,j2  ,k2+1)
                  prmax                  = ZERO_8
               endif
                  F_in   (i2  ,j2 ,k2+1) =  prmax + F_in(i2  ,j2  ,k2+1)
                  prmax                  = ZERO_8

            endif
       
            !- ADJ of
            !- z interpolation
            !- --------------- 
            if (zcubic_L) then

               pp_8(1) = F_out(i,j,k) * aa_z_m_8(1) 
               pp_8(2) = F_out(i,j,k) * bb_z_m_8(1) 
               pp_8(3) = F_out(i,j,k) * cc_z_m_8(1) 
               pp_8(4) = F_out(i,j,k) * dd_z_m_8(1) 
               aa_8(1) = pp_z_m_8(1)  * F_out(i,j,k)
               bb_8(1) = pp_z_m_8(2)  * F_out(i,j,k)
               cc_8(1) = pp_z_m_8(3)  * F_out(i,j,k)
               dd_8(1) = pp_z_m_8(4)  * F_out(i,j,k)

               F_out(i,j,k) = ZERO_8

               rrk_8 = TRIPROD_ad(pp_8(4),rrk_m_8,ra_8,rb_8,rc_8)*p_zdabc_8(kk+1)
               rrk_8 = TRIPROD_ad(pp_8(3),rrk_m_8,ra_8,rb_8,rd_8)*p_zcabd_8(kk+1) + rrk_8
               rrk_8 = TRIPROD_ad(pp_8(2),rrk_m_8,ra_8,rc_8,rd_8)*p_zbacd_8(kk+1) + rrk_8
               rrk_8 = TRIPROD_ad(pp_8(1),rrk_m_8,rb_8,rc_8,rd_8)*p_zabcd_8(kk+1) + rrk_8
               pp_8(4) = ZERO_8
               pp_8(3) = ZERO_8
               pp_8(2) = ZERO_8
               pp_8(1) = ZERO_8

               pp_8(1) =     dd_8(1)*dd_m_8(1) + pp_8(1)
               pp_8(2) =     dd_8(1)*dd_m_8(2) + pp_8(2)
               pp_8(3) =     dd_8(1)*dd_m_8(3) + pp_8(3)
               pp_8(4) =     dd_8(1)*dd_m_8(4) + pp_8(4)
               dd_8(2) = pp_y_m_8(2)*dd_8  (1) + dd_8(2)
               dd_8(3) = pp_y_m_8(3)*dd_8  (1) + dd_8(3)
               dd_8(4) = pp_y_m_8(4)*dd_8  (1) + dd_8(4)
               dd_8(1) = pp_y_m_8(1)*dd_8  (1)

               pp_8(1) =     aa_8(1)*aa_m_8(1) + pp_8(1)
               pp_8(2) =     aa_8(1)*aa_m_8(2) + pp_8(2)
               pp_8(3) =     aa_8(1)*aa_m_8(3) + pp_8(3)
               pp_8(4) =     aa_8(1)*aa_m_8(4) + pp_8(4)
               aa_8(2) = pp_y_m_8(2)*aa_8  (1) + aa_8(2)
               aa_8(3) = pp_y_m_8(3)*aa_8  (1) + aa_8(3)
               aa_8(4) = pp_y_m_8(4)*aa_8  (1) + aa_8(4)
               aa_8(1) = pp_y_m_8(1)*aa_8  (1)

            else

               pp_8(2) = F_out(i,j,k) * bb_z_m_8(1) 
               pp_8(3) = F_out(i,j,k) * cc_z_m_8(1) 
               bb_8(1) = pp_z_m_8(2)  * F_out(i,j,k)
               cc_8(1) = pp_z_m_8(3)  * F_out(i,j,k)

               F_out(i,j,k) = ZERO_8

               pp_8(3) = -pp_8(2) + pp_8(3) 
               pp_8(2) = ZERO_8
               rrk_8   =  pp_8(3)*p_zbc_8(kk+1)
               pp_8(3) = ZERO_8

            endif

            !- ADJ of
            !- y interpolation
            !- ---------------

            pp_8(1) =     cc_8(1)*cc_m_8(1) + pp_8(1)
            pp_8(2) =     cc_8(1)*cc_m_8(2) + pp_8(2)
            pp_8(3) =     cc_8(1)*cc_m_8(3) + pp_8(3)
            pp_8(4) =     cc_8(1)*cc_m_8(4) + pp_8(4)
            cc_8(2) = pp_y_m_8(2)*cc_8  (1) + cc_8(2)
            cc_8(3) = pp_y_m_8(3)*cc_8  (1) + cc_8(3)
            cc_8(4) = pp_y_m_8(4)*cc_8  (1) + cc_8(4)
            cc_8(1) = pp_y_m_8(1)*cc_8  (1) 

            pp_8(1) =     bb_8(1)*bb_m_8(1) + pp_8(1)
            pp_8(2) =     bb_8(1)*bb_m_8(2) + pp_8(2)
            pp_8(3) =     bb_8(1)*bb_m_8(3) + pp_8(3)
            pp_8(4) =     bb_8(1)*bb_m_8(4) + pp_8(4)
            bb_8(2) = pp_y_m_8(2)*bb_8  (1) + bb_8(2)
            bb_8(3) = pp_y_m_8(3)*bb_8  (1) + bb_8(3)
            bb_8(4) = pp_y_m_8(4)*bb_8  (1) + bb_8(4)
            bb_8(1) = pp_y_m_8(1)*bb_8  (1) 

            ra_8 = adx_bsy_8(jj-1)
            rb_8 = adx_bsy_8(jj  )
            rc_8 = adx_bsy_8(jj+1)
            rd_8 = adx_bsy_8(jj+2)

            rrj_8 = TRIPROD_ad(pp_8(4),rrj_m_8,ra_8,rb_8,rc_8)*Adx_ydabc_8(jj)
            rrj_8 = TRIPROD_ad(pp_8(3),rrj_m_8,ra_8,rb_8,rd_8)*Adx_ycabd_8(jj) + rrj_8
            rrj_8 = TRIPROD_ad(pp_8(2),rrj_m_8,ra_8,rc_8,rd_8)*Adx_ybacd_8(jj) + rrj_8
            rrj_8 = TRIPROD_ad(pp_8(1),rrj_m_8,rb_8,rc_8,rd_8)*Adx_yabcd_8(jj) + rrj_8
            pp_8(4) = ZERO_8
            pp_8(3) = ZERO_8
            pp_8(2) = ZERO_8
            pp_8(1) = ZERO_8

            !- ADJ of
            !- x interpolation
            !- ---------------

            if (zcubic_L) then

               call INT_CUB_I1_ad (dd_8(4),F_in,pp_8,F_in_m,pp_m_8,i2,j2+2,k2+3,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
               call INT_CUB_I1_ad (dd_8(3),F_in,pp_8,F_in_m,pp_m_8,i2,j2+1,k2+3,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
               call INT_CUB_I1_ad (dd_8(2),F_in,pp_8,F_in_m,pp_m_8,i2,j2,  k2+3,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
               call INT_CUB_I1_ad (dd_8(1),F_in,pp_8,F_in_m,pp_m_8,i2,j2-1,k2+3,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)

               call INT_CUB_I1_ad (aa_8(4),F_in,pp_8,F_in_m,pp_m_8,i2,j2+2,k2,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
               call INT_CUB_I1_ad (aa_8(3),F_in,pp_8,F_in_m,pp_m_8,i2,j2+1,k2,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
               call INT_CUB_I1_ad (aa_8(2),F_in,pp_8,F_in_m,pp_m_8,i2,j2,  k2,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
               call INT_CUB_I1_ad (aa_8(1),F_in,pp_8,F_in_m,pp_m_8,i2,j2-1,k2,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)

            endif


            call INT_CUB_I1_ad (cc_8(4),F_in,pp_8,F_in_m,pp_m_8,i2,j2+2,k2+2,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
            call INT_CUB_I1_ad (cc_8(3),F_in,pp_8,F_in_m,pp_m_8,i2,j2+1,k2+2,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
            call INT_CUB_I1_ad (cc_8(2),F_in,pp_8,F_in_m,pp_m_8,i2,j2,  k2+2,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
            call INT_CUB_I1_ad (cc_8(1),F_in,pp_8,F_in_m,pp_m_8,i2,j2-1,k2+2,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
            

            call INT_CUB_I1_ad (bb_8(4),F_in,pp_8,F_in_m,pp_m_8,i2,j2+2,k2+1,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
            call INT_CUB_I1_ad (bb_8(3),F_in,pp_8,F_in_m,pp_m_8,i2,j2+1,k2+1,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
            call INT_CUB_I1_ad (bb_8(2),F_in,pp_8,F_in_m,pp_m_8,i2,j2,  k2+1,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)
            call INT_CUB_I1_ad (bb_8(1),F_in,pp_8,F_in_m,pp_m_8,i2,j2-1,k2+1,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk)


            ra_8 = adx_bsx_8(ii-1)
            rb_8 = adx_bsx_8(ii  )
            rc_8 = adx_bsx_8(ii+1)
            rd_8 = adx_bsx_8(ii+2)

            rri_8 = TRIPROD_ad(pp_8(4),rri_m_8,ra_8,rb_8,rc_8)*Adx_xdabc_8(ii)
            rri_8 = TRIPROD_ad(pp_8(3),rri_m_8,ra_8,rb_8,rd_8)*Adx_xcabd_8(ii) + rri_8
            rri_8 = TRIPROD_ad(pp_8(2),rri_m_8,ra_8,rc_8,rd_8)*Adx_xbacd_8(ii) + rri_8
            rri_8 = TRIPROD_ad(pp_8(1),rri_m_8,rb_8,rc_8,rd_8)*Adx_xabcd_8(ii) + rri_8
            pp_8(4) = ZERO_8
            pp_8(3) = ZERO_8
            pp_8(2) = ZERO_8
            pp_8(1) = ZERO_8

            F_z(i,j,k) = rrk_8 + F_z(i,j,k)
            F_y(i,j,k) = rrj_8 + F_y(i,j,k)
            F_x(i,j,k) = rri_8 + F_x(i,j,k)

         enddo
      enddo
   enddo

   !---------------------------------------------------------------------
   return
end subroutine adx_tricub_lag3d3_ad


!/**
subroutine INT_CUB_I1_ad (X_8,F3D,AA_8,F3D_m,AA_m_8,II,JJ,KK, &
                          F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk_in)

   implicit none
#include "adx_dims.cdk"
   !@objective ADJ of INT_CUB_I1 
   !@arguments

   integer II,JJ,KK,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk_in
   real,dimension(F_aminx:F_amaxx,F_aminy:F_amaxy,F_nk_in) :: &
        F3D, &                !I, field to interpolate
        F3D_m                 !I, field to interpolate TRAJ
   real*8, dimension(4)  :: AA_8, AA_m_8
   real*8                :: X_8
   !@revisions
   ! v4_XX - Tanguay M.        - Adjustments GEM413
   !**/
   !---------------------------------------------------------------------

   call INT_CUB_I2_ad(X_8,F3D  ,AA_8  (1),AA_8  (2),AA_8  (3),AA_8  (4), &
                          F3D_m,AA_m_8(1),AA_m_8(2),AA_m_8(3),AA_m_8(4),II,JJ,KK, &
                      F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk_in)

   !---------------------------------------------------------------------
   return
end subroutine INT_CUB_I1_ad 


!/**
subroutine INT_CUB_I2_ad (X_8,F3D  ,AA_8  ,BB_8  ,CC_8  ,DD_8  , &
                              F3D_m,AA_m_8,BB_m_8,CC_m_8,DD_m_8,II,JJ,KK, &
                          F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk_in)

   implicit none
#include "adx_dims.cdk"
   !@objective ADJ of INT_CUB_I2
   !@arguments

   integer II,JJ,KK,F_aminx,F_amaxx,F_aminy,F_amaxy,F_nk_in
   real,dimension(F_aminx:F_amaxx,F_aminy:F_amaxy,F_nk_in) :: &
        F3D, &                !I, field to interpolate
        F3D_m                 !I, field to interpolate TRAJ
   real*8  :: AA_8,BB_8,CC_8,DD_8,AA_m_8,BB_m_8,CC_m_8,DD_m_8,X_8 
   !@revisions
   ! v4_XX - Tanguay M.        - Adjustments GEM413
   !**/
   !---------------------------------------------------------------------


   F3D(II-1,JJ,KK) = AA_m_8* X_8 + F3D(II-1,JJ,KK)
   F3D(II  ,JJ,KK) = BB_m_8* X_8 + F3D(II  ,JJ,KK)
   F3D(II+1,JJ,KK) = CC_m_8* X_8 + F3D(II+1,JJ,KK)
   F3D(II+2,JJ,KK) = DD_m_8* X_8 + F3D(II+2,JJ,KK)

   AA_8 = X_8*F3D_m(II-1,JJ,KK) + AA_8
   BB_8 = X_8*F3D_m(II  ,JJ,KK) + BB_8
   CC_8 = X_8*F3D_m(II+1,JJ,KK) + CC_8
   DD_8 = X_8*F3D_m(II+2,JJ,KK) + DD_8  

   X_8 = 0.

   !---------------------------------------------------------------------
   return
end subroutine INT_CUB_I2_ad
