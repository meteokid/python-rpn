!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer, 
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms 
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer 
!version 3 or (at your option) any later version that should be found at: 
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software; 
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), 
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
***s/r hzd_hof_ad - ADJ of hzd_hof_tl 
*
#include "model_macros_f.h"
*
      subroutine hzd_hof_ad ( F_u   , F_v  , F_psd , F_tp , F_ip ,
     %                        F_td  , F_t  , F_it  , F_plt,   
     %                        F_pip , F_w  , F_qp  , F_mu , F_mul,  
     %                        F_pipm, F_qpm, DIST_DIM,Nk)
*
      implicit none
*
      integer DIST_DIM, Nk
*
      real   F_u  (DIST_SHAPE,Nk), F_v (DIST_SHAPE,Nk),
     %       F_psd(DIST_SHAPE,Nk), F_tp(DIST_SHAPE,Nk),
     %       F_ip (DIST_SHAPE,Nk), F_td(DIST_SHAPE,Nk),
     %       F_t  (DIST_SHAPE,Nk), F_it(DIST_SHAPE,Nk),
     %       F_plt(DIST_SHAPE,Nk),  
     %       F_pip(DIST_SHAPE,Nk), F_w (DIST_SHAPE,Nk),
     %       F_qp (DIST_SHAPE,Nk), F_mu(DIST_SHAPE,Nk),
     %       F_mul(DIST_SHAPE,Nk)
*
      real  F_pipm(DIST_SHAPE,Nk), F_qpm(DIST_SHAPE,Nk)
*
*author
*     M.Tanguay
*
*revision
* v2_21 - Tanguay M.        - initial MPI version
* v2_31 - Tanguay M.        - ADJ of control for diffusion on momentum only
* v3_03 - Tanguay M.        - Adjoint NoHyd configuration
* v3_11 - Tanguay M.        - AIXport+Opti+OpenMP for TLM-ADJ
*                           - Remove F_xfis 
* v3_20 - Tanguay M.        - Introduce Hzd_hzdmain_n_L 
*
*object
*     see id section
*
*arguments
* 
*implicits
#include "fft.cdk"
#include "glb_ld.cdk"
#include "ldnh.cdk"
#include "dcst.cdk"
#include "cstv.cdk"
#include "trp.cdk"
#include "hzd.cdk"
#include "opr.cdk"
#include "schm.cdk"
#include "geomg.cdk"
#include "lun.cdk"
#include "ptopo.cdk"
*
      integer i, j, k, dpwr
      real*8 wk1_8
      real   wk2(LDIST_SHAPE,Nk), wk2_m(LDIST_SHAPE,Nk), wk2x_m(LDIST_SHAPE,Nk)
*
      real*8 ZERO_8
      parameter (ZERO_8=0.0)
*
*     ______________________________________________________
*
      dpwr = Hzd_pwr / 2
      if (Lun_debug_L) write(Lun_out,1000) Hzd_pwr
*
*     Zero adjoint variables
*     ----------------------
      wk1_8 = ZERO_8
*
!$omp parallel do
      do k=1,l_nk
      do j=l_miny,l_maxy
      do i=l_minx,l_maxx
      wk2(i,j,k) = ZERO_8
      enddo
      enddo
      enddo
!$omp end parallel do
*
      if ( Hzd_cdiff .gt. 0.0 ) then
*
      if ( Hzd_uvwdt_L ) goto 9988
*
*     ----------------
*     START TRAJECTORY
*     ----------------
*
*     Save pi' for the diffusion of q'
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*
         if ( (.not.Schm_hydro_L) .and. Schm_difqp_L ) then
*
         if(Hzd_hzdmain_0_L) then
*
!$omp parallel do 
            do k = 1, Nk
            do j = 1,l_nj
            do i = 1,l_ni
*
               wk2_m (i,j,k) = F_pipm(i,j,k)
               wk2x_m(i,j,k) = wk2_m (i,j,k) 
*
            end do
            end do
            end do
!$omp end parallel do
*
*     The hydrostatic pressure: pi'
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*
         call hzd_solfft(F_pipm, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
         endif
*
         endif
*
*****************************
*  3. Nonyhydrostatic model *
*****************************
*
         if ( (.not.Schm_hydro_L) .and. Schm_difqp_L ) then
*
*        Indirect diffusion of q'
*        ~~~~~~~~~~~~~~~~~~~~~~~~
*
         if(Hzd_hzdmain_0_L) then
*
!$omp parallel do 
         do k=1, Nk  !
         do j=1,l_nj ! p = (pi)exp(q')
         do i=1,l_ni ! ~~~~~~~~~~~~~~~
*
            wk2_m(i,j,k) = ( Geomg_z_8(k) + dble(wk2x_m(i,j,k)) )
     %                       *exp( dble(F_qpm(i,j,k)) )
*
         end do
         end do
         end do
!$omp end parallel do
*
         endif
*
*
         if(Hzd_hzdmain_0_L) then
         call hzd_solfft(wk2_m, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
         endif
*
         endif
*
*     --------------
*     END TRAJECTORY
*     --------------
*
*****************************
*  ADJ of                   *
*  3. Nonyhydrostatic model * 
*****************************
*
         if ( (.not.Schm_hydro_L) .and. Schm_difqp_L ) then
*
*        START ADJOINT CALCULATIONS
*        --------------------------
!$omp parallel do
         do k=1, Nk  !
         do j=1,l_nj ! q' = log(p/pi)
         do i=1,l_ni ! ~~~~~~~~~~~~~~
*
            wk2   (i,j,k) = ( dble(F_qp (i,j,k))*( Geomg_z_8(k) + dble(F_pipm(i,j,k))) )
     %                     /( dble(wk2_m(i,j,k))*( Geomg_z_8(k) + dble(F_pipm(i,j,k))) ) + wk2   (i,j,k)
            F_pip (i,j,k) = (-dble(wk2_m(i,j,k))*(                dble(F_qp  (i,j,k))) )
     %                     /( dble(wk2_m(i,j,k))*( Geomg_z_8(k) + dble(F_pipm(i,j,k))) ) + F_pip (i,j,k)
            F_qp  (i,j,k) = ZERO_8
*
         end do
         end do
         end do
!$omp end parallel do
*
         call hzd_solfft_ad(wk2, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
*        Indirect diffusion of q'
*        ~~~~~~~~~~~~~~~~~~~~~~~~
!$omp parallel do
         do k=1, Nk  !
         do j=1,l_nj ! p = (pi)exp(q')
         do i=1,l_ni ! ~~~~~~~~~~~~~~~
*
            F_qp(i,j,k) = ( Geomg_z_8(k) + dble(wk2x_m (i,j,k)) )
     %                       *exp( dble(F_qpm(i,j,k)) ) * dble(wk2(i,j,k)) + F_qp(i,j,k)
*
            wk2 (i,j,k) = (                dble(wk2   (i,j,k)) )
     %                       *exp( dble(F_qpm(i,j,k)) )
*
         end do
         end do
         end do
!$omp end parallel do
*
         call hzd_solfft_ad(F_mul, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
         call hzd_solfft_ad(F_mu, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
*
*        END ADJOINT CALCULATIONS
*        ------------------------
         endif
*
         if ( .not. Schm_hydro_L ) then
*
*     ADJ of
*     Vertical wind (physical)
*     ~~~~~~~~~~~~~~~~~~~~~~~~
*
         call hzd_solfft_ad(F_w, wk1_8, Fft_pri_8 ,
     %                   Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                   trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                   trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                   l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                   trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                   Ptopo_npex,Ptopo_npey)
         endif
*
*     ADJ of
*     The hydrostatic pressure: pi'
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         call hzd_solfft_ad(F_pip, wk1_8, Fft_pri_8 ,
     %                      Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                      trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                      trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                      l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                      trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                      Ptopo_npex,Ptopo_npey)
*
!$omp parallel
*
*     ADJ of
*     Save pi' for the diffusion of q'
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         if ( (.not.Schm_hydro_L) .and. Schm_difqp_L ) then
!$omp do 
            do k = 1, Nk
            do j = 1,l_nj
            do i = 1,l_ni
*
               F_pip(i,j,k) = wk2(i,j,k) + F_pip(i,j,k)
                wk2 (i,j,k) = ZERO_8
*
            end do
            end do
            end do
!$omp end do
         endif
*
*     ADJ of
*     The temperature: T', T'lin & hence T
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
!$omp do
         do k=1, Nk
         do j=1,l_nj
         do i=1,l_ni
            F_tp(i,j,k) = F_t(i,j,k) + F_tp(i,j,k)
            F_t (i,j,k) = ZERO_8
         end do
         end do
         end do
!$omp end do
*
!$omp end parallel 
*
         call hzd_solfft_ad(F_plt, wk1_8, Fft_pri_8 ,
     %                      Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                      trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                      trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                      l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                      trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                      Ptopo_npex,Ptopo_npey)
*
         call hzd_solfft_ad(F_tp, wk1_8, Fft_pri_8 ,
     %                      Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                      trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                      trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                      l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                      trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                      Ptopo_npex,Ptopo_npey)
*
*     ADJ of
* updating phi' at the top
*
!$omp parallel 
*
!$omp do 
         do j = 1,l_nj
         do i = 1,l_ni
            F_it(i,j,1) = F_ip(i,j,1) + F_it(i,j,1)
            F_ip(i,j,1) = ZERO_8
         end do
         end do
!$omp end do 
*
*     ADJ of
*     Mass related fields
*     ~~~~~~~~~~~~~~~~~~~
!$omp do 
         do k = 2, Nk
         do j = 1,l_nj
         do i = 1,l_ni
            F_ip(i,j,k) = F_it(i,j,k) + F_ip(i,j,k)
            F_it(i,j,k) = ZERO_8
         end do
         end do
         end do
!$omp end do 
*
!$omp end parallel 
*
         call hzd_solfft_ad(F_it, wk1_8, Fft_pri_8,
     %                      Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                      trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                      trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                      l_maxx,l_miny,l_maxy,Nk,G_ni,l_ni,l_nj,
     %                      trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                      Ptopo_npex,Ptopo_npey)
*
         call hzd_solfft_ad(F_ip, wk1_8, Fft_pri_8,
     %                      Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                      trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                      trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                      l_maxx,l_miny,l_maxy,Nk,G_ni,l_ni,l_nj,
     %                      trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                      Ptopo_npex,Ptopo_npey)
*
 9988 continue
*
*     ADJ of
*     Total divergence
*     ~~~~~~~~~~~~~~~~
         call hzd_solfft_ad(F_td, wk1_8, Fft_pri_8 ,
     %                      Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                      trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                      trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                      l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                      trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                      Ptopo_npex,Ptopo_npey)
*
*     ADJ of
*     Vertical motion in pressure coord.
*     ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
         call hzd_solfft_ad(F_psd, wk1_8, Fft_pri_8 ,
     %                      Hzd_as_8,Hzd_cs_8,Hzd_deltas_8,
     %                      trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                      trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                      l_maxx, l_miny, l_maxy,Nk,G_ni,l_ni,l_nj,
     %                      trp_12dn,Opr_opsxp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                      Ptopo_npex,Ptopo_npey)
*
*     ADJ of
*     Momentum
*     ~~~~~~~~
         call hzd_solfft_ad(F_v, wk1_8, Fft_pri_8 ,
     %                      Hzd_av_8,Hzd_cv_8,Hzd_deltav_8,
     %                      trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                      trp_12dn,trp_22n,G_nj, dpwr,l_minx,
     %                      l_maxx,l_miny,l_maxy,Nk,G_ni,l_ni,l_nj,
     %                      trp_12dn,Opr_opsxp0_8,Hzd_yp0_8, Hzd_cdiff,
     %                      Ptopo_npex,Ptopo_npey)
*
         call hzd_solfft_ad(F_u, wk1_8, Fft_pri_8, 
     %                      Hzd_au_8,Hzd_cu_8,Hzd_deltau_8,
     %                      trp_12dmin,trp_12dmax,trp_22min,trp_22max,
     %                      trp_12dn,trp_22n,G_nj, dpwr,l_minx, 
     %                      l_maxx,l_miny,l_maxy,Nk,G_ni,l_ni,l_nj,
     %                      trp_12dn,Hzd_xp0_8,Opr_opsyp0_8, Hzd_cdiff,
     %                      Ptopo_npex,Ptopo_npey)
*
      endif
*
 1000 format(/,
     $  3X,'ADJ of PERFORM DEL-',i1,' FFT HORIZONTAL DIFFUSION: (S/R HZD_HOF_AD)')
*     __________________________________________________________________
*
      return
      end
