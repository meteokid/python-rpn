! ------------------------------------------------------------------------------------------------------------------------------
! EC-RPN COMM Group License/Disclaimer, version 2, Last Modified: Jan 08th, 2008 , Environment Canada
! 
! This is free software, you can use/redistribute/modify it under the terms of the EC-RPN COMM Group License/Disclaimer version 2 
! or (at your option) any later version that should be found at http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
! 
! This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the EC-RPN COMM Group License/Disclaimer for more details.
! 
! You should have received a copy of the EC-RPN COMM Group License/Disclaimer along with this software; if not, write to the
! EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), CANADA, H9P 1J3 ; or send e-mail to service.rpn@ec.gc.ca
! ------------------------------------------------------------------------------------------------------------------------------
***s/r diag_var_tl - TLM of diag_var 
*
#include "model_macros_f.h"
*
      subroutine diag_var_tl (F_BsPq,  F_mu,  F_s,  F_q,
     %                        F_BsPq_m,F_mu_m,F_s_m,F_q_m,
     %                        i0,in,j0,jn,nk)
*     
      implicit none
*     
      integer nk
      real F_BsPq(LDIST_SHAPE,0:Nk+1), F_mu(LDIST_SHAPE,Nk+1)
      real F_s(LDIST_SHAPE),           F_q(LDIST_SHAPE,0:Nk+1)
*
      real F_BsPq_m(LDIST_SHAPE,0:Nk+1), F_mu_m(LDIST_SHAPE,Nk+1)
      real F_s_m(LDIST_SHAPE),           F_q_m(LDIST_SHAPE,0:Nk+1)
*
*author
*     M.Tanguay
*
*revision
* v4_04 - Tanguay M.        - initial MPI version
* v4_04 - Tanguay M.        - Staggered version TL/AD 
*
*object
***********************************************************************
* See GEMergo4.doc
***********************************************************************
*
*arguments
*  Name        I/O                 Description
*----------------------------------------------------------------
* F_BsPq       O    - perturbation ln(pressure)
* F_mu         O    - 
* F_s          I    - 
* F_q          I    - non-hydro perturbation of log(p)
* i0,in,j0,jn  I    - index over which computation will be made.
*----------------------------------------------------------------
*
*implicits
#include "lun.cdk"
#include "dcst.cdk"
#include "glb_ld.cdk"
#include "geomg.cdk"
#include "cstv.cdk"
#include "type.cdk"
#include "ver.cdk"
#include "schm.cdk"
*     
**
      integer i,j,k,i0,in,j0,jn,nij
      real*8 ONE_8, HALF_8, xxx_8
      parameter(ONE_8=1.d0, HALF_8=.5d0 )
      real*8, dimension(i0:in,j0:jn):: xrec_8, yrec_8
      real*8, dimension(i0:in,j0:jn):: xrec_m_8, yrec_m_8
*
*     ---------------------------------------------------------------
*
      if(
     $     i0.lt.1-G_halox   .or.
     $     in.gt.l_ni+G_halox.or.
     $     j0.lt.1-G_haloy   .or.
     $     jn.gt.l_nj+G_haloy
     $     )then
         call gem_stop('diag_var_TL : dimension error',-1)
      endif
*
      nij = (in - i0 + 1)*(jn - j0 + 1)
*
!$omp parallel private (xxx_8,xrec_8,yrec_8,xrec_m_8,yrec_m_8)
*
!$omp do
       do k=0,l_nk+1
         if(k.eq.0) then
            xxx_8=0.d0
         else
            xxx_8=Ver_b_8%m(k)
         endif
         do j= j0, jn
         do i= i0, in
*
*           TRAJECTORY
*           ----------
            F_BsPq_m(i,j,k)=xxx_8*F_s_m(i,j)
*
*           TLM
*           ---
            F_BsPq(i,j,k)=xxx_8*F_s(i,j)
*
         enddo
         enddo
      enddo
!$omp enddo
*
      if (.not. Schm_hydro_L) then
!$omp do
      do k=0,l_nk+1
         do j= j0, jn
         do i= i0, in
*
*           TRAJECTORY
*           ----------
            F_BsPq_m(i,j,k) = F_BsPq_m(i,j,k) +  F_q_m(i,j,k)
*
*           TLM
*           ---
            F_BsPq(i,j,k) = F_BsPq(i,j,k) +  F_q(i,j,k)
*
         enddo
         enddo
         if(k.gt.0) then
            xxx_8=Ver_idz_8%t(k)
            do j= j0, jn
            do i= i0, in
*
*              TRAJECTORY
*              ----------
               xrec_m_8(i,j) =ONE_8+ver_dbdz_8%t(k)*F_s_m(i,j)
*
*              TLM
*              ---
               xrec_8(i,j) =ver_dbdz_8%t(k)*F_s(i,j)
*
            enddo
            enddo
*
*           TRAJECTORY
*           ----------
            call vrec ( yrec_m_8, xrec_m_8, nij )
*
            do j= j0, jn
            do i= i0, in
*
*              TLM
*              ---
               yrec_8(i,j) = -xrec_8(i,j)/xrec_m_8(i,j)**2
*
            enddo
            enddo
*
            do j= j0, jn
            do i= i0, in
*
*              TRAJECTORY
*              ----------
               F_mu_m(i,j,k)= xxx_8*(F_q_m(i,j,k)-F_q_m(i,j,k-1))*yrec_m_8(i,j)
*
*              TLM
*              ---
               F_mu(i,j,k)= xxx_8*(F_q_m(i,j,k)-F_q_m(i,j,k-1))*yrec_8  (i,j) + 
     $                      xxx_8*(F_q  (i,j,k)-F_q  (i,j,k-1))*yrec_m_8(i,j)
*
            enddo
            enddo
         endif
      enddo
!$omp enddo
      endif
*      
!$omp end parallel
*
*     ---------------------------------------------------------------
*
      return
      end
