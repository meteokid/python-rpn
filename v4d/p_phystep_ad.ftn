***s/r p_phystep_ad - Apply the adjoint simplified  physical processes: CMC/RPN package
*
#include <model_macros_f.h>
*
      subroutine p_phystep_ad (F_stepno)
      implicit none
*
      integer F_stepno
*
*author
*     Stephane Laroche        Janvier 2001
*
*revision
* v3_00 - Laroche S.       - initial MPI version
* v3_02 - Laroche S.       - allows physic variables outputs
* v3_11 - Laroche S.       - AIXport+Opti+OpenMP for TLM-ADJ
*
*object
*       Computes the physical processes and apply the resulting
*       tendencies to the dynamic variables of the model:
*       CMC/RPN package
*
*arguments
*  Name        I/O                 Description
*----------------------------------------------------------------
* F_stepno      O           step number
*----------------------------------------------------------------
*
*implicits
#include "glb_ld.cdk"
#include "lun.cdk"
#include "mem.cdk"
#include "macro.cdk"
#include "ptopo.cdk"
#include "busind.cdk"
#include "dimout.cdk"
#include "v4dg.cdk"
#include "p4d_simp.cdk"
#include "p_bus.cdk"
*
*modules
      integer  open_db_file,rewind_db_file,close_db_file
      external open_db_file,rewind_db_file,close_db_file
**
      integer dim,err,i,j,k,obussiz
      real, dimension(:,:), allocatable :: obusval
      real*8, parameter :: ZERO_8 = 0.0
      real, dimension(:), allocatable :: up,vp,wp,tp,qp,trp,
     $                                   um,vm,tm,trm,lnpsm,
     $                                   sig,kmm,ktm,
     $                                   upm,vpm,tpm,qpm,trpm,
     $                                   umm,vmm,tmm,    trmm
*
*     ---------------------------------------------------------------
*
      call iniobus(obussiz)
      allocate(obusval(l_ni*l_nj,obussiz))
      dim = (l_maxx-l_minx+1)*(l_maxy-l_miny+1)*l_nk
      allocate (up(dim),vp(dim),wp(dim),tp(dim),qp(dim),
     $          um(dim),vm(dim),tm(dim),lnpsm(dim),
     $          sig(dim),kmm(dim),ktm(dim),
     $          trp(dim*h2o_ntr),trm(dim*h2o_ntr),
     $          upm(dim),vpm(dim),tpm(dim),qpm(dim),
     $          umm(dim),vmm(dim),tmm(dim),
     $          trpm(dim),trmm(dim))
*
*       Read TRAJ for the simplified physics
*       -------------------------------------
        call v4d_rwtraj_sigma  (sig,LDIST_DIM,l_nk)
        call v4d_rwtraj_phystep(upm,vpm,tpm,qpm,trpm,
     $                          umm,vmm,tmm,    trmm,
     $                          LDIST_DIM,l_nk)
        if( P4d_pbl.eq.2) call v4d_rwtraj_kmkt(kmm,ktm,LDIST_DIM,l_nk)
*
*
*     Set adjoint variables to zero
*     -----------------------------
*
      do i=1,dim
          up(i) = ZERO_8
          vp(i) = ZERO_8
          wp(i) = ZERO_8
          tp(i) = ZERO_8
          qp(i) = ZERO_8
          um(i) = ZERO_8
          vm(i) = ZERO_8
          tm(i) = ZERO_8
       lnpsm(i) = ZERO_8
      end do
      do i=1,dim*h2o_ntr
         trm(i) = ZERO_8
         trp(i) = ZERO_8
      end do
*
*
*C   6.	Apply physics tendencies to the appropriate dynamic fields
*C      and insure consistency with other dynamic variables
*       ----------------------------------------------------------
*
      
      call p_apply_ad ( up, vp, um, vm, tm, trm, tp, qp,
     $                  tpm, trpm, qpm,
     $                  LDIST_DIM, l_nk)
*
*      
*
*C       4.	compute physics tendencies
*		--------------------------
*	initialize the number of slices previously done
*
      Mem_pslic = 0
*
      if (.not.Mem_phyncore_L) then
         err = open_db_file   (Lun_waphy)
         err = rewind_db_file (Lun_waphy)
      endif
*
*
!$omp parallel
!$omp do
      do j=1,Ptopo_npeOpenMP
      call p_physlb_ad ( j, F_stepno,obusval,
     $                   up, vp, wp, tp, qp,  trp,
     $                   um, vm,     tm,      trm,
     $                   upm, vpm,  tpm, qpm, trpm,
     $                   umm, vmm,  tmm,      trmm,
     $                   lnpsm, sig, kmm, ktm, LDIST_DIM, l_nk)
      enddo
!$omp enddo
!$omp end parallel
*
*
*C       2.	load all fields required by the physic in memory
*		------------------------------------------------
*
      call out_phy(obusval,l_ni,l_nj,F_stepno)
      deallocate(obusval)
      call p_vmmphy_ad ( up, vp, wp, tp, qp, trp,
     $                   um, vm,     tm,     trm,
     $                   lnpsm, sig, LDIST_DIM,l_nk )
*
*
      if (.not.Mem_phyncore_L) err = close_db_file (Lun_waphy)
*
      deallocate (up,vp,wp,tp,qp,trp,um,vm,tm,trm,lnpsm,sig,kmm,ktm,
     $            upm,vpm,tpm,qpm,trpm,umm,vmm,tmm,trmm)
*
*
*     ---------------------------------------------------------------
*
      return
      end
